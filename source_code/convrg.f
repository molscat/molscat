      SUBROUTINE CONVRG(J,SR,SI,SROLD,SIOLD,ICON,DRCON,ICONVU,NOPSQ,
     1                  IPRINT)
C  Copyright (C) 2020 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  SUBROUTINE TO ASSIST IN THE ESTIMATION OF CONVERGENCE ERRORS
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      SAVE
      DIMENSION SR(1),SI(1),SROLD(1),SIOLD(1)
C
C  COMMON BLOCK FOR CONTROL OF USE OF PROPAGATION SCRATCH FILE
      LOGICAL IREAD,IWRITE
      COMMON /PRPSCR/ ESHIFT,ISCRU,IREAD,IWRITE
C
C  COMMON BLOCK FOR CONTROL OF PROPAGATION SEGMENTS
      COMMON /RADIAL/ RMNINT,RMXINT,RMID,RMATCH,DRS,DRL,STEPS,STEPL,
     1                POWRS,POWRL,TOLHIS,TOLHIL,CAYS,CAYL,unset,
     2                IPROPS,IPROPL,NSEG

      IF (J.GT.1) GOTO 200

      ISCRU=0

      RMIN=RMNINT
      RMAX=RMXINT

      IF (IPRINT.GE.1) THEN
        IF (ICON.EQ.1) THEN
          IF (STEPS.GT.0.D0) THEN
            WRITE(6,500) 'STEPS','HALVE'
          ELSE
            WRITE(6,500) 'DRS','DOUBLE'
          ENDIF
          IF (NSEG.GT.1) THEN
            IF (STEPL.GT.0.D0) THEN
              WRITE(6,500) 'STEPL','HALVE'
            ELSE
              WRITE(6,500) 'DRL','DOUBLE'
            ENDIF
          ENDIF
        ELSEIF (ICON.EQ.2) THEN
          WRITE(6,510) 'RMAX','DECREASE',DRCON
        ELSEIF (ICON.EQ.3) THEN
          WRITE(6,510) 'RMIN','INCREASE',DRCON
        ENDIF
  500   FORMAT(/'  CONVERGENCE TESTS: ',A,' WILL ',A,' EACH TIME')
  510   FORMAT(/'  CONVERGENCE TESTS: ',A,' WILL ',A,' BY ',1P,G11.4,
     1         ' EACH TIME')
      ENDIF

      IF (ICONVU.LT.0) GOTO 200

      NSQOLD=NOPSQ
      DO 100 I=1,NOPSQ
        SROLD(I)=SR(I)
  100   SIOLD(I)=SI(I)
      IF (ICONVU.EQ.0) GOTO 300

      REWIND ICONVU
      WRITE(ICONVU) NOPSQ
      WRITE(ICONVU) (SROLD(I),I=1,NOPSQ)
      WRITE(ICONVU) (SIOLD(I),I=1,NOPSQ)
      GOTO 300
C
  200 JCONVU=ABS(ICONVU)
      IF (ICONVU.EQ.0) GOTO 300

      REWIND(JCONVU)
      READ(JCONVU) NSQOLD
      READ(JCONVU) (SROLD(I),I=1,NSQOLD)
      READ(JCONVU) (SIOLD(I),I=1,NSQOLD)

      IF (IPRINT.GE.1) WRITE(6,601) JCONVU
  601 FORMAT(/'  CONVERGENCE TESTS: REFERENCE S-MATRIX READ IN FROM ',
     1       'CHANNEL',I3)
  300 IF (NOPSQ.NE.NSQOLD) GOTO 600

      IF (J.GT.1 .OR. ICONVU.LT.0) THEN
        ERRSM=0.D0
        ERRTP=0.D0
        DO 400 I=1,NOPSQ
          DIF = (SR(I)-SROLD(I))**2 + (SI(I)-SIOLD(I))**2
          ERRSM=ERRSM+DIF
          DIF = (SR(I)**2 - SROLD(I)**2 + SI(I)**2 - SIOLD(I)**2)**2
          ERRTP=ERRTP+DIF
  400   CONTINUE
C
        ERRSM=SQRT(ERRSM/DBLE(NOPSQ))
        ERRTP=SQRT(ERRTP/DBLE(NOPSQ))
        XSM=LOG10(MAX(ERRSM,1.D-30))
        XTP=LOG10(MAX(ERRTP,1.D-30))
        IF (NSEG.EQ.1) THEN
          IF (STEPS.LT.0.D0) THEN
            WRITE(6,602) RMIN,RMAX,'DR',DRS
          ELSE
            WRITE(6,602) RMIN,RMAX,'STEPS',STEPS
          ENDIF
  602     FORMAT(/'  FOR RMIN =',F7.2,' RMAX =',F11.2,
     1           ' ',A,' =',F8.4)
        ELSE
C
C  THIS DOES NOT COVER THE FULL SET OF POSSIBILITIES, BUT IS GOOD ENOUGH
          IF (STEPS.LT.0.D0 .AND. STEPL.LT.0.D0) THEN
            IF (DRL.NE.unset) THEN
              WRITE(6,603) RMNINT,DRS,RMID,DRL,RMXINT
  603         FORMAT(/'  FOR RMIN =',F7.2,' DRS =',F8.4,' RMID =',F7.2,
     1                ' DRL =',F8.4,' RMAX =',F11.2)
            ELSE
              WRITE(6,604) RMNINT,RMID,RMXINT,DRS
  604         FORMAT(/'  FOR RMIN =',F7.2,' RMID =',F7.2,
     1                ' RMAX =',F11.2,' DR =',F8.4)
            ENDIF
          ELSEIF (STEPS.GT.0.D0 .AND. STEPL.GT.0.D0) THEN
            WRITE(6,605) RMNINT,STEPS,RMID,STEPS,RMXINT
  605       FORMAT(/'  FOR RMIN =',F7.2,' STEPS =',F7.2,' RMID =',F7.2,
     1              ' STEPL =',F7.2,' RMAX =',F11.2)
          ENDIF
        ENDIF

        WRITE(6,610) ERRSM,XSM,ERRTP,XTP
  610   FORMAT('  RMS CHANGE IN S-MATRIX ELEMENTS IS ',7X,
     3         1P,E12.5,5X,'LOG IS',0P,F8.3/
     4         '  RMS CHANGE IN TRANSITION PROBABILITIES IS ',
     5         1P,E12.5,5X,'LOG IS',0P,F8.3)
      ENDIF
C
C  SET THE PROPAGATION LIMITS IN DRIVE TO THE NEW VALUES
      IF (ICON.EQ.1 .AND. STEPS.LE.0.D0) THEN
        IF (DRS.NE.unset) DRS=DRS*2.D0
      ELSEIF (ICON.EQ.1 .AND. STEPS.GT.0.D0) THEN
        STEPS=STEPS/2.D0
      ENDIF
      IF (ICON.EQ.1 .AND. STEPL.LE.0.D0) THEN
        IF (DRL.NE.unset .AND. DRL.NE.-1.D0) DRL=DRL*2.D0
      ELSEIF (ICON.EQ.1 .AND. STEPL.GT.0.D0) THEN
        STEPL=STEPL/2.D0
      ENDIF
      IF (ICON.EQ.2) RMXINT=RMXINT-DRCON
      IF (ICON.EQ.3) RMNINT=RMNINT+DRCON
      RETURN
C
  600 WRITE(6,620) NOPSQ,NSQOLD
  620 FORMAT(/'  *** ERROR IN CONVRG - NUMBER OF OPEN CHANNELS HAS ',
     1       ' CHANGED'/5X,'NOPSQ =',I5,6X,'NSQOLD =',I5)
      RETURN
      END
