      SUBROUTINE SETBAS
C  Copyright (C) 2025 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE basis_data, ONLY: ELEVEL, IDENT, ISYM, J1MAX, J1MIN, J1STEP,
     b                      J2MAX, J2MIN, J2STEP, JLEVEL, JMAX, JMIN,
     b                      JSTEP, KMAX, NLEVEL, ROTI
      USE pair_state, ONLY : JSTATE
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER IP(4)
      SAVE
C
C  SUBROUTINE TO GENERATE LEVEL LISTS AND MONOMER ENERGIES
C  FOR ITYPE = 1, 2, 3, 5
C
C  CHANGED JUN 2008 TO HANDLE NH3 SYMMETRY AND INVERSION SPLITTINGS:
C        ISYM(4) = 0 FOR BOSONS, 1 FOR FERMIONS
C        ISYM(3) = 1 TO RESTRICT K TO 1, 2, 4, 5, 7...
C        ISYM(3) = 3 TO RESTRICT K TO 0, 3, 6, 9, 12...
C        TERM FOR INVERSION SYMMETRY ADDED TO MONOMER ENERGIES
C        WITH ROTI(11) USED FOR TUNNELLING SPLITTING
C        NOTE THAT JSTATE(2*NSTATE+1)=1 IS
C                      |+> TUNNELLING STATE FOR NH3
C                      |-> TUNNELLING STATE FOR ND3
C
C  CRLS 06-2022: ALL ENTRY POINTS CHANGED TO USE ALLOCATABLE ARRAY FOR JSTATE
C                JSTATE IS CARRIED IN THE pair_state MODULE SO THAT ITS
C                SIZE CAN BE SET IN THESE LOWER ROUTINES
C
      LOGICAL LEVIN,EIN
      INTEGER NSTATE
C --------------------------------------------------------- SETBAS ABOVE
      ENTRY SET1(LEVIN,EIN,NSTATE,IPRINT)

      BE=ROTI(1)
      ALPHAE=ROTI(3)
      DE=ROTI(5)
      IF (LEVIN) GOTO 1902

      IF (IPRINT.GE.1) WRITE(6,601) JMIN,JMAX,JSTEP
  601 FORMAT(/'  MOLECULAR QUANTUM NUMBERS OBTAINED FROM JMIN =',
     1       I3,',  JMAX =',I3,',  AND JSTEP =',I2)
      JMIN=MAX(0,JMIN)
      JMAX=MAX(JMIN,JMAX)
      NLEVEL=0
      DO I=JMIN,JMAX,JSTEP
        NLEVEL=NLEVEL+1
        JLEVEL(NLEVEL)=I
      ENDDO
      GOTO 1802

 1902 IF (IPRINT.GE.1) WRITE(6,632) NLEVEL
  632 FORMAT(/'  MOLECULAR QUANTUM NUMBERS TAKEN FROM JLEVEL ',
     1       'INPUT.  NLEVEL =',I3)

 1802 JMIN=JLEVEL(1)
      JMAX=JMIN
      NSTATE=NLEVEL
      ALLOCATE (JSTATE(NSTATE*2))
      DO I=1,NLEVEL
        JI=JLEVEL(I)
        IF (JI.LT.JMIN) JMIN=JI
        IF (JI.GT.JMAX) JMAX=JI
        JSTATE(I+NSTATE)=I
        JSTATE(I       )=JI
      ENDDO
      IF (EIN) GOTO 7002

      IF (IPRINT.GE.1) THEN
        WRITE(6,633) BE
  633   FORMAT(/'  ENERGY LEVELS OBTAINED FROM B(E) =',F12.6)
        IF (ALPHAE.NE.0.D0) WRITE(6,634) ALPHAE
  634   FORMAT('  WITH B(V) CALCULATED FROM B(E) AND ALPHA(E) =',F10.6)
        IF (DE.NE.0.D0) WRITE(6,635) DE
  635   FORMAT('  ENERGY LEVELS CORRECTED FOR D(E) =',F12.8)
      ENDIF

      DO I=1,NSTATE
        JI=JSTATE(I)
        FJ=JI*(JI+1)
        ELEVEL(I)=(BE-ALPHAE/2.D0)*FJ - DE*FJ*FJ
      ENDDO
      RETURN

 7002 IF (IPRINT.GE.1) WRITE(6,631)
  631 FORMAT(/'  ENERGY LEVELS TAKEN FROM ELEVEL INPUT')
      RETURN
C  * * * * * * * * * * * * * * * * * * * * * * * END OF SET1 AND SETBAS
C
      ENTRY SET2(LEVIN,EIN,NSTATE,IPRINT)

      BE=ROTI(1)
      ALPHAE=ROTI(3)
      DE=ROTI(5)
      WE=ROTI(7)
      WEXE=ROTI(9)
      IF (LEVIN) GOTO 2902

      WRITE(6,201)
  201 FORMAT(/'  * * * ERROR.  FOR ITYPE=2 &BASIS MUST SPECIFY NLEVEL ',
     1       'AND J, V PAIRS')
      STOP

 2902 IF (IPRINT.GE.1) WRITE(6,632) NLEVEL
      JMIN=JLEVEL(1)
      JMAX=JMIN
      NSTATE=NLEVEL
      ALLOCATE (JSTATE(NSTATE*3))
      DO I=1,NLEVEL
        JI=JLEVEL(2*I-1)
        JVI=JLEVEL(2*I)
        JMIN=MIN(JMIN,JI)
        JMAX=MAX(JMAX,JI)
        JSTATE(I         )=JI
        JSTATE(I+NSTATE  )=JVI
        JSTATE(I+NSTATE*2)=I
      ENDDO
      IF (EIN) GOTO 2002

      IF (IPRINT.GE.1) THEN
        WRITE(6,202) WE,BE
  202   FORMAT(/'  ENERGY LEVELS OBTAINED FROM W(E) =',F10.4,
     1         ',   B(E) =',F10.4/9X,'WITH ZERO ENERGY AT V=0, J=0')
        IF (WEXE.NE.0.D0) WRITE(6,636) WEXE
  636   FORMAT('  CORRECTED FOR W(E)X(E) =',F10.4)
        IF (ALPHAE.NE.0.D0) WRITE(6,634) ALPHAE
        IF (DE.NE.0.D0) WRITE(6,635) DE
      ENDIF

      DO I=1,NSTATE
        JI =JSTATE(     I)
        JVI=JSTATE(NSTATE+I)
        FJ=JI*(JI+1)
        FV=JVI
        ELEVEL(I)=WE*FV-WEXE*FV*(FV+1.D0)
     1              +(BE-ALPHAE*(FV+0.5D0))*FJ - DE*FJ*FJ
      ENDDO
      RETURN

 2002 IF (IPRINT.GE.1) WRITE(6,631)
      RETURN
C     * * * * * * * * * * * * * * * * * * * * * * * * * * * END OF SET2
C
      ENTRY SET3(LEVIN,EIN,NSTATE,IPRINT)

      BE1=ROTI(1)
      BE2=ROTI(2)
      ALPHE1=ROTI(3)
      ALPHE2=ROTI(4)
      DE1=ROTI(5)
      DE2=ROTI(6)
      IF (IDENT.EQ.0) GOTO 1993

      J2MIN=J1MIN
      J2MAX=J1MAX
      J2STEP=J1STEP
      IF (BE2.EQ.0.D0) THEN
        BE2=BE1
        ROTI(2)=ROTI(1)
      ENDIF
      IF (ALPHE2.EQ.0.D0) THEN
        ALPHE2=ALPHE1
        ROTI(4)=ROTI(3)
      ENDIF
      IF (DE2.EQ.0.D0) THEN
        DE2=DE1
        ROTI(6)=ROTI(5)
      ENDIF

 1993 IF (LEVIN) GOTO 5303

      IF (IPRINT.GE.1)
     1  WRITE(6,310) J1MIN,J1MAX,J1STEP,J2MIN,J2MAX,J2STEP
  310 FORMAT(/'  QUANTUM NUMBERS OF ROTOR 1 OBTAINED FROM J1MIN =',I3,
     1       ', J1MAX =',I3,', J1STEP =',I2//
     2       '  QUANTUM NUMBERS OF ROTOR 2 OBTAINED FROM J2MIN =',I3,
     3       ', J2MAX =',I3,', J2STEP =',I2)
      J1MIN=MAX(J1MIN,0)
      J1MAX=MAX(J1MIN,J1MAX)
      J1STEP=MAX(J1STEP,1)
      J2MIN=MAX(J2MIN,0)
      J2MAX=MAX(J2MAX,J2MIN)
      J2STEP=MAX(J2STEP,1)
      NLEVEL=0
      I=1
      DO JJ1=J1MIN,J1MAX,J1STEP
      DO JJ2=J2MIN,J2MAX,J2STEP
        IF (IDENT.NE.0 .AND. JJ1.GT.JJ2) CYCLE
        JLEVEL(I)=JJ1
        JLEVEL(I+1)=JJ2
        I=I+2
        NLEVEL=NLEVEL+1
      ENDDO
      ENDDO
      GOTO 1023

 5303 IF (IPRINT.GE.1) WRITE(6,333) NLEVEL
  333 FORMAT(/'  PAIR LEVEL QUANTUM NUMBERS TAKEN FROM ',
     1       'JLEVEL INPUT.  NLEVEL =',I3)

C  PROCESS JLEVEL TO JSTATE FORMAT.  JMIN(JMAX) ARE LOW(HIGH) OF J12.
 1023 JMIN=ABS(JLEVEL(1)-JLEVEL(2))
      JMAX=JMIN
C  EXPAND J1, J2  TO  J1, J2, J12
      NSTATE=0
      DO I=1,NLEVEL
        JJ1=JLEVEL(2*I-1)
        JJ2=JLEVEL(2*I)
        NSTATE=NSTATE+2*MIN(JJ1,JJ2)+1
      ENDDO
      ALLOCATE (JSTATE(NSTATE*4))
      ISTATE=1
      DO I=1,NLEVEL
        JJ1=JLEVEL(2*I-1)
        JJ2=JLEVEL(2*I)
        JK=ABS(JJ1-JJ2)
        JTOP=JJ1+JJ2
      DO J12=JK,JTOP
        JSTATE(ISTATE         )=JJ1
        JSTATE(ISTATE+NSTATE  )=JJ2
        JSTATE(ISTATE+NSTATE*2)=J12
        JSTATE(ISTATE+NSTATE*3)=I
        ISTATE=ISTATE+1
        JMIN=MIN(JMIN,J12)
        JMAX=MAX(JMAX,J12)
      ENDDO
      ENDDO

C  SET ELEVEL VALUES
      IF (EIN) GOTO 1073

      IF (IPRINT.GE.1) THEN
        WRITE(6,313) 1,BE1
        IF (ALPHE1.NE.0.D0) WRITE(6,634) ALPHE1
        IF (DE1.NE.0.D0) WRITE(6,635) DE1
        WRITE(6,313) 2,BE2
  313   FORMAT(/'  ENERGY LEVELS OF ROTOR',I2,' OBTAINED FROM B(E) =',
     1          F12.6)
        IF (ALPHE2.NE.0.D0) WRITE(6,634) ALPHE2
        IF (DE2.NE.0.D0) WRITE(6,635) DE2
      ENDIF

      DO I=1,NLEVEL
        FJ=DBLE(JLEVEL(2*I-1))
        GJ=DBLE(JLEVEL(2*I))
        FJ=FJ*(FJ+1.D0)
        GJ=GJ*(GJ+1.D0)
        ELEVEL(I)=(BE1-ALPHE1*0.5D0)*FJ - DE1*FJ*FJ
     1         +  (BE2-ALPHE2*0.5D0)*GJ - DE2*GJ*GJ
      ENDDO
      RETURN

 1073 IF (IPRINT.GE.1) WRITE(6,312)
  312 FORMAT(/'  PAIR LEVEL ENERGIES TAKEN FROM ELEVEL INPUT')
      RETURN
C  * * * * * * * * * * * * * * * * * * * * * * * * * * * END OF SET3
C
      ENTRY SET5(LEVIN,EIN,NSTATE,IPRINT)
C
C  N.B. WE USE D(L,K,M) WITH EDMONDS CONVENTIONS OF PHASE FOR THE
C  BASIS FUNCTIONS.  THIS IS SAME AS THADDEUS IN H2CO PAPER.
C
      A=ROTI(1)
      B=ROTI(3)
      C=ROTI(5)
      JMININ=JMIN
      IF (LEVIN) GOTO 5305

      NLEVEL=0
      I=0
      IF (IPRINT.GE.1) THEN
        WRITE(6,601) JMIN,JMAX,JSTEP
        IF (KMAX.LT.0) WRITE(6,602) -KMAX
  602   FORMAT(10X,'  ABS(K) FOR ALL LEVELS SET TO',I4)
        IF (KMAX.GE.0 .AND. KMAX.LT.JMAX) WRITE(6,603) KMAX
  603   FORMAT(10X,'  ONLY LEVELS WITH K <=',I3,' INCLUDED IN BASIS')
      ENDIF
      JMIN=MAX(JMIN,0)
      JMAX=MAX(JMIN,JMAX)

C  ISYM(1) IS INTERPRETED BITWISE: THE BITS ARE FLAGS AS FOLLOWS
C       0 - ODD  K EXCLUDED
C       1 - EVEN K EXCLUDED
C       3 - ODD  +/-K * (-1)**J EXCLUDED
C       4 - EVEN +/-K * (-1)**J EXCLUDED

      JSYM=ISYM(1)
      DO IBIT=1,4
        IP(IBIT)=MOD(JSYM,2)
        JSYM=JSYM/2
      ENDDO

C  THE FOLLOWING COMBINATIONS WILL RESULT IN NO BASIS FUNCTIONS
C  IP(1)=IP(2)=1, IP(3)=IP(4)=1

      IF (IP(1)+IP(2).EQ.2 .OR. IP(3)+IP(4).EQ.2) THEN
        WRITE(6,*) '  THIS COMBINATION OF SYMMETRY RESTRICTIONS',
     1             ' REDUCES THE NUMBER OF PAIR LEVELS TO ZERO'
        STOP
      ENDIF

!  Start of long DO loop #1
      DO JJ=JMIN,JMAX
      DO KK=0,JJ
        IF (KMAX.LT.0 .AND. KK+KMAX.NE.0)      CYCLE
        IF (KMAX.GE.0 .AND. KK.GT.KMAX)        CYCLE
C  TESTS ON EVEN/ODD MONOMER K
        IF(IP(1).EQ.1 .AND. MOD(KK,2).EQ.1)    CYCLE
        IF(IP(2).EQ.1 .AND. MOD(KK,2).EQ.0)    CYCLE
C  TESTS ON K MULTIPLE OF 3 OR NOT
        IF (ISYM(3).EQ.1 .AND. MOD(KK,3).EQ.0) CYCLE
        IF (ISYM(3).EQ.3 .AND. MOD(KK,3).NE.0) CYCLE
        IF (JSTEP.GT.1 
     1      .AND. MOD(JJ+KK,JSTEP).NE.MOD(JMININ,JSTEP))   GOTO 5314
        IF(IP(3).EQ.1 .AND. MOD(JJ,2).EQ.1)                GOTO 5314
        IF(IP(4).EQ.1 .AND. MOD(JJ,2).EQ.0)                GOTO 5314
        IF(ISYM(2).GE.0 .AND. MOD(KK+1,2).EQ.ISYM(2))      GOTO 5314
        JLEVEL(I+1)=JJ
        JLEVEL(I+2)=KK
        JLEVEL(I+3)=2
        I=I+3
        NLEVEL=NLEVEL+1
 5314   IF (KK.EQ.0) CYCLE
        IF (JSTEP.GT.1 
     1      .AND. MOD(JJ+KK+1,JSTEP).NE.MOD(JMININ,JSTEP)) CYCLE
        IF(IP(3).EQ.1 .AND. MOD(JJ,2).EQ.0)                CYCLE
        IF(IP(4).EQ.1 .AND. MOD(JJ,2).EQ.1)                CYCLE
        IF(ISYM(2).GE.0 .AND. MOD(KK,2).EQ.ISYM(2))        CYCLE
        JLEVEL(I+1)=JJ
        JLEVEL(I+2)=KK
        JLEVEL(I+3)=1
        I=I+3
        NLEVEL=NLEVEL+1
      ENDDO
      ENDDO
!  End of long DO loop #1
      GOTO 5355

 5305 IF (IPRINT.GE.1) WRITE(6,632) NLEVEL
 5355 JMIN=JLEVEL(1)
      JMAX=JMIN
      NSTATE=NLEVEL
      ALLOCATE (JSTATE(NSTATE*4))

      DO I=1,NLEVEL
        JSTATE(I         )=JLEVEL(3*I-2)
        JSTATE(I+NSTATE  )=JLEVEL(3*I-1)
        JSTATE(I+NSTATE*2)=JLEVEL(3*I)
        JSTATE(I+NSTATE*3)=I
        JJ=JSTATE(I)
        IF (JJ.LT.JMIN) JMIN=JJ
        IF (JJ.GT.JMAX) JMAX=JJ
      ENDDO
      IF (EIN) GOTO 5335

      IF (IPRINT.GE.1) THEN
        WRITE(6,604) A,B,C
  604   FORMAT(/'  ENERGY LEVELS OBTAINED FROM ZEROTH-ORDER ',
     1         'NEAR-SYMMETRIC TOP FORMULA'/
     2         10X,'ROTATIONAL CONSTANTS ARE A, B, C (CM-1) =',3F12.4/
     3         10X,'N.B. THESE MOMENTS MUST CORRESPOND RESPECTIVELY ',
     4         'TO X, Y, Z COORDINATES USED TO DEFINE INTERACTION ',
     5         'POTENTIAL')
        IF (ROTI(10).NE.0.D0)
     1    WRITE(6,605) ROTI(10),ROTI(11),ROTI(12),(-1)**ISYM(4)
  605   FORMAT(/'  TUNNELLING SPLITTING                   =',F12.7,
     1         /'  WITH CENTRIFUGAL TERMS FOR J(J+1)-K**2 =',F12.7,
     2         /'  AND                    FOR K**2        =',F12.7,
     3         /'  WITH K=0 STATE ALLOWED FOR STATE OF |',I2,
     4          '> SYMMETRY')
        IF (ISYM(3).EQ.1) WRITE(6,606) 'E'
        IF (ISYM(3).EQ.3) WRITE(6,606) 'A'
  606   FORMAT('  3-FOLD SYMMETRY: ONLY ',A1,' STATES INCLUDED')
        IF(JSTEP.GT.1) WRITE(6,608) JSTEP,JMININ,JSTEP,MOD(JMININ,JSTEP)
  608   FORMAT(/'  JSTEP =',I2,', JMIN =',I2,': ROTOR FUNCTIONS',
     1          ' INCLUDED ONLY IF MOD(J+K+PRTY,',I1,') IS ',I1)
        IF(ISYM(1).GT.0) WRITE(6,609) ISYM(1),(IP(I),I=1,4)
  609   FORMAT(/'  ROTOR FUNCTIONS EXCLUDED BASED ON ISYM(1) =',I3/
     1          '  CORRESPONDING TO BIT PATTERN',4I2
     2          '  FOR EXCLUDING ODD AND EVEN K, ODD AND EVEN J+PRTY')
        IF(ISYM(2).GE.0) WRITE(6,610) ISYM(2),ISYM(2)
  610   FORMAT(/'  ISYM(2) =',I2,': ROTOR FUNCTIONS INCLUDED ONLY IF',
     1          '  MOD(K+PRTY,2) IS',I2)
      ENDIF

      DO I=1,NSTATE
        JJ=JSTATE(I)
        KK=ABS(JSTATE(I+NSTATE))
        SS=(-1.D0)**JSTATE(I+2*NSTATE)
        HKK=(A+B)*DBLE(JJ*(JJ+1)-KK*KK)/2.D0+C*DBLE(KK*KK)
        IF (MOD(JSTATE(I)+JSTATE(I+2*NSTATE),2).NE.0)
     1    HKK=HKK+(-1.D0)**ISYM(4)*
     1              (ROTI(10)-ROTI(11)*(JJ*(JJ+1)-KK*KK)-ROTI(12)*KK*KK)
C  OFF-DIAGONAL CONTRIBUTION ONLY FROM K=1/K=-1 CASE. . .
        IF (KK.EQ.1) HKK=HKK+ SS * (A-B) *
     1                        SQRT(DBLE((JJ*(JJ+1)-KK*(KK-1))*
     1                                  (JJ*(JJ+1)-(KK-1)*(KK-2))))/4.D0
        ELEVEL(I)=HKK
      ENDDO
      RETURN

 5335 IF (IPRINT.GE.1) WRITE(6,631)
      RETURN
      END
C  * * * * * * * * * * * * * * * * * * * * * * * * * * * END OF SET5
