      SUBROUTINE GETLEV(N,EINT,INDLEV,L,CM2RU,
     1                  EREF,ENERGY,NNRG,DEGTOL,MXSIG,IPRINT,
     2                  LNEVER,IBOUND,EFACT,UNAME)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential
      USE basis_data, ONLY: NLEVEL, ELEVEL, MXELVL
C
C  SUBROUTINE TO ANALYSE EINT ARRAY RETURNED BY YTRANS AND
C  CREATE AN ELEVEL ARRAY WITH POINTERS TO IT IN INDLEV
C  THIS REUSE OF INDLEV SHOULD PROBABLY USE A NEW ARRAY INSTEAD
C
C  THIS VERSION (FEB 2018) IS DESIGNED TO ACCUMULATE ELEVEL
C  ACROSS MULTIPLE VALUES OF JTOT AND SYMMETRY TYPE
C  (FOR A SINGLE SET OF EXTERNAL FIELD VARIABLES)
C  NLEVEL MUST BE SET/RESET TO 0 FOR THE FIRST JTOT/SYMMETRY
C  AND IS INCREMENTED EACH TIME THROUGH GETLEV
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION EINT(N),INDLEV(N),L(N),ENERGY(NNRG)
      LOGICAL LNEVER(1),LFIRST,LIST
      CHARACTER(8) UNAME
      CHARACTER(6) NAMEL

C  ALTERED TO USE ARRAY SIZES FROM MODULE sizes ON 23-06-17 BY CRLS

      IF (NCONST.EQ.0 .AND. NRSQ.EQ.0) THEN
        DO 1 IL=1,NLEVEL
          LNEVER(IL)=.FALSE.
          DO 2 IE=1,NNRG
            IF (EREF+ENERGY(IE).GT.ELEVEL(IL)) GOTO 1
    2     CONTINUE
          LNEVER(IL)=.TRUE.
    1   CONTINUE
        RETURN
      ENDIF
C

      NLVOLD=NLEVEL
      LIST=(IPRINT.GE.10 .OR. (IPRINT.GE.6 .AND. NLEVEL.EQ.0))
      DO 10 I=1,N
        ECHAN=EINT(I)/CM2RU
C
C  IF THIS CHANNEL IS FROM A LEVEL WE ALREADY HAVE,
C  ASSIGN AN APPROPRIATE INDEX
C
        DO 20 IL=1,NLEVEL
          IF (ABS(ECHAN-ELEVEL(IL)).LT.DEGTOL) THEN
            INDLEV(I)=IL
            GOTO 10
          ENDIF
   20   CONTINUE
C
C  REACH HERE IF THIS CHANNEL IS FROM A NEW LEVEL
C
C  FIRST CHECK WHETHER THIS LEVEL WILL EVER GIVE OPEN CHANNELS
C
        DO 30 IE=1,NNRG
          IF (EREF+ENERGY(IE).GT.ECHAN) GOTO 40
   30   CONTINUE
C
C  REACH HERE IF NEVER OPEN: SKIP IT AND SET INDEX TO -1 AS A FLAG
C
        INDLEV(I)=-1
        IF (IPRINT.GE.11) WRITE(6,603) I,ECHAN
  603   FORMAT('  CHANNEL',I5,' WITH ENERGY',F19.12,' IS NEVER OPEN AT',
     1         ' REQUESTED ENERGIES: SKIP')
        GOTO 10
C
C  REACH HERE FOR NEW LEVEL THAT IS OPEN AT SOME REQUESTED ENERGY
C  ADD IT TO ELEVEL AND INDEX THIS CHANNEL TO IT
C  FIRST CHECK STORAGE IS AVAILABLE
C
   40   IF (NLEVEL.GE.MXSIG .AND. MXSIG.GT.0) THEN
          IF (IPRINT.GE.10) WRITE(6,605) ECHAN,I,' MXSIG',MXSIG
          INDLEV(I)=0
          GOTO 10
        ELSEIF (NLEVEL.GE.MXELVL) THEN
          IF (IPRINT.GE.10) WRITE(6,605) ECHAN,I,'MXELVL',MXELVL
          INDLEV(I)=0
          GOTO 10
        ENDIF
C
  605   FORMAT('  NO SPACE TO STORE NEW LEVEL AT',F19.12,
     1         ' FOR CHANNEL',I6,' BECAUSE INDEX EXCEEDS ',A6,' =',2I6)
C
        NLEVEL=NLEVEL+1
        ELEVEL(NLEVEL)=ECHAN
        INDLEV(I)=NLEVEL
        LNEVER(NLEVEL)=.FALSE.
        IF (L(I).EQ.0) LIST=.TRUE.
C       IF (IPRINT.GE.6) WRITE(6,606) NLEVEL,ECHAN,I,L(I)
   10 CONTINUE

      IF (IPRINT.GE.6 .AND. .NOT.LIST)
     1  WRITE(6,'(/2X,A)') 'THRESHOLD LIST: NO NEW LEVELS IDENTIFIED'

      IF (IPRINT.LT.6 .OR. .NOT.LIST) RETURN

      WRITE(6,'(/2X,A)') 'THRESHOLD LIST:'
      WRITE(6,'(2X,A)') 'NEW PAIR LEVELS ARE ASSIGNED EACH TIME A'//
     1                  ' NEW THRESHOLD ENERGY IS FOUND'

      IF (IBOUND.EQ.0) THEN
        NAMEL='    L '
      ELSE
        NAMEL='<L**2>'
      ENDIF

      IF (EFACT.NE.1.D0) THEN
        WRITE(6,601) DEGTOL,NAMEL,TRIM(ADJUSTL(UNAME))
      ELSE
        WRITE(6,601) DEGTOL,NAMEL
      ENDIF

  601 FORMAT(/'  THRESHOLDS CALCULATED FROM ASYMPTOTIC HAMILTONIAN',
     1        ' USING DEGENERACY TOLERANCE',1PE9.2,' CM-1:'//
     2        2X,'CHANNEL',2X,A6,5X,'PAIR LEVEL',4X,
     3        'PAIR ENERGY (CM-1)',:4X,'PAIR ENERGY (',A,')')

      DO ILVL=1,NLVOLD
        DO I=1,N
          ECHAN=EINT(I)/CM2RU
          IF (ABS(ECHAN-ELEVEL(ILVL)).LT.DEGTOL) THEN
            IF ((L(I).EQ.0 .AND. IPRINT.GE.6) .OR. IPRINT.GE.10)
     1        WRITE(6,606) I,L(I),INDLEV(I)
          ENDIF
        ENDDO
      ENDDO

      DO ILVL=NLVOLD+1,NLEVEL
        LFIRST=.TRUE.
        DO I=1,N
          ECHAN=EINT(I)/CM2RU
          IF (ABS(ECHAN-ELEVEL(ILVL)).LT.DEGTOL) THEN
            IF (LFIRST) THEN
              IF (EFACT.NE.1.D0) THEN
                WRITE(6,606) I,L(I),INDLEV(I),ECHAN,ECHAN/EFACT
              ELSE
                WRITE(6,606) I,L(I),INDLEV(I),ECHAN
              ENDIF
              LFIRST=.FALSE.
            ELSEIF ((L(I).EQ.0 .AND. IPRINT.GE.6) .OR. IPRINT.GE.10)
     1      THEN
              WRITE(6,606) I,L(I),INDLEV(I)
            ENDIF
          ENDIF
        ENDDO
      ENDDO

  606 FORMAT(2X,I6,3X,I5,7X,I5:,3X,F19.12,:7X,G19.12)

      RETURN
      END
