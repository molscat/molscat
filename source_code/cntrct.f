      SUBROUTINE CNTRCT(N, M, R, EMAX, VLTEMP,
     1                  VL, IV, EINT, CENT,
     2                  EP2RU, CM2RU, RSCALE, MXLAM, NHAM, IPRINT)
C  Copyright (C) 2022 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential, ONLY: NCONST, NRSQ, VCONST
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  ROUTINE TO CONTRACT THE ANGULAR BASIS SET BY TRANSFORMING
C  THE VL ARRAY TO A REPRESENTATION THAT IS DIAGONAL AT R.
C
C  ON ENTRY: N IS SIZE OF ORIGINAL BASIS,
C            VL CONTAINS THE POTENTIAL MATRIX,
C            IV (IF USED) CONTAINS ADDRESSES OF POTENTIAL MATRIX ELEMENTS,
C            MXLAM IS MAXIMUM NUMBER OF TERMS IN POTENTIAL
C            IPRINT CONTROLS VERBOSITY OF OUTPUT
C  DURING:   W IS USED FOR THE HAMILTONIAN MATRIX
C            T IS USED FOR ITS EIGENVECTORS
C            EINT IS USED FOR THE THRESHOLDS,
C            CENT IS USED FOR THE CENTRIFUGAL TERMS,
C            P IS USED FOR THE CONSTANT TERMS IN THE POTENTIAL
C  ON EXIT:  M IS THE SIZE OF THE NEW BASIS
C            NHAM IS MXLAM+2
C  27-07-2020: ALTERED BY CRLS TO USE ALLOCATABLE ARRAYS.
C              NOTE - WE HAVE NO TEST CASES THAT USE THIS, SO THERE
C                     MIGHT BE BUGS!
      ALLOCATABLE :: W(:,:),T(:,:),P(:),WTEMP(:),DIAG(:)
C
      DIMENSION VL(NHAM,*),IV(*),EINT(N),CENT(N),VLTEMP(NHAM+2,*)
      COMMON /VLSAVE/ IVLU
      COMMON /VLFLAG/ IVLFL
C
      DATA ZERO /0.D0/, HALF /0.5D0/, ONE /1.D0/
C
      IF (IVLFL.GT.0) THEN
        M=N
        WRITE(6,*) ' BASIS SET CONTRACTION NOT ALLOWED FOR IVLFL > 0'
        RETURN
      ENDIF
C
      IF (NCONST.GT.0 .OR. NRSQ.GT.0) THEN
        M=N
        WRITE(6,*) ' BASIS SET CONTRACTION NOT ALLOWED FOR NCONST > 0',
     1             ' OR NRSQ > 0'
        WRITE(6,*) ' BUT THIS MAY BE EASY TO FIX IN SUBROUTINE CNTRCT'
        RETURN
      ENDIF
C
      ERED=0.D0
      NHAM=MXLAM
      ALLOCATE (W(N,N),P(MXLAM+NCONST+NRSQ),DIAG(N))
      CALL HAMMAT(W,N,R,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1            RSCALE,DIAG,MXLAM,NHAM,IPRINT)
      DEALLOCATE (P)
C
      IFAIL=0
      ALLOCATE (T(N,N))
      CALL DIAGVC(W,N,N,DIAG,T)
      DEALLOCATE (DIAG)
C
      IF (IPRINT.GE.13) WRITE(6,602) EMAX,(DIAG(I),I=1,N)
  602 FORMAT(/'  CONTRACTION WILL USE REDUCED ENERGY CUTOFF OF',
     1       F9.2/'  REDUCED EIGENVALUES OF ANGULAR HAMILTONIAN ARE'/
     2       (2X,9F8.2))
C
C  SCAN EIGENVALUES TO DECIDE HOW MANY CHANNELS TO KEEP
C
      DO I=1,N
        M=I
        IF (DIAG(I).GT.EMAX) EXIT
      ENDDO
C
      IF (IPRINT.GE.3) WRITE(6,603) N,M,R
  603 FORMAT(/'  CONTRACTING BASIS SET FROM N =',I5,' TO M =',I4/
     1       '  USING FIXED-R EIGENVECTORS AT R =',F7.3)
      NHAMC=NHAM+2
C
      NVLC=M*(M+1)/2
      ALLOCATE (WTEMP(N*M))
C
      IF (IVLU.GT.0) REWIND IVLU
      DO LL=1,NHAM
        IF (IVLU.EQ.0) THEN
          IJ=0
          DO I=1,N
          DO J=1,I
            IJ=IJ+1
            W(J,I)=VL(LL,IJ) !VL(IX) COPY A BLOCK OF VL MATRIX TO W SPACE
          ENDDO
          ENDDO
        ELSE
          READ(IVLU) ((W(J,I),J=1,I),I=1,N)
        ENDIF
C TRANSFORM VL BLOCK WITH FIRST M EIGENVECTORS HELD IN T.  PUT RESULTS
C IN WTEMP
        CALL DSYMM('L','U',N,M,ONE,W,N,T,N,ZERO,WTEMP,N)
C THEN COMPLETE BY COMBINING WTEMP^T * T AND T^T * WTEMP TO ENSURE SYMMETRY
C AND PUT RESULT INTO W
        CALL DSYR2K('U','T',M,N,HALF,WTEMP,N,T,N,ZERO,W,N)
C
C COPY RESULTS BACK INTO TEMPORARY VL ARRAY

C
        IJ=0
        DO I=1,M
        DO J=1,I
          IJ=IJ+1
          VLTEMP(LL,IJ)=W(J,I)
        ENDDO
        ENDDO
      ENDDO
      DEALLOCATE (WTEMP)
C
      IF (IVLU.GE.10) CLOSE(IVLU)
C
C  ALSO TRANSFORM (ORIGINALLY DIAGONAL) EINT MATRIX
      IJ=0
      DO I=1,M
      DO J=1,I
        TEMP=0.D0
        DO K=1,N
          TEMP=TEMP+EINT(K)/EP2RU*T(K,I)*T(K,J)
        ENDDO
        VLTEMP(NHAM+1,IJ)=TEMP
        W(I,J)=TEMP
      ENDDO
      ENDDO
C
C  AND (ORIGINALLY DIAGONAL) CENTRIFUGAL TERMS
      IJ=0
      DO I=1,M
      DO J=1,I
        IJ=IJ+1
        TEMP=0.D0
        DO K=1,N
          TEMP=TEMP+CENT(K)*T(K,I)*T(K,J)
        ENDDO
        VLTEMP(NHAM+2,IJ)=TEMP
      ENDDO
      ENDDO

      DEALLOCATE (T)
C
C  MONITOR HOW WELL THE CONTRACTED BASIS GIVES THE MONOMER LEVELS
C  NOTE THAT THE VL ARRAY CONTAINS EINT INFORMATION IN EXTERNAL
C  UNITS, NOT IN REDUCED UNITS AS IN MOLSCAT V14
C
      IF (IPRINT.GE.7) THEN
        ALLOCATE (DIAG(M))
        CALL DIAGVL(W,N,M,DIAG)
        WRITE(6,604) (DIAG(I),I=1,7)
  604   FORMAT('  LOWEST EIGENVALUES OF E(INTERNAL) IN CONTRACTED',
     1         ' BASIS SET ARE'/(1X,7F11.4))
        DEALLOCATE (DIAG)
      ENDIF
C
      NCONST=1
      NRSQ=1
      VCONST(1)=1.D0
      IVLU=0
      NHAM=NHAMC
C
      RETURN
      END
