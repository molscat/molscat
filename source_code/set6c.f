      SUBROUTINE SET6C(NSTATE,EIN,EFACT,EUNAME,IPRINT,IASYMU)
C  Copyright (C) 2025 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE basis_data, ONLY: ELEVEL, EMAX, ISYM, JLEVEL, JMAX, JMIN,
     b                      JSTEP, NLEVEL, ROTI
      USE pair_state, ONLY: ATAU, JSTATE
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  CALCULATE ASYMMETRIC ROTOR ENERGY LEVELS AND WAVEFUNCTIONS
C  FROM ROTATIONAL CONSTANTS. WRITTEN BY JM Hutson, MARCH 1989.
C  MODIFIED TO HANDLE SPHERICAL TOP SYMMETRY, APRIL 1991.
C  MODIFIED TO USE WORKSPACE PROPERLY FOR VERSION 12, NOV 1993.
C  MODIFIED TO HANDLE 3-FOLD SYMMETRY, JAN 2021.
C
C  CRLS 06-2022: CONVERTED TO USE ALLOCATABLE ARRAYS, INCLUDING THE
C                JSTATE AND ATAU ARRAYS WHICH ARE NOW IN pair_state MODULE
C  CRLS 08-2022: CODE FOR WRITING TO IASYMU MOVED FROM SET4/SET6 TO HERE

      LOGICAL EIN
      CHARACTER(8) EUNAME
      ALLOCATABLE JTEMP(:),ATEMP(:),EVAL(:),EVEC(:,:)
      INTEGER IP(10)
C  MODS 11 JUL 94 FOR V14 CMBASE
C  N.B IPAR WAS EQUIVALENCED TO J2MAX, NOW TO ISYM(1)
C  AUG 18 CMBASE REPLACED BY MODULE basis_data
C
      DATA TOL/1.D-8/

      IF (EIN .AND. IPRINT.GE.1) WRITE(6,601)
  601 FORMAT(/'  NON-ZERO PAIR ENERGY LEVELS GIVEN IN ELEVEL'/
     1       '  WILL OVERRIDE THOSE CALCULATED FROM ROTATIONAL ',
     2       'CONSTANTS')
      IF (ROTI(1).EQ.ROTI(3) .AND. ROTI(3).EQ.ROTI(5)) THEN
        IF (IPRINT.GE.1) WRITE(6,602) ROTI(1),ROTI(7),ROTI(10)
  602   FORMAT(/'  SPHERICAL ROTOR LEVELS CALCULATED FROM'/
     1         '  A = B = C =',F11.5/'  DJ',8X,'=',E11.3/
     2         '  DT',8X,'=',E11.3)
        IF (ABS(ROTI(10)).LT.1.D-8 .AND. IPRINT.GE.1) WRITE(6,603)
  603   FORMAT('  *** WARNING: IF ABS(DT) IS LESS THAN ABOUT 1.D-8,',
     1         ' THE PROGRAM MAY FAIL TO DISTINGUISH LEVELS OF',
     2         ' DIFFERENT SYMMETRY')
      ELSEIF (IPRINT.GE.1) THEN
        WRITE(6,604) ROTI(1),ROTI(3),ROTI(5),ROTI(7),ROTI(8),ROTI(9)
  604   FORMAT(/'  ASYMMETRIC ROTOR LEVELS CALCULATED FROM'/
     1          '  A   =',F10.5,8X,'B   =',F10.5,8X,'C   =',F10.5/
     2          '  DJ  =',E10.3,8X,'DJK =',E10.3,8X,'DK  =',E10.3//
     3          '  A, B AND C MUST CORRESPOND TO THE X, Y AND Z',
     4          ' COORDINATES USED TO DEFINE THE INTERACTION POTENTIAL')
      ENDIF
C
C  ISYM(1) IS INTERPRETED BITWISE: THE BITS ARE FLAGS AS FOLLOWS
C       0 - ODD  K EXCLUDED
C       1 - EVEN K EXCLUDED
C       2 - ODD  +/-K * (-1)**J EXCLUDED
C       3 - EVEN +/-K * (-1)**J EXCLUDED
C       4 - DEGENERACY = 1 EXCLUDED
C       5 - DEGENERACY = 2 EXCLUDED
C       6 - DEGENERACY = 3 EXCLUDED
C       7 - DEGENERACY > 3 EXCLUDED
C       8 - FUNCTIONS INCLUDING K.EQ.3N EXCLUDED
C       9 - FUNCTIONS INCLUDING K.NE.3N EXCLUDED
C
C  NOTE THAT THIS LOGIC WAS CHANGED IN AUGUST 1992,
C  IN A WAY THAT ALTERS THE INPUT VALUE OF ISYM REQUIRED,
C  FOLLOWING BETA TESTING OF VERSION 11
C
C  CRLS 06-2022: DECONSTRUCTION OF ISYM(1) MOVED OUT OF LOOP - THIS
C                ANALYSIS ONLY NEEDS TO BE DONE ONCE

      JSYM=ISYM(1)
      DO IBIT=1,10
        IP(IBIT)=MOD(JSYM,2)
        JSYM=JSYM/2
      ENDDO

C  THE FOLLOWING COMBINATIONS WILL RESULT IN NO BASIS FUNCTIONS
C
C  IP(1)=IP(2)=1, IP(3)=IP(4)=1, IP(5)=IP(6)=IP(7)=IP(8)=1,
C  IP(9)=IP(10)=1
      IF (IP(1)+IP(2).EQ.2 .OR. IP(3)+IP(4).EQ.2 .OR.
     1    IP(9)+IP(10).EQ.2 .OR. IP(5)+IP(6)+IP(7)+IP(8).EQ.4) THEN
        WRITE(6,*) '  THIS COMBINATION OF SYMMETRY RESTRICTIONS',
     1             ' REDUCES THE BASIS SIZE TO ZERO'
        STOP
      ENDIF

      NLVL=0
      ESAVE=-999.D0
      NSTATE=0
      NTAU=0
      MAXJST=(JMAX+1)**2
      MAXTAU=(JMAX+1)*(2*JMAX+1)*(2*JMAX+3)/3
      ALLOCATE (JSTATE(6*MAXJST),ATAU(MAXTAU))
      WRITE(6,*)EIN
      IF (NLEVEL.GT.0) THEN
        WRITE(6,*)' *** WARNING: SET6C SETS LEVELS USING JMIN AND JMAX.'
        WRITE(6,*)'              NLEVEL, JLEVEL AND ELEVEL WILL BE ',
     1            'IGNORED'
      ENDIF
C
!  Start of long DO loop #1
      DO J=JMIN,JMAX
        NK=J+J+1
C
        ALLOCATE (EVEC(NK,NK),EVAL(NK))
        CALL ASROT(J,EVEC,EVAL,NK,IPRINT)
!  Start of long DO loop #2
        DO IK=1,NK
C
C  CHECK LEVEL ENERGY AND PARITY TO SEE WHETHER WE REALLY WANT IT
C
          ELEV=EVAL(IK)
          IF (EMAX.GT.0.D0 .AND. ELEV.GT.EMAX) CYCLE
          IPLEV=IPASYM(J,NK,EVEC(1,IK))
C
          IF (JSTEP.EQ.1 .AND. ISYM(1).LE.0 .AND. ISYM(2).LT.0) GOTO 410
C
C  FIND DEGENERACY
C
          IDEG=0
          DO KK=1,NK
            IF (ABS(EVAL(KK)-ELEV).LT.TOL) IDEG=IDEG+1
          ENDDO
C
C  CHANGED LOGIC FOR JSTEP=2 TO SELECT ON (-1)**(J+KK+PRTY)
C  AND FOR ISYM(2) TO SELECT ON (-1)**KK. JMH, FEB 2023
C
          KK=IPLEV/2
          IF (MOD(J+KK+IPLEV,JSTEP).NE.MOD(JMIN,JSTEP)) CYCLE
C
          IF (ISYM(2).GE.0 .AND. MOD(KK+IPLEV,2).NE.ISYM(2)) CYCLE
C
          IF (IP(1).EQ.1 .AND. IPLEV.GE.2) CYCLE
C
          IF (IP(2).EQ.1 .AND. IPLEV.LE.1) CYCLE
C
          IF (IP(3).EQ.1 .AND. MOD(IPLEV+J,2).EQ.1) CYCLE
C
          IF (IP(4).EQ.1 .AND. MOD(IPLEV+J,2).EQ.0) CYCLE
C
          IF (IP(5).EQ.1 .AND. IDEG.EQ.1) CYCLE
C
          IF (IP(6).EQ.1 .AND. IDEG.EQ.2) CYCLE
C
          IF (IP(7).EQ.1 .AND. IDEG.EQ.3) CYCLE
C
          IF (IP(8).EQ.1 .AND. IDEG.GT.3) CYCLE
C
C  CAPABILITY ADDED BY JMH, JAN 2021 TO HANDLE 3-FOLD SYMMETRY
C  BIT 8, ADD 256: EXCLUDE FUNCTIONS WITH K A MULTIPLE OF 3
C  BIT 9, ADD 512: EXCLUDE FUNCTIONS WITH K NOT A MULTIPLE OF 3
C
C  EXCLUDE IF THERE ARE COMPONENTS WITH K A MULTIPLE OF 3
          DO KBAS=1,NK
            IF (IP(9).EQ.1 .AND. MOD(KBAS-1-J,3).EQ.0 .AND.
     1          ABS(EVEC(KBAS,1)).GT.1D-8) CYCLE
          ENDDO

C  EXCLUDE IF THERE ARE COMPONENTS WITH K NOT A MULTIPLE OF 3
          DO KBAS=1,NK
            IF (IP(10).EQ.1 .AND. MOD(KBAS-1-J,3).NE.0 .AND.
     1          ABS(EVEC(KBAS,IK)).GT.1D-8) CYCLE
          ENDDO

  410     NSTATE=NSTATE+1

          IF (NSTATE.GT.MAXJST) THEN
            WRITE(6,*)' *** ERROR: SPACE ALLOWED FOR JSTATE ARRAY ',
     1                'IN ROUTINE SET6C IS NOT LARGE ENOUGH'
            WRITE(6,*)MAXJST,JMAX,J,IK
            STOP
          ENDIF

          IF (NLEVEL.GT.0 .AND. NSTATE.GT.NLEVEL) CYCLE
C
C  ARRIVE HERE IF WE ARE INCLUDING THIS STATE: STORE JSTATE AND TAU
C                                              IN TEMPORARY LOCATIONS
C
          PREV=ESAVE
          ESAVE=ELEV
C  IF TWO LEVELS OF THE SAME J ARE DEGENERATE, WE NEED TO CHOOSE WHETHER
C  TO TREAT THEM AS ONE LEVEL OR TWO. THE CHOICE MADE HERE IS TO TREAT
C  THEM AS ONE LEVEL FOR A SPHERICAL TOP WITH A (ROTI(1)) = C (ROTI(5))
C  BUT AS TWO IF THE Z AXIS IS DISTINCT (SYMMETRIC OR NEAR-SYMMETRIC
C  TOP, OR A NEAR-DEGENERACY FOR HIGH K FOR A LESS SYMMETRIC TOP.
C  JMH CHANGED ROTI(3) TO ROTI(5) TO DO THIS, JANUARY 2021
          IF (ABS(ESAVE-PREV).GT.TOL .OR. ROTI(1).NE.ROTI(5)) THEN
            NLVL=NLVL+1
            JLEVEL(2*NLVL-1)=J
            JLEVEL(2*NLVL)=IK -1-J
            IF (.NOT.EIN) ELEVEL(NLVL)=ELEV
          ENDIF
C
          JSTATE(6*NSTATE-5)=J
          JSTATE(6*NSTATE-4)=IK -1-J
          JSTATE(6*NSTATE-3)=IPLEV
          JSTATE(6*NSTATE-2)=NTAU
          JSTATE(6*NSTATE-1)=NK
          JSTATE(6*NSTATE  )=NLVL
C
C  NTAU KEEPS TRACK OF WHERE WE ARE PUTTING THE COEFFICIENTS,
C
          IF (NTAU+NK.GT.MAXTAU) THEN
            WRITE(6,*)' *** ERROR: SPACE ALLOWED FOR ATAU ARRAY ',
     1                'IN ROUTINE SET6C IS NOT LARGE ENOUGH'
            WRITE(6,*)MAXTAU,JMAX,J,IK
            STOP
          ENDIF

          DO I=1,NK
            ATAU(NTAU+I)=EVEC(I,IK)
          ENDDO
          NTAU=NTAU+NK
          IF (NLEVEL.GT.0 .AND. NSTATE.EQ.NLEVEL) THEN
            DEALLOCATE (EVAL,EVEC)
            GOTO 451
          ENDIF
        ENDDO
!  End of long DO loop #2
        DEALLOCATE (EVAL,EVEC)
      ENDDO
!  End of long DO loop #1

      IF (IPRINT.GE.1) THEN
        IF (JSTEP.GT.1) WRITE(6,608) JSTEP,JMIN,JSTEP,MOD(JMIN,JSTEP)
  608   FORMAT(/'  JSTEP =',I2,', JMIN =',I2,': ROTOR FUNCTIONS',
     1          ' INCLUDED ONLY IF MOD(J+K+PRTY,',I1,') IS ',I1)
        IF (ISYM(1).GT.0) WRITE(6,609) ISYM(1),(IP(I),I=1,10)
  609   FORMAT(/'  ROTOR FUNCTIONS EXCLUDED BASED ON ISYM(1) =',I4,
     1          ' CORRESPONDING TO BIT PATTERN'/'  FOR EXCLUDING',
     2          ' ODD AND EVEN K, ODD AND EVEN J+PRTY, DEGENERACY'
     3          ' 1, 2, 3, >3, SYMMETRY A, E'/
     4          17X,I1,7X,I1,7X,I1,7X,I1,22X,I1,2X,I1,2X,I1,3X,I1,11X,
     5          I1,2X,I1)
        IF (ISYM(2).GE.0) WRITE(6,610) ISYM(2),ISYM(2)
  610   FORMAT(/'  ISYM(2) =',I2,': ROTOR FUNCTIONS INCLUDED ONLY IF',
     1          '  MOD(K+PRTY,2) IS',I2)
      ENDIF
C
C  REDUCE SIZE OF JSTATE AND ATAU IF NECESSARY
C
      IF (NLEVEL.GT.0) NSTATE=MIN(NSTATE,NLEVEL)
451   IF (NLEVEL.EQ.0) NLEVEL=NLVL
      IF (NSTATE.LT.MAXJST) THEN
        ALLOCATE (JTEMP(6*NSTATE),ATEMP(NTAU))
        JTEMP=JSTATE(1:6*NSTATE)
        ATEMP=ATAU(1:NTAU)
        DEALLOCATE (JSTATE,ATAU)
        ALLOCATE (JSTATE(6*NSTATE),ATAU(NTAU))
        JSTATE=JTEMP
        ATAU=ATEMP
        DEALLOCATE (JTEMP,ATEMP)
      ENDIF

C  REARRANGE JSTATE
      ALLOCATE (JTEMP(6*NSTATE))
      DO ISTATE=1,NSTATE
        DO IQN=1,6
          JTEMP(NSTATE*(IQN-1)+ISTATE)=JSTATE(6*(ISTATE-1)+IQN)
        ENDDO
      ENDDO
      JSTATE=JTEMP
      DEALLOCATE (JTEMP)

C  OPTION ADDED (AUG 94) TO OUTPUT ROTOR WFNS TO IASYMU

      IF (IASYMU.GT.0 .AND. IPRINT.GE.1)
     1  WRITE(6,1098) IASYMU,TRIM(EUNAME)
 1098 FORMAT(/'  *** SET6C WILL OUTPUT ROTOR WAVEFUNCTIONS TO UNIT',
     1       I4,' FOR FUTURE INPUT'/
     2       '  ENERGIES ARE WRITTEN IN ',A)
C     IF (IPRINT.GE.2) WRITE(6,499) NSTATE,TRIM(EUNAME)
C 499 FORMAT(/I5,' ROTOR FUNCTIONS HAVE BEEN RETAINED'/
C    1       '         J    ITAU    ENERGY (',A,')      ENERGY (CM-1)')
      DO I=1,NSTATE
        JI  =JSTATE(         I)
        ITAU=JSTATE(  NSTATE+I)
        ISTA=JSTATE(3*NSTATE+I)
        NK  =JSTATE(4*NSTATE+I)
        INDX=JSTATE(5*NSTATE+I)
        IF (IASYMU.GT.0) THEN
          WRITE(IASYMU,500,ERR=1099) JI,ITAU,ELEVEL(INDX)/EFACT,
     1                               TRIM(EUNAME)
C         WRITE(IASYMU,*,ERR=1099) JI,ITAU,ELEVEL(INDX)/EFACT
          WRITE(IASYMU,501,ERR=1099) (ATAU(ISTA+II),II=1,NK)
        ENDIF
C       IF (IPRINT.GE.3) WRITE(6,502) I,JI,ITAU,ELEVEL(INDX)/EFACT,
C    1                                ELEVEL(INDX)
  500   FORMAT(2I5,F15.10,1X,A)
  501   FORMAT(6F12.8)
  502   FORMAT(3I5,5X,F15.10,1X,F15.10)
      ENDDO
      CLOSE(IASYMU)
      RETURN

 1099 WRITE(6,*) '  *** SET6C. ERROR WRITING TO IASYMU; WFNS NOT SAVED'

      RETURN
      END
