      SUBROUTINE YTOK(NB,WVEC,L,N,NOPEN,Y,KMTRIX,RVAL,CENT)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  ROUTINE TO OBTAIN THE K MATRIX FROM THE LOG DERIVATIVE MATRIX
C  ON ENTRY:  Y HOLDS THE LOG DERIVATIVE MATRIX
C  ON EXIT:   KMTRIX HOLDS THE K MATRIX
C  SEE: B.R.JOHNSON, JOURNAL OF COMPUTATIONAL PHYSICS 13, 445 (1973)
C
C  EXTENDED TO NON-INTEGER ANGULAR MOMENTUM L BY M.MORITA 15-07-2014
C  MODIFIED TO USE NON-INTEGER BESSEL-FUNCTION ROUTINES UNCONDITIONALLY
C  BY JMH 14-12-2018
C
      DOUBLE PRECISION KMTRIX
      DIMENSION NB(N), WVEC(N), L(N), Y(N,N), KMTRIX(NOPEN,N), CENT(N)
      ALLOCATABLE :: SJ(:),SJP(:),SN(:),SNP(:)
C  SJ,SJP HOLD THE RICCATI-BESSEL FUNCTIONS AND DERIVATIVES
C  SN,SNP HOLD THE MODIFIED SPHERICAL BESSEL FUNCTIONS AND DERIVATIVES
C
      IF (NOPEN.EQ.0) RETURN

      ALLOCATE (SJ(N),SJP(N),SN(N),SNP(N))
      DO I = 1,NOPEN
        NX = NB(I)
        DW = WVEC(NX)
        DARG = DW*RVAL
        ELL=SQRT(CENT(NX)+0.25D0)-0.5D0
        CALL RBESJY(ELL, DARG, UJ, UJP, UN, UNP)
        ROOTDW = SQRT(DW)
        SJ(NX) = UJ/ROOTDW
        SJP(NX) = UJP*ROOTDW
        SN(NX) = UN/ROOTDW
        SNP(NX) = UNP*ROOTDW
      ENDDO

      DO I = NOPEN+1,N
        NX = NB(I)
        DW = ABS(WVEC(NX))
        DARG = DW*RVAL
        ELL=SQRT(CENT(NX)+0.25D0)-0.5D0
        CALL RBESSK(ELL, DARG, RATIO)
        SN(NX) = 1.D0
        SNP(NX) = RATIO*DW
      ENDDO
C
      CALL YTOKG(NB,N,NOPEN,SJ,SJP,SN,SNP,Y,KMTRIX)
      DEALLOCATE (SJ,SJP,SN,SNP)
      RETURN
      END
