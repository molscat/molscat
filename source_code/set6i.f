      SUBROUTINE SET6I(JSTATE,MXLV,NSTATE,MXA,IUNIT,IPRINT)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  ON ENTRY: NSTATE IS THE SIZE OF THE BASIS
C  ON EXIT:  JSTATE CONTAINS THE LABELS FOR THE BASIS FUNCTIONS
C            A CONTAINS SOMETHING
C
C  CRLS 06-2022: CONVERTED TO USE ATAU FROM pair_state MODULE
C
      USE pair_state, ONLY: ATAU
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION JSTATE(4,MXLV)
      ALLOCATABLE ATMP(:),jstemp(:,:)
      DATA IDU/5/
C
      NTOP=MXLV
      ISTA=0
C  ALLOW FOR EMPTY SET OF BASIS FUNCTIONS, I.E., NSTATE.LE.0
      IF (NSTATE.LE.0) GOTO 3000
C  SET MXLV=0, CALL CHCK6I AND RETURN
      IF (NSTATE.LE.NTOP) GOTO 1000
C  GET PARAMETERS FOR BASIS FUNCTIONS FROM CHANNEL IASYMU (CALLED
C  IUNIT HERE)
C
C  IF NSTATE SET TOO LARGE, RESET TO MAX POSSIBLE
      IF (IPRINT.GE.1) WRITE(6,603) NSTATE,NTOP
  603 FORMAT(/'  INPUT NLEVEL =',I7,
     1       '  REPLACED BY MAX ALLOWED BY DIMENSIONS =',I5)
      NSTATE=NTOP
C
 1000 IF (IUNIT.GT.0) GOTO 1001
      WRITE(6,601) IUNIT,IDU
  601 FORMAT(/'  ILLEGAL UNIT =',I12,'  SPECIFIED FOR IASYMU, ',
     1       'CHANGED TO',I4)
      IUNIT=IDU
 1001 IF (IPRINT.GE.1) WRITE(6,602) IUNIT,NSTATE
  602 FORMAT(/'  ASYMMETRIC TOP BASIS WILL BE INPUT FROM UNIT IASYMU =',
     &       I4//'  NUMBER OF REQUESTED LEVELS, NLEVEL =',I4 )
C
      NL=0
C
C  CRLS 06-2022: USE A REASONABLE ESTIMATE FOR THE SIZE NEEDED FOR ATAU
      ALLOCATE (ATAU(NSTATE**2))
C
C  CYCLE OVER BASIS FUNCTIONS, READING IN LABELS AND ENERGIES FOR EACH
C  ONE.
!  Start of long DO loop #1
      DO III=1,NSTATE
        READ(IUNIT,*,END=9000) JI,ITAU,EIN
  500   FORMAT(2I5,F15.10)
C  N.B. ENERGY (EIN) NOT USED FOR IOS, KEPT FOR MOLSCAT COMPATIBILITY
        NL=NL+1
        JI=ABS(JI)
        NK=2*JI+1
C
C  CRLS 06-2022: EVERY TIME WE ARE ABOUT TO OVERFLOW SPACE ALLOWED FOR ATAU
C                1) ALLOCATE A TEMPORARY ARRAY THE SAME SIZE AS ATAU
C                2) COPY ATAU TO THE TEMPORARY ARRAY,
C                3) DEALLOCATE ATAU
C                4) REALLOCATE A LARGER-SIZED ATAU ARRAY
C                5) COPY THE CONTENTS OF THE TEMPORARY ARRAY BACK TO ATAU
C                6) DEALLOCATE THE TEMPORARY ARRAY
        NATAU=SIZE(ATAU)
        IF (NATAU.LT.ISTA+NK) THEN
          ALLOCATE (ATMP(NATAU))
          ATMP=ATAU
          DEALLOCATE (ATAU)
          ALLOCATE (ATAU(ISTA+NK))
          ATAU(1:NATAU)=ATMP
          DEALLOCATE (ATMP)
        ENDIF
        READ(IUNIT,*,END=9100) (ATAU(ISTA+I),I=1,NK)
  501   FORMAT(6F12.8)
        IF (IPRINT.GE.1) WRITE(6,604) NL,JI,ITAU
  604   FORMAT(/'  INPUT LEVEL',I4,'  J, TAU =',2I4)
        MJI=-JI
        IF (IPRINT.GE.1) WRITE(6,605) (ATAU(ISTA+1+JI+I),I,I=MJI,JI)
  605   FORMAT(10X,'INPUT COEFFICIENTS ARE'/(10X,6(F12.6,'(',I3,')')))
C
C  CHECK THAT THE PARITIES OF THE CURRENT SET OF LEVELS ARE CORRECT, IE
C  THAT: THE A COEFFICIENTS ARE NORMALIZED;
C        AND CHECK THAT THE A COEFFICIENTS OF THIS SET OF FUNCTIONS HAVE
C        CONSISTENT +/- AND EVEN/ODD PARITY
        IPAR=IPASYM(JI,NK,ATAU(ISTA+1))
        IF (IPAR.NE.-1) GOTO 2001
        IF (IPRINT.GE.1) WRITE(6,619)
  619   FORMAT(/'  *** ILLEGAL PARITY. BASIS FUNCTION REMOVED.')
        NL=NL-1
        CYCLE
C  ADD INDICES TO JSTATE. . .
 2001   JSTATE(1,NL)=JI
        JSTATE(2,NL)=ITAU
        JSTATE(3,NL)=IPAR
        JSTATE(4,NL)=ISTA
        ISTA=ISTA+NK
      ENDDO
!  End of long DO loop #1

C  END OF FILE  AND OTHER ERROR CONDITIONS
 9000 WRITE(6,606) IUNIT,NL
  606 FORMAT(/'  END OF FILE ON UNIT',I4,' AFTER',I4,' FUNCTIONS.')
      IF (IUNIT.NE.5) CLOSE(IUNIT)
      GOTO 2400

 9001 WRITE(6,607) MXA,NL
  607 FORMAT(/'  OUT OF ROOM IN ATAU MATRIX.  MXA, NSTATE =',2I6)
      NL=NL-1
      GOTO 2400

 9100 WRITE(6,608) NL
  608 FORMAT(/'  * * * ERROR.  END OF FILE BEFORE ATAU CARDS FOR LEVEL',
     &       I4,'. * * *   TERMINATING.')
      STOP
C
 2400 NSTATE=NL
 3000 MXA=ISTA
C
C  FINALLY, CHECK THAT IF TWO SETS OF LEVELS HAVE THE SAME VALUE OF JI
C  THEY ARE ORTHOGONAL TO EACH OTHER.
C
C  USE A TEMPORARY ARRAY JSTEMP TO SWOP ROWS AND COLUMNS IN JSTATE SO THAT
C  CAN USE THE SAME ROUTINE CHECK6 AS IS USED BY RASYMU
      ALLOCATE (JSTEMP(NSTATE,4))
      DO I=1,4
        DO J=1,NSTATE
          JSTEMP(J,I)=JSTATE(I,J)
        ENDDO
      ENDDO
      CALL CHECK6(NSTATE,JSTEMP,ATAU,.TRUE.)
      DEALLOCATE(JSTEMP)
      RETURN
      END
