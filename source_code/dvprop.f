      SUBROUTINE DVPROP(N,MXLAM,NHAM,
     1                  SR,VL,IV,EINT,CENT,L,NB,
     3                  RSTART,RSTOP,NSTEP,HH,NSTAB,
     4                  ERED,EP2RU,CM2RU,RSCALE,IPRINT)
C  Copyright (C) 2025 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential, ONLY: NCONST, NRSQ
C
C  DE VOGELAERE PROPAGATION (DOUBLE PRECISION)
C  THIS IS A CUT-DOWN VERSION OF PAUL MCGUIRE'S PROGRAM
C  INCLUDING STABILIZATION TO SUPPRESS CLOSED-CHANNEL GROWTH
C  THIS VERSION (JMH DEC 2018)
C  ACCEPTS AND RETURNS A LOG-DERIVATIVE MATRIX IN SR
C  BUT DOES NOT DO ITS OWN MATCHING TO SCATTERING BOUNDARY CONDITIONS
C
C  NSTAB  IS NUMBER OF STEPS TAKEN BEFORE STABILIZATION.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER L(N),NB(N),IV(*)
      INTEGER IPRINT
      ALLOCATABLE :: Y(:,:),YP(:,:),F(:,:),DIAG(:),A(:),P(:)
      DIMENSION SR(N*N),VL(*),EINT(N),CENT(N),R(4)
C
C  INDICES ON Y, YP, F ARE (ITH SOLN. COMP, NTH SOLN, KTH R VALUE)
C
C  COMMON BLOCK FOR CONTROL OF USE OF PROPAGATION SCRATCH FILE
      LOGICAL IREAD,IWRITE,IREADR,IWRITR
      COMMON /PRPSCR/ ESHIFT,ISCRU,ISCRUR,IREAD,IWRITE,IREADR,IWRITR
      IF (IREAD .AND. IPRINT.GE.5) WRITE(6,668)
  668 FORMAT(/'  DE VOGELAERE PROPAGATION WITH STORED POTENTIAL',
     1        ' MATRICES')
C
C  ZERO STORAGE . . .
C
      NP1=N+1
      NSQ=N*N

      ALLOCATE (Y(NSQ,4),F(NSQ,4),YP(NSQ,2))
      DO I=1,4
      DO IJ=1,NSQ
        Y(IJ,I)=0.D0
        F(IJ,I)=0.D0
      ENDDO
      ENDDO
C
C  SUBROUTINE DVSTRT REPLACED BY INITIALISATION FROM YINIT IN NOV 2018
C  SR CONTAINS INITIAL LOG-DERIVATIVE MATRIX:
C  COPY IT TO WAVEFUNCTION DERIVATIVE IN YP(IJ,1)
C
      DO IJ=1,NSQ
        YP(IJ,1)=SR(IJ)
        YP(IJ,2)=0.D0
        SR(IJ)=0.D0
      ENDDO
C
C  AND SET WAVEFUNCTION IN Y(IJ,2) TO UNIT MATRIX:
C  OFF-DIAGONAL ELEMENTS ALREADY ZERO
C
      IJ=1
      DO I=1,N
        Y(IJ,2)=1.D0
        IJ=IJ+NP1
      ENDDO
C
C  ********** START PROPAGATION **********
      NSTAB=MAX(NSTAB,1)
      H2=HH/2.D0
      R(2)=RSTART
C  GET F(,,2) FROM Y(,,2)
      R4=R(2)
C
      ALLOCATE (A(NSQ),DIAG(N),P(MXLAM+NCONST+NRSQ))
      IF (IREAD) THEN
        READ(ISCRU) A
        DO IJ=1,NSQ,NP1
          A(IJ)=A(IJ)-ESHIFT
        ENDDO
      ELSE
        CALL HAMMAT(A,N,R4,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1              RSCALE,DIAG,MXLAM,NHAM,IPRINT)
        IF (IWRITE) WRITE(ISCRU) A
      ENDIF
C
      CALL DSYMM('L','L',N,N,1.D0,A,N,Y(1,2),N,0.D0,F(1,2),N)
      CALL DAXPY(NSQ,1.D0,F(1,2),1,Y(1,2),1)
      CALL DAXPY(NSQ,-H2,YP(1,1),1,Y(1,1),1)
      CALL DAXPY(NSQ,0.5D0*H2*H2,F(1,2),1,Y(1,1),1)
C  GET F(,,1) FROM THIS Y(,,1).  NEEDS POTENTIAL AT R(1)
      R(1)=R(2)-H2
      R4=R(1)
C
      IF (IREAD) THEN
        READ(ISCRU) A
        DO IJ=1,NSQ,NP1
          A(IJ)=A(IJ)-ESHIFT
        ENDDO
      ELSE
        CALL HAMMAT(A,N,R4,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1              RSCALE,DIAG,MXLAM,NHAM,IPRINT)
        IF (IWRITE) WRITE(ISCRU) A
      ENDIF
C
      CALL DSYMM('L','L',N,N,1.D0,A,N,Y(1,1),N,0.D0,F(1,1),N)
C
C  **********      MAIN BODY OF ITERATION  **********
C  PROPAGATE FROM (-1/2) AND (0) TO (1/2) AND (1).
!  Start of long DO loop #1
      DO ISTEP=1,NSTEP
      CALL DAXPY(NSQ,1.D0,Y(1,2),1,Y(1,3),1)
      CALL DAXPY(NSQ,H2,YP(1,1),1,Y(1,3),1)
      CALL DAXPY(NSQ,H2*H2*4.D0/6.D0,F(1,2),1,Y(1,3),1)
      CALL DAXPY(NSQ,-H2*H2/6.D0,F(1,1),1,Y(1,3),1)
      R(3)=R(2)+H2
      R4=R(3)
C
      IF (IREAD) THEN
        READ(ISCRU) A
        DO IJ=1,NSQ,NP1
          A(IJ)=A(IJ)-ESHIFT
        ENDDO
      ELSE
        CALL HAMMAT(A,N,R4,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1              RSCALE,DIAG,MXLAM,NHAM,IPRINT)
        IF (IWRITE) WRITE(ISCRU) A
      ENDIF
C
      CALL DSYMM('L','L',N,N,1.D0,A,N,Y(1,3),N,0.D0,F(1,3),N)
      CALL DAXPY(NSQ,1.D0,Y(1,2),1,Y(1,4),1)
      CALL DAXPY(NSQ,HH,YP(1,1),1,Y(1,4),1)
      CALL DAXPY(NSQ,HH*HH/6.D0,F(1,2),1,Y(1,4),1)
      CALL DAXPY(NSQ,HH*HH/3.D0,F(1,3),1,Y(1,4),1)
      R(4)=R(3)+H2
      R4=R(4)
C
      IF (IREAD) THEN
        READ(ISCRU) A
        DO IJ=1,NSQ,NP1
          A(IJ)=A(IJ)-ESHIFT
        ENDDO
      ELSE
        CALL HAMMAT(A,N,R4,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1              RSCALE,DIAG,MXLAM,NHAM,IPRINT)
        IF (IWRITE) WRITE(ISCRU) A
      ENDIF
C
      CALL DSYMM('L','L',N,N,1.D0,A,N,Y(1,4),N,0.D0,F(1,4),N)
      CALL DAXPY(NSQ,1.D0,YP(1,1),1,YP(1,2),1)
      CALL DAXPY(NSQ,HH/6.D0,F(1,2),1,YP(1,2),1)
      CALL DAXPY(NSQ,HH/6.D0,F(1,4),1,YP(1,2),1)
      CALL DAXPY(NSQ,HH*4.D0/6.D0,F(1,3),1,YP(1,2),1)
C
C  **********      THIS ENDS DE VOGELAERE CYCLE    **********
C
      IF (ISTEP.EQ.NSTEP) CYCLE
C
C  ********** STABILIZATION EVERY NSTAB STEPS     **********
      IF (ISTEP-NSTAB*(ISTEP/NSTAB).EQ.0) THEN
        IF (IPRINT.GE.13) WRITE(6,673) R(4)
  673   FORMAT('  STABILIZATION DONE AT R =',E12.4)
C  FIRST 2 COLS OF Y AND F AND ALSO A USED AS SCRATCH IN STABIL.
        CALL STABIL(N,NB,Y(1,4),YP(1,2),F(1,3),F(1,4),
     &              A,Y(1,1),Y(1,2),F(1,1),F(1,2))
      ENDIF
C
C  **********   RE-INITIALIZE FOR NEXT CYCLE OF PROPAGATION *********
      R(1)=R(3)
      R(2)=R(4)
      CALL DCOPY(NSQ,YP(1,2),1,YP(1,1),1)
      CALL DCOPY(NSQ,Y(1,3),1,Y(1,1),1)
      CALL DCOPY(NSQ,Y(1,4),1,Y(1,2),1)
      CALL DCOPY(NSQ,F(1,3),1,F(1,1),1)
      CALL DCOPY(NSQ,F(1,4),1,F(1,2),1)
      DO IJ=1,NSQ
        YP(IJ,2)=0.D0
        Y(IJ,3)=0.D0
        Y(IJ,4)=0.D0
      ENDDO
C
      ENDDO
!  End of long DO loop #1
C
C  ********** ASYMPTOTIC REGION **********
C
C  Y(,4)  CONTAINS WAVEFUNCTION AT R4
C  YP(,2) CONTAINS WAVEFUNCTION DERIVATIVE AT R4
C  FORM LOG-DERIVATIVE Y IN SR
C    Y = PSI' PSI^-1 SO Y PSI = PSI' SO PSI^T Y^T = PSI'^T
C  TRANSPOSE PSI AND PSI'
      CALL DCOPY(NSQ,YP(1,2),1,SR,1)
      CALL TRNSP(SR,N)
      CALL TRNSP(Y(1,4),N)
      CALL DGESV(N,N,Y(1,4),N,Y,SR,N,IER)
      DEALLOCATE(Y,YP,F,DIAG,A,P)
      IF (IER.NE.0) THEN
        WRITE(6,689) IER
  689   FORMAT(' * * * ERROR. IER =',I2,' FROM DGESV IN DVPROP')
        STOP
      ENDIF
C
      IF (IWRITE) WRITE(ISCRU) IWRITE
      RETURN
      END
