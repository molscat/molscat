      SUBROUTINE DERMAT(IDER,W,N,R,P,VL,IV,CENT,
     1                  EP2RU,RSCALE,MXLAM,NHAM,IPRINT)
C  Copyright (C) 2025 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential, ONLY: CONLEN, NCONST, NRSQ
C
C  EVALUATES THE IDER'TH DERIVATIVE OF THE POTENTIAL AT RADIUS R
C  W = VCOUPL + VCENT
C  ORDER OF THE REAL SYMMETRIC MATRIX W IS N
C  THE FULL MATRIX IS COMPUTED
C  VL IS THE PREVIOUSLY COMPUTED MATRIX OF THE COUPLING POTENTIAL
C  IV IS AN INDEX ARRAY MAPPING P ONTO VL, SUCH THAT VL(I) IS
C        A COEFFICIENT TO MULTIPLY P(IV(I))
C  CENT(I) IS L*(L+1) FOR THE I-TH CHANNEL
C    (BUT VL ARRAY IS USED INSTEAD OF CENT IF NRSQ = 1)
C  EP2RU IS THE CONVERSION FACTOR FROM ENERGY IN EPSIL UNITS TO REDUCED ENERGY
C  IN (1/RUNIT**2) UNITS.
C  EP2RU = MUNIT*URED*RUNIT**2*EP2CM/BFCT, CONCEPTUALLY 2*MU/HBAR^2 IN
C  APPROPRIATE UNITS
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      SAVE RSAVE
C
      DIMENSION W(N,N),VL(*),IV(*),CENT(N),IDUM1(1),P(*)
      ALLOCATABLE :: P1(:),P2(:)
C
C  COMMON BLOCK FOR DERIVATIVES
      LOGICAL NUMDER
      COMMON /DERIVS/ NUMDER
C
C  STEP SIZE FOR NUMERICAL DERIVATIVES: SHOULD REALLY BE ADJUSTABLE
      PARAMETER (DEL = 1.D-3)
C
C  COMPUTE THE RADIAL PARTS OF THE POTENTIAL
C  IDUM1 AND IDUM2 ARE DUMMY ARGUMENTS HERE.
C
      IPDIM=MXLAM+NCONST+NRSQ
!  Start of long IF block #1
      IF (NUMDER) THEN
C
C  NUMERICAL DERIVATIVE OPTION.
C  NOTE THAT IF IDER = 2 THIS ASSUMES THAT
C  THE POTENTIAL ITSELF IS ALREADY IS ALREADY IN THE FIRST
C  MXLAM ELEMENTS OF P. THIS IS NOT TRUE IF DERMAT HAS BEEN
C  CALLED MORE RECENTLY THAN HAMMAT, SO THE IDER = 2 CALL
C  MUST PRECEDE THE IDER = 1 CALL.
C
C  FIRST SEE WHETHER DERMAT HAS BEEN CALLED BEFORE FOR THIS
C  VALUE OF R, AND IF SO SKIP POTENTIAL EVALUATIONS
C
C
        ALLOCATE (P1(IPDIM),P2(IPDIM))
        IF (R.NE.RSAVE) THEN
          RSAVE=R
          RR=R-DEL
          CALL POTENL(0,MXLAM,IDUM1,RR*RSCALE,P1,IDUM2,IPRINT)
          RR=R+DEL
          CALL POTENL(0,MXLAM,IDUM1,RR*RSCALE,P2,IDUM2,IPRINT)
        ENDIF
C
        DO I=1,MXLAM
          P1T=P1(I)
          P2T=P2(I)
          IF (IDER.EQ.1) P(I) = (P2T-P1T)/(2.D0*DEL)
          IF (IDER.EQ.2) P(I) = (P2T+P1T-2.D0*P(I))/(DEL*DEL)
        ENDDO
        DEALLOCATE (P1,P2)
C
      ELSE
        CALL POTENL(IDER,MXLAM,IDUM1,R*RSCALE,P,IDUM2,IPRINT)
      ENDIF
!  End of long IF block #1
C
C  CONVERT POTENTIAL P FROM EPSIL ENERGY UNITS TO REDUCED UNITS
C
      DO I=1,MXLAM
        P(I)=EP2RU*P(I)
      ENDDO
C
C  EXTEND ARRAY WITH ANY CONSTANT TERMS IN HAMILTONIAN (ZERO DERIV)
C
      DO I=1,NCONST
        P(MXLAM+I)=0.D0
      ENDDO
C
C  PROCESS ANY POTENTIAL SCALING FACTOR IN SCALAM
C
      CALL SCAPOT(P,MXLAM)
C
C  NEXT LINE NEEDED ONLY FOR BOUND, WHICH DOES NOT AT PRESENT (12/19)
C  USE PROPAGATORS THAT CALL DERMAT
C     CALL PERTRB(R*RSCALE,P,MXLAM+NCONST+NRSQ,0)
C
C  EXTEND ARRAY WITH ANY TERMS THAT MULTIPLY 1/R**2
C
      IF (NRSQ.GT.0) THEN
        IF (IDER.EQ.1) RSQ=-2.D0/R**3
        IF (IDER.EQ.2) RSQ=+6.D0/R**4
        RSQ=MIN(RSQ,1.D16)
        DO I=1,NRSQ
          P(MXLAM+NCONST+I)=RSQ
        ENDDO
      ENDIF
C
      CALL SUMLAM(VL,P,IV,W,N,NHAM)
C
C  NOW COMPUTE THE DIAGONAL CONTRIBUTIONS W(I,I).
C
      IF (NRSQ.EQ.0) THEN
        DO I=1,N
          W(I,I) = W(I,I) + RSQ*CENT(I)
        ENDDO
      ENDIF
C
C  ADD SPHERICAL HARMONIC CONFINEMENT IF PRESENT
C
      IF (CONLEN.GT.0.D0) THEN
        IF (IDER.EQ.1) RSQ=2.D0*R/CONLEN**4
        IF (IDER.EQ.2) RSQ=2.D0/CONLEN**4
        DO I=1,N
          W(I,I) = W(I,I) + RSQ
        ENDDO
      ENDIF
C
      RETURN
      END
