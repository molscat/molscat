      SUBROUTINE IOSDRV(NNRG,NPR,ENERGY,JTOTL,JTOTU,JSTEP,TEST,NCAC,
     1                  IFLS,LINE,LTYPE,ITYPE,LMAX,MMAX,
     2                  IPROGM,URED,LABEL,IREF,IPOT,
     3                  LAMBDA,MXLAM,NHAM,IRMSET,IRXSET,RVFAC,
     4                  IPRINT,NVC,ISAVEU,TITIME,RUNIT,RMIN,
     5                  RMAX,MONQN,IBOUND,WAVE,ERED,EP2RU,CM2RU,RSCALE,
     6                  DRMAX,NSTAB,ILDSVU)
C  Copyright (C) 2020 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  INTEGRATED MOLSCAT/IOS IMPLEMENTED APR 86 CAMBRIDGE, ENGLAND.
C   -- A GUTTED VERSION OF IOS1, INTERFACED TO CCP6 MOLSCAT.
C  THIS IS A DRIVER FOR THE IOS CODE;  MOLSCAT/DRIVER CALLS
C  BASIN TO READ &BASIS, WHICH THEN CALLS IOSBIN.
C  DRIVER THEN CALLS POTENL TO GET &POTL DATA,
C  AND FINALLY CALLS IOSDRV TO SET UP AND PERFORM IOS CALCULATION.
C
      INTEGER IPRINT
      DIMENSION ENERGY(NNRG),TEST(2),LINE(*),LTYPE(*),
     1          LAMBDA(MXLAM),MONQN(*)
      LOGICAL WAVE
      CHARACTER(80) LABEL
      ALLOCATABLE VLI(:),PWGHT(:,:),LM(:,:),IXQL(:,:)
C
C  LAST CHANGED 1/19/93.   NEW DYNAMIC MEMORY HANDLING
C  ** VERSION 6 / OCT  85/ ADDS ITYPE=6 CAPABILITY
C                        / ALSO ALLOWS "UNEXPANDED" POTL, V(R,ANGLES)
C  ** VERSION 5 / MAR  81/ ADDS INTFLG=4 (MOLSCAT V.8)
C               / JUNE 82/ REPLACES PLM WITH R. T PACK VERSION.
C  ** VERSION 4 / MAY. 78/ ADDS ITYPE=5 CODE.
C               / SEP. 78/ **TEMPORARY** ISAVEU CAPABILITY
C               / APR. 79/ CHANGED FOR ISCRU (MOLSCAT V.7) COMPATABIL
C  ** VERSION 3 / DEC. 77/ IS TOTALLY NEW ORGANIZATION TO ACCOMMODATE
C                          ITYPE=2 (VIBROTOR - ATOM)
C  ** VERSION 2 / OCT. 77/ ADDS WKB (R.T PACK) CAPABILITY **
C  ** VERSION 1 / SEP. 77/ INTERFACE HOUSTON PROGRAM W/MOLSCAT.
C

      COMMON /VLFLAG/ IVLFL

      WRITE(6,68)
   68 FORMAT(/'  IOSDRV ENTERED.  SET-UP FOR INFINITE ORDER SUDDEN',
     1       ' CALCULATION.')
C
C  CONTINUE WITH SET-UP FOR IOS.  PROCESS &POTL LAM(MXLAM) DATA
C  SET NGPT, LMAX AND GAUSS PTS/WTS.
C  N.B. LMAX/MMAX INITIALLY CONTAIN HIGHEST L,M VALUES
C  DESIRED FOR QLM.  LMAX IS RESET TO EQUAL THE *NUMBER* OF L,M
C  VALUES IN LM,SLLR,SLLI,ETC.
      CALL IOSBGP(MXLAM,LAMBDA,MXXXXL,NGPT,LMAX,MMAX,NQL,NIXQL)
C
      NV=NVC*(NVC+1)*NHAM/2
C
C  SET UP PWGHT, VLI TABLES  -  ALSO IXQL TABLE
C
      ALLOCATE (VLI(MXXXXL*NGPT),PWGHT(NGPT,LMAX),LM(3,LMAX),
     1          IXQL(NIXQL,NQL))
      CALL IOSB1(PWGHT,VLI,IXQL,LM,NGPT,LMAX,MXXXXL,NIXQL,NQL)
C
      IF (ISAVEU.LE.0)  GOTO 3000
C
C  ISAVEU OUTPUT -- MAY 92 VERSION
C
      WRITE(6,3600) ISAVEU
 3600 FORMAT(///'  QLS/QLT SAVED (MAY 92 FORMAT) ON UNIT ISAVEU =',I3)
      IPOUT=100+IPROGM
      ITOUT=100+ITYPE-100*(ITYPE/100)
      WRITE(ISAVEU,3601) LABEL,ITOUT,NVC,NQL,URED,IPOUT
 3601 FORMAT(2X,A80/3I4,F8.4,I4)
C
 3000 CALL GCLOCK(TJTIME)
      TIME=TJTIME-TITIME
      WRITE(6,640) TIME
  640 FORMAT(/'  TIME TO SET UP CALCULATION WAS',F8.2,
     1       '   SECONDS.  EXIT IOSDRV')
      WRITE(6,69)
   69 FORMAT(/30('===='))
C
C  PASS CONTROL TO IOSCLC TO DO CALCULATION.
C
      CALL IOSCLC(NNRG,ENERGY,JTOTL,JTOTU,JSTEP,IPRINT,ISAVEU,
     1            ITYPE,RMIN,RMAX,IRMSET,IRXSET,RVFAC,
     2            NCAC,TEST,RUNIT,NVC,LMAX,NGPT,NQL,
     3            NIXQL,MXXXXL,LAMBDA,MXLAM,NHAM,
     4            VLI,PWGHT,LM,IXQL,
     8            IREF,IFLS,LINE,LTYPE,IPOT,MONQN,IBOUND,WAVE,
     9            ERED,EP2RU,CM2RU,RSCALE,DRMAX,NSTAB,ILDSVU)
C
C  RELEASE STORAGE USED BY IOSDRV/IOSCLC/SCCTRL
      DEALLOCATE (VLI,PWGHT,LM,IXQL)
      RETURN
      END
