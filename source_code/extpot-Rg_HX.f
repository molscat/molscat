      FUNCTION EXTPOT(R,COSTH)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE physical_constants
C
C  SUBROUTINE TO EVALUATE HFD-TYPE FITTING POTENTIAL WITH
C  POINT-CHARGE + QUADRUPOLE INDUCTION ENERGY
C  FOR RARE GAS - HYDROGEN HALIDE SYSTEMS
C  (FUNCTIONAL FORMS H4, H5 AND H6 AND ETA-DEPENDENT VARIANTS)
C
C  VERSION OF JUNE 1991 (INCORPORATING COM SHIFT)
C  NOTE THAT THE VPRM ARRAY IS NOT COMPATIBLE WITH EARLIER
C  VERSIONS - THIS ONE USES 9:12 INSTEAD OF 5:8 FOR BETA, SO THAT
C  5:8 ARE AVAILABLE FOR HIGHER-ORDER LEGENDRE TERMS IN EPSILON.
C
C  SEE J. M. HUTSON, J. CHEM. PHYS. 96, 6752 (1992) (AR-HF)
C               AND  J. PHYS. CHEM. 96, 4237 (1992) (AR-HCL)
C  FOR EARLIER WORK INVOLVING RELATED POTENTIAL FORMS, SEE
C                    J. CHEM. PHYS. 89, 4550 (1988);
C                    J. CHEM. PHYS. 91, 4448 (1989);
C                    J. CHEM. PHYS. 91, 4455 (1989).
C
C  FUNCTION BY J. M. HUTSON, DEPARTMENT OF CHEMISTRY,
C  UNIVERSITY OF DURHAM, DURHAM, DH1 3LE, ENGLAND.
C  ELECTRONIC MAIL:   J.M.HUTSON @ DURHAM.AC.UK
C
C  THIS FUNCTION EVALUATES THE AR-HX ETA-DEPENDENT POTENTIALS FOR A
C  SINGLE VALUE OF THE INTERMOLECULAR DISTANCE (R) AND
C  COSINE THETA (COSTH).
C  R MUST BE SUPPLIED IN ANGSTROMS AND THE RESULT IS RETURNED
C  IN CM-1, BUT INTERNAL WORKINGS ARE IN ATOMIC UNITS.
C
C  ON ENTRY, THE POTENTIAL PARAMETERS MUST BE IN COMMON BLOCK POTL.
C  THE ETA=0, LINEAR AND QUADRATIC TERMS ARE IN SEPARATE BLOCKS OF 16.
C  FOR THE H6 POTENTIAL, THE BLOCKS ARE:
C    VPRM(1:8)   ARE LEGENDRE COMPONENTS OF WELL DEPTH EPS
C    VPRM(9:12)  ARE LEGENDRE COMPONENTS OF EXPONENT BETA
C    VPRM(13:16) ARE LEGENDRE COMPONENTS OF MINIMUM DISTANCE RE
C    VPRM(17:32) AND VPRM(33:48) REPEAT THE PATTERN FOR LINEAR AND
C                   QUADRATIC TERMS IN ETA
C    VPRM(49)    IS GAMMA (IE FIXED C10/C8 RATIO)
C  THE VPRM ARRAY IS USED SLIGHTLY DIFFERENTLY FOR THE H4 AND H5 FORMS.
C
C  THE REQUIRED VALUE OF ETA MUST BE IN COMMON BLOCK VIB
C
C  SUBROUTINE EXTINT MUST BE CALLED BEFORE EXTPOT (ONCE IN EACH
C  PROGRAM RUN, OR FOR EACH NEW SET OF POTENTIAL PARAMETERS)
C  TO READ LONG-RANGE PARAMETERS AND SET UP COMMON BLOCKS POL AND DISP.
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      SAVE
      DIMENSION P(10),DP(10)
      DIMENSION POT(16),DIPV(3),QUADV(3),C6V(3),ALAVV(3),DALPV(3),
     1          APARV(3),APERPV(3)
C
      COMMON /POTL / VPRM(100)
      COMMON /POL  / ALPRG,DIP,QUAD,RHX,RCM
      COMMON /DISP / C6,DA,F1,F3
      COMMON /VIB  / ETA
C
      DATA IPOT   /6/
      DATA ETLAST /-999.D0/
C     DATA AUE    /219474.6354D0/
C     DATA AUR    /0.52917706D0/
C
C  20-09-2016: UPDATED TO USE MODULE (physical_constants) THAT CONTAINS
C              CONSISTENT AND UP-TO-DATE VALUES
      PARAMETER (AUE=hartree_in_inv_cm, AUR=bohr_to_Angstrom)
C
      F(COSTH,WIDTH)=1.D0-(0.5D0*(1.D0+COSTH))**WIDTH
C
C  CHECK IF ETA HAS CHANGED, AND RECALCULATE POT IF NECESSARY
C
      IF (ETA.NE.ETLAST) THEN
        DO 50 I=1,16
          POT(I)=VPRM(I)+ETA*(VPRM(I+16)+ETA*VPRM(I+32))
   50   CONTINUE
        GAMMA=VPRM(49)
C
C  LONG-RANGE COEFFICIENTS
C
        DIP=DIPV(1)+ETA*(DIPV(2)+ETA*DIPV(3))
        QUAD=QUADV(1)+ETA*(QUADV(2)+ETA*QUADV(3))
        C6=C6V(1)+ETA*(C6V(2)+ETA*C6V(3))
        ALAV=ALAVV(1)+ETA*(ALAVV(2)+ETA*ALAVV(3))
        DALPHA=DALPV(1)+ETA*(DALPV(2)+ETA*DALPV(3))
        APAR=APARV(1)+ETA*(APARV(2)+ETA*APARV(3))
        APERP=APERPV(1)+ETA*(APERPV(2)+ETA*APERPV(3))
        DA=DALPHA/(3.D0*ALAV)
        F1=4.D0*APERP/ALAV
        F3=(2.D0*APAR-8.D0*APERP/3.D0)/ALAV
        ETLAST=ETA
      ENDIF
C
C  CHECK THAT THE LONG-RANGE PARAMETERS HAVE BEEN INITIALISED.
C  THIS IS NOT ENTIRELY FOOLPROOF, BUT IT IS A USEFUL CHECK.
C
      IF (C6.LE.0.D0 .OR. ALPRG.LE.0.D0) GOTO 998
C
C  TRANSFORM INPUT R AND COS THETA TO COORDINATE SYSTEM FOR
C  REFERENCE ISOTOPIC SPECIES
C
      T=-CMSHFT/R
      TEMP=SQRT(1.D0+T*(T+COSTH+COSTH))
      RPRIME=R*TEMP
      CSTHPR=(COSTH+T)/TEMP
C
      IF (CSTHPR.EQ.CLAST .AND. POT(1).EQ.EPLAST) GOTO 100
      CLAST=CSTHPR
      EPLAST=POT(1)
C
C  EVALUATE WELL DEPTH AND RMIN: LEGENDRE SUM OR F(COS THETA)
C
      IF (IPOT.EQ.5) THEN
        EPS=POT(1)+POT(2)*F(CSTHPR,POT(3))
        IF (CSTHPR.LT.0.D0) EPS=EPS+POT(4)*CSTHPR*CSTHPR
      ELSE
        EPS=SUMLEG(POT(1),5,CSTHPR)
      ENDIF
      BETA=SUMLEG(POT(9),4,CSTHPR)
      IF (IPOT.LE.5) THEN
        RE=POT(13)+POT(14)*F(CSTHPR,POT(15))
        IF (CSTHPR.LT.0.D0) RE=RE+POT(16)*CSTHPR*CSTHPR
      ELSE
        RE=SUMLEG(POT(13),4,CSTHPR)
      ENDIF
C
C  CONVERT EVERYTHING INTO ATOMIC UNITS
C
      EPS=EPS/AUE
      BETA=BETA*AUR
      RE=RE/AUR
C
C  GET A AND C8 FROM EPS, BETA AND RE
C  THIS IS A GENERALISATION OF THE PROCEDURE DESCRIBED IN
C  THE APPENDIX TO J. M. HUTSON, J. CHEM. PHYS. 89, 4550 (1988)
C
      CALL GETAC(EPS,RE,CSTHPR,0.D0,BETA,C6,DA,F1,F3,GAMMA,A,C8)
C
  100 RR=RPRIME/AUR
C
C  GET LEADING TERMS IN INDUCTION POTENTIAL
C
      V=VIND(RR,CSTHPR)
C
C  GET DISPERSION DAMPING FUNCTIONS AND DERIVATIVES
C
      CALL DAMP(6,10,BETA,RR,P,DP)
C
C  NOW EVALUATE THE POTENTIAL ITSELF
C
      R2=RR*RR
      R6=R2**3
      V6=-C6/R6*(1.D0+DA*(1.5D0*CSTHPR*CSTHPR-0.5D0))
      R7=RR*R6
      V7=-C6/R7*CSTHPR*(F1+F3*CSTHPR*CSTHPR)
      V=V+V6*P(6)+V7*P(7)
      V=V+A*EXP(-BETA*RR)-C8*(P(8)+GAMMA*P(10)/R2)/(R6*R2)
      EXTPOT=V*AUE
      RETURN
C
  998 WRITE(6,999)
  999 FORMAT(' *** ERROR IN SUBROUTINE EXTPOT.'/
     1       ' EXTINT MUST BE CALLED BEFORE EXTPOT TO READ PARAMETERS',
     2       ' FOR LONG-RANGE POTENTIAL.')
      STOP
C===========================================================================
      ENTRY EXTINT
C
C  READ INDUCTION AND DISPERSION PARAMETERS (IN ATOMIC UNITS)
C  AND PROCESS THEM A LITTLE.
C  IPOT IS THE NUMERIC PART OF THE FUNCTION NAME (H4, H5, H6)
C
      READ(5,*) IPOT
      READ(5,*) ALPRG
      READ(5,*) (DIPV(I),I=1,3)
      READ(5,*) (QUADV(I),I=1,3)
      READ(5,*) (C6V(I),I=1,3)
      READ(5,*) (ALAVV(I),I=1,3)
      READ(5,*) (DALPV(I),I=1,3)
      READ(5,*) (APARV(I),I=1,3)
      READ(5,*) (APERPV(I),I=1,3)
      READ(5,*) CMSHFT,RHX,RCM
C
      WRITE(6,601) ALPRG,DIPV,QUADV,C6V,ALAVV,DALPV,APARV,APERPV
  601 FORMAT('  INDUCTION AND DISPERSION PARAMETERS (ATOMIC UNITS):'/
     1       '  RARE GAS POLARIZABILITY',T40,F8.3/
     2       '  DIATOM DIPOLE MOMENT',T40,3F8.4/
     3       '  DIATOM QUADRUPOLE MOMENT',T40,3F8.3/
     4       '  ISOTROPIC C6 COEFFICIENT',T40,3F8.3/
     5       '  DIATOM ISOTROPIC POLARIZABILITY',T40,3F8.3/
     6       '  DIATOM POLARIZABILITY ANISOTROPY',T40,3F8.3/
     7       '  DIATOM PARALLEL A TENSOR',T40,3F8.3/
     8       '  DIATOM PERPENDICULAR A TENSOR',T40,3F8.3)
C
      WRITE(6,602) RHX,CMSHFT,RCM
  602 FORMAT('  DIATOM BOND LENGTH',T45,F8.6,' A'/
     1       '  DIATOM CENTRE OF MASS SHIFT',T45,F8.6,' A'/
     2       '  DISTANCE FROM X ATOM TO QUADRUPOLE ORIGIN',T45,F8.6,
     3       ' A')
      RHX=RHX/AUR
      RCM=RCM/AUR
      RETURN
      END
C===========================================================================
      SUBROUTINE GETAC(EPS,R,COST,T,BETA,C6,DA,F1,F3,GAMMA,A,C8)
C
C  SUBROUTINE TO CALCULATE A AND C8 REQUIRED TO GIVE SPECIFIED
C  WELL DEPTH AND RMIN FOR GIVEN BETA AND DISPERSION AND
C  INDUCTION INTERACTIONS UP TO 1/R**7.
C  ON INPUT:
C    DA    = (ALPHA(PAR)-ALPHA(PERP))/(ALPHA(PAR)+2*ALPHA(PERP))
C    F1    = 4 A(PERP) / ALPHA(AV)
C    F3    = (2 A(PAR) - 8 A(PERP)/3) / ALPHA(AV)
C  COMMON BLOCK POL MUST CONTAIN MULTIPOLES AND POLARIZABILITIES
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      LOGICAL NOC8
      DIMENSION P(10),DP(10)
      DATA NOC8/.FALSE./
C
      TEMP=SQRT(1.D0 + T*T + 2.D0*T*COST)
      RPRIME=R*TEMP
      CSTHPR=(COST+T)/TEMP
      CALL DVIND(RPRIME,CSTHPR,V,DV)
      CALL DAMP(6,10,BETA,RPRIME,P,DP)
      R2=RPRIME*RPRIME
      R6=R2**3
      V6=-C6/R6*(1.D0+DA*(1.5D0*CSTHPR*CSTHPR-0.5D0))
      V7=-C6/(RPRIME*R6)*CSTHPR*(F1+F3*CSTHPR*CSTHPR)
      V=V+V6*P(6)+V7*P(7)
      DV=DV+V6*(DP(6)-6.D0*P(6)/RPRIME)
     1     +V7*(DP(7)-7.D0*P(7)/RPRIME)
C
      IF (NOC8) GOTO 200
      R8=R6*R2
      C8=(BETA*(EPS+V)+DV)/((DP(8)-P(8)*(8.D0/RPRIME-BETA))
     1   +(DP(10)-P(10)*(10.D0/RPRIME-BETA))*GAMMA/R2)*R8
      A=(C8*(P(8)+GAMMA*P(10)/R2)/R8-EPS-V)*EXP(BETA*RPRIME)
      RETURN
C
C  SPECIAL CASE OF C8=0, IGNORING INPUT EPS
C
  200 C8=0.D0
      A=DV/BETA*EXP(BETA*RPRIME)
      RETURN
      END
C==========================================================================
      SUBROUTINE DVIND(R,COSTH,V,DV)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DATA EPS /1.D-4/
      V=VIND(R,COSTH)
      DV=(VIND(R+EPS,COSTH)-VIND(R-EPS,COSTH))/(EPS+EPS)
      RETURN
      END
C==========================================================================
      FUNCTION VIND(R,COSTH)
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION Q(2),RQ(2)
      COMMON /POL   / ALPRG,DIP,QUAD,RHX,RCM
C
C  THIS ROUTINE WORKS ENTIRELY IN ATOMIC UNITS.
C
C  THE EXPRESSION FOR VIND CAN BECOME UNPHYSICALLY NEGATIVE
C  AT VERY SMALL R. TO SUPPRESS THIS, LIMIT HOW SMALL AN R
C  IS USED IN THE ACTUAL CALCULATION (ADDED BY JMH, 22 MAY 2002).
C  THIS SLIGHTLY AWKWARD APPROACH ENSURES THAT V IS CONTINUOUS
C
      RR=R
      RCUT=3.8D0
      IF (R.LT.RCUT) RR=RCUT+0.01D0*(R-RCUT)
C
      PI=ACOS(-1.D0)
C
      NCHARG=2
      Q(1)=DIP/RHX
      Q(2)=-Q(1)
      RQ(1)=RHX-RCM
      RQ(2)=-RCM
      QUADP=QUAD-(Q(1)*RQ(1)*RQ(1)+Q(2)*RQ(2)*RQ(2))
C
      SINTH=SQRT(1.0D0-COSTH*COSTH)
C
      EZ=0.0D0
      EX=0.0D0
      DO 200 NC=1,NCHARG
        RRGQ=SQRT(RR*RR+RQ(NC)*RQ(NC)-2.0D0*RR*RQ(NC)*COSTH)
        SINE=SINTH*RQ(NC)/RRGQ
        COSN=SQRT(1.0D0-SINE*SINE)
        FAC=Q(NC)/(RRGQ*RRGQ)
        EZ=EZ+FAC*COSN
        EX=EX+FAC*SINE
        IF (NC.NE.2) GOTO 200
        COSIN=(RR*COSTH-RQ(2))/RRGQ
C  GUARD AGAINST COSIN>1 (IF CONSTRUCT ADDED BY JMH, 22 MAY 2002)
        IF (ABS(COSIN).GE.1.D0) THEN
          SININ=0.D0
        ELSE
          SININ=SQRT(1.0D0-COSIN*COSIN)
        ENDIF
        EQUAD=1.5D0*QUADP*(3.D0*COSIN*COSIN-1.D0)/RRGQ**4
        EZ=EZ+EQUAD*COSN
        EX=EX+EQUAD*SINE
        EQUAD=3.0D0*QUADP*SININ*COSIN/RRGQ**4
C  NOTE THAT THE NEXT LINE SHOULD IN FACT READ
C  EZ=EZ-EQUAD*SINE
C  HOWEVER, THE FITS TO OBTAIN THE AR-HF H6(4,3,2) AND
C  AR-HCL H6(4,3,0) POTENTIALS USED THE ROUTINE WITH THE
C  (INCORRECT) POSITIVE SIGN, SO THE POTENTIAL THAT FITS THE DATA
C  IS ACTUALLY DEFINED WITH THE POSITIVE SIGN. ALL SUBSEQUENT
C  CALCULATIONS THAT I KNOW ABOUT HAVE USED THIS ROUTINE.
        EZ=EZ+EQUAD*SINE
        EX=EX+EQUAD*COSN
  200 CONTINUE
C
      DIPZ=EZ*ALPRG
      DIPX=EX*ALPRG
      EIND=-0.5D0*(EZ*DIPZ+EX*DIPX)
      VIND=EIND
C
      RETURN
      END
