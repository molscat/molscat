      SUBROUTINE YTOKG(NB,N,NOPEN,SJ,SJP,SN,SNP,Y,KMTRIX)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  ROUTINE TO OBTAIN THE K MATRIX FROM THE LOG DERIVATIVE MATRIX
C  ON ENTRY:  Y HOLDS THE LOG DERIVATIVE MATRIX
C             SJ,SJP HOLD RICCATI-BESSEL FUNCTIONS AND DERIVATIVES
C             SN,SNP HOLD MODIFIED SPHERICAL BESSEL FUNCTIONS
C             NB HOLDS THE MAPPING FROM THRESHOLDS TO ENERGY ORDERED
C             THRESHOLDS
C  ON EXIT,   KMTRIX HOLDS THE K MATRIX
C             T IS WORK SPACE
C  SEE: B.R.JOHNSON, JOURNAL OF COMPUTATIONAL PHYSICS 13, 445 (1973)
C
      DOUBLE PRECISION KMTRIX
      DIMENSION NB(N), SJ(N), SJP(N), SN(N), SNP(N),
     1          Y(N,N), KMTRIX(1)
      ALLOCATABLE :: T(:,:)
C
      CALL DSYFIL('U',N,Y,N)
C
C  GENERATE THE MATRIX IN SECOND SQUARE BRACKET OF EQN 18
      ALLOCATE (T(N,NOPEN))
      DO J = 1,NOPEN
        NXJ = NB(J)
      DO I = 1,N
        NXI = NB(I)
        T(I,J) = Y(NXI,NXJ)*SJ(NXJ)
      ENDDO
      ENDDO
C
      DO I = 1,NOPEN
        T(I,I) = T(I,I) - SJP(NB(I))
      ENDDO
C
C  GENERATE THE MATRIX IN FIRST SQUARE BRACKET OF EQN 18
C  KMTRIX IS NOTIONALLY N X N HERE
      IND = 0
      DO J = 1,N
        NXJ = NB(J)
      DO I = 1,N
        IND = IND + 1
        NXI = NB(I)
        KMTRIX(IND) = Y(NXI,NXJ)*SN(NXJ)
      ENDDO
      ENDDO
C
      IND = - N
      DO I = 1,N
        IND = IND + N + 1
        KMTRIX(IND) = KMTRIX(IND) - SNP(NB(I))
      ENDDO
C
C  COMPUTE SOLUTION TO EQN 18 AND PLACE IN T
      CALL DGESV(N,NOPEN,KMTRIX,N,SJ,T,N,IER)
      IF (IER.NE.0) THEN
        WRITE(6,900) IER
 900    FORMAT(/' ***** ERROR IN LINEAR EQUATION SOLVER IN YTOKG.',
     1         '  IER =',I4,'.  RUN HALTED.')
        STOP
      ENDIF
C
C  COPY NOPEN X NOPEN PART OF SOLUTION INTO KMTRIX
      IND = 0
      DO J = 1,NOPEN
      DO I = 1,NOPEN
        IND = IND + 1
        KMTRIX(IND) = T(I,J)
      ENDDO
      ENDDO
      DEALLOCATE (T)
C
C  KMTRIX NOW HOLDS THE K MATRIX.  FORCE SYMMETRY ON IT.
C
      CALL KSYM(KMTRIX, NOPEN)
      RETURN
C
      END
