      SUBROUTINE RMPROP(N,NSQ,MXLAM,NHAM,
     1                  RMAT,VECNOW,Q,VL,IV,EINT,CENT,P,
     2                  W,VECOLD,EIGOLD,EIGNOW,DIAG,R1,R2,
     3                  R3,R4,N1,N2,
     4                  RSTART,RSTOP,NSTEP,DR,POW,
     5                  ERED,EP2RU,CM2RU,RSCALE,IPRINT)
C  Copyright (C) 2020 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  THIS ROUTINE SOLVES THE COUPLED EQUATIONS USING THE
C  R-MATRIX PROPAGATOR METHOD
C  OF STECHEL, WALKER & LIGHT, J. CHEM. PHYS. 69, 3518 (1978)
C  THIS VERSION USES A CONSTANT STEP SIZE DR FOR R < RMID, AND
C  DR*R/RMID FOR R > RMID. THE CONSTANT-STEP METHOD IS RECOMMENDED BY
C  ANDERSON, J. CHEM. PHYS. 77, 4431 (1982).
C
C  THIS ROUTINE MODIFIED BY J. M. HUTSON, S. GREEN AND C. R. LE SUEUR
C  ALTHOUGH IT PROPAGATES THE R MATRIX, IN AN ADIABATIC REPRESENTATION,
C  THE QUANTITY ACCEPTED AND RETURNED IN THE ARRAY RMAT
C  IS THE LOG-DERIVATIVE MATRIX IN THE FREE (PRIMITIVE) BASIS SET,
C  WHICH IS THE INVERSE OF THE R MATRIX.
C
C  THIS VERSION DOES NOT USE VTOL, MAXSTP, XEPS
C  DOES NOT ASSIGN RTURN, AND DOES NOT RETURN IK FOR RMSET
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER IPRINT
      DIMENSION RMAT(NSQ),VECNOW(NSQ),Q(NSQ),VL(*),IV(*),
     1          EINT(N),CENT(N),
     2          P(MXLAM),W(NSQ),VECOLD(NSQ),EIGOLD(N),
     3          EIGNOW(N),DIAG(N),R1(N),R2(N),R3(N),
     4          R4(N),N1(*),N2(*)
C
C  COMMON BLOCK FOR CONTROL OF USE OF PROPAGATION SCRATCH FILE
      LOGICAL IREAD,IWRITE
      COMMON /PRPSCR/ ESHIFT,ISCRU,IREAD,IWRITE
C
C  ----------------------------------------------------------------
C  SET UP TO USE UNIT  (ISCRU)
      ICODE=0
      IF (IREAD) ICODE=2
      IF (IWRITE) ICODE=1
C  ---------------------------------------------------------------
C
      RTMP=RSTOP
C  INVERT SUPPLIED LOG-DERIVATIVE MATRIX TO GET R MATRIX
C
      CALL SYMINV(RMAT,N,N,IFAIL)
      IF (IFAIL.GT.N) GOTO 480
C
C  ICODE=1/2 MEANS PROPAGATION ISN'T/IS TO BE DONE WITH STORED DATA
      IF (ICODE.EQ.1 .AND. IPRINT.GE.4) WRITE(6,150) RSTART
  150 FORMAT(/'  START R-MATRIX PROPAGATOR AT RSTART =',F8.5)
      IF (IPRINT.GE.20 .AND. ICODE.EQ.1) WRITE(6,160)
  160 FORMAT(/'    KSTEP    RNOW       EIGVAL(1)      EIGVAL(N)',3X,
     1        '(UNITS OF EPSIL)'/)
C
      IF (IREAD) GOTO 170
C
      RNOW=RSTART+0.5D0*DR
      DRNOW=DR
      DRNEW=DRNOW
      CALL WAVMAT(W,N,RNOW,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1            RSCALE,DIAG,MXLAM,NHAM,IPRINT)
      IFAIL=0
      CALL DIAGVC(W,N,N,EIGOLD,VECOLD)
      IF (ISCRU.LE.0) GOTO 190

      WRITE(ISCRU) RNOW,DRNOW,NSTEP
      WRITE(ISCRU) EIGOLD,VECOLD
      WRITE(ISCRU) DRNEW
      GOTO 190
C
  170 READ(ISCRU) RNOW,DRNOW,NSTEP
      READ(ISCRU) EIGOLD,VECOLD
      READ(ISCRU) DRNEW
      DO 180 I=1,N
        EIGOLD(I)=EIGOLD(I)-ESHIFT
  180 CONTINUE
C
C  TRANSFORM SUPPLIED R MATRIX TO LOCAL BASIS
C
  190 CALL TRNSFM(VECOLD,RMAT,Q,N,.FALSE.,.TRUE.)
C
      RNOW=RSTART-0.5D0*DRNOW

C  PROPAGATE R MATRIX
      DO 430 KSTEP=1,NSTEP
        RNOW=RNOW+0.5D0*(DRNOW+DRNEW)
        DRNOW=DRNEW
        IF (.NOT.IREAD) GOTO 250
C
C  IF ICODE = 2 READ EIGNOW AND Q MATRIX FROM DISK
        READ(ISCRU) EIGNOW,Q
        DO 240 I=1,N
          EIGNOW(I)=EIGNOW(I)-ESHIFT
  240   CONTINUE
        GOTO 290
C
C  IF ICODE = 1 CALCULATE EIGNOW AND Q MATRIX
  250   CALL WAVMAT(W,N,RNOW,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1              RSCALE,DIAG,MXLAM,NHAM,IPRINT)
C
        IFAIL=0
        CALL DIAGVC(W,N,N,EIGNOW,VECNOW)
        CALL SGNCHK(VECOLD,VECNOW,N)
        CALL DGEMUL(VECOLD,N,'T',VECNOW,N,'N',Q,N,
     1              N,N,N)
        IF (ISCRU.GT.0) WRITE(ISCRU) EIGNOW,Q
        DO 280 I=1,NSQ
  280     VECOLD(I)=VECNOW(I)
  290   CONTINUE
C
C  CALCULATE PROPAGATOR FOR R MATRIX.
        NOPLOC=0
        DO 320 I=1,N
          EIG=EIGNOW(I)
          FLAM=SQRT(ABS(EIG))
          IF (EIG.GE.0.D0) GOTO 300
          R1(I) = -1.D0/(FLAM*TAN(DRNEW*FLAM))
          R2(I) = -1.D0/(FLAM*SIN(DRNEW*FLAM))
          NOPLOC=NOPLOC+1
          GOTO 310

  300     R1(I) = 1.D0/(FLAM*TANH(DRNEW*FLAM))
          R2(I) = 1.D0/(FLAM*SINH(DRNEW*FLAM))
  310     R3(I) = R2(I)
          R4(I) = R1(I)
  320   CONTINUE
C
        CALL TRNSFM(Q,RMAT,VECNOW,N,.FALSE.,.TRUE.)
C
        IND=-N
        DO 330 I=1,N
          IND=IND+N+1
          RMAT(IND)=RMAT(IND)+R1(I)
  330   CONTINUE
        CALL SYMINV(RMAT,N,N,IFAIL)
        IF (IFAIL.GT.N) GOTO 480

        CALL DSYFIL('U',N,RMAT,N)
        IND=0
        DO 340 IC=1,N
        DO 340 IR=1,N
          IND=IND+1
          RMAT(IND)=-R3(IR)*RMAT(IND)*R2(IC)
  340   CONTINUE
        IND=-N
        DO 350 I=1,N
          IND=IND+N+1
          RMAT(IND)=RMAT(IND)+R4(I)
  350   CONTINUE
        IF (IREAD) GOTO 400
C
C  IF ICODE /= 1 COMPUTE NEW STEP SIZE
        RTMP=RNOW+DRNEW/2.D0
        DRNEW=DRCALC(RSTART,RSTOP,KSTEP,NSTEP,POW)
C
        DO 360 I=1,N
          EIGOLD(I)=EIGNOW(I)
  360   CONTINUE
C
C  ADDED TEST ON KSTEP SO THAT IF MULTIPLE SEGMENTS ARE USED, THE
C  END STEP WRITES OUT VECOLD REGARDLESS OF WHETHER RMAX HAS BEEN
C  EXCEEDED
        IF (RTMP.LT.RSTOP .AND. KSTEP.LT.NSTEP) GOTO 380

  370   IF (ISCRU.LE.0) GOTO 450

        DRNEW=-9999.D0
        WRITE(ISCRU) DRNEW
        WRITE(ISCRU) VECOLD
        GOTO 450

  380   IF (ISCRU.GT.0) WRITE(ISCRU) DRNEW
        EIG1=(ERED+EIGOLD(1))/EP2RU
        EIGN=(ERED+EIGOLD(N))/EP2RU
        IF (IPRINT.GE.20) WRITE(6,390) KSTEP,RNOW,EIG1,EIGN
  390   FORMAT(1X,I7,F11.5,2(1PE16.6))
        GOTO 430
C
C  IF ICODE = 2 READ NEW STEP SIZE FROM DISK
  400   READ(ISCRU) DRNEW
        IF (DRNEW.NE.-9999.D0) GOTO 430
        READ(ISCRU) VECOLD
        GOTO 450
C
  430 CONTINUE
C
C  END OF R-MATRIX PROPAGATION LOOP
C
C  REACH HERE WHEN ASYMPTOTIC REGION IS REACHED
  450 CALL TRNSP(VECOLD,N)
      CALL TRNSFM(VECOLD,RMAT,VECNOW,N,.FALSE.,.TRUE.)

C  THIS LINE IS CRUCIAL BECAUSE Y->S REQUIRES THE CORRECT VALUE OF R
      RSTOP=RNOW+DRNOW/2.D0
C
C     CONVERT R MATRIX BACK TO LOG-DERIVATIVE
C
      CALL SYMINV(RMAT,N,N,IFAIL)
      IF (IFAIL.GT.N) GOTO 480
C
C  CALCULATED VALUE CAN BE IN ERROR IF MULTIPLE SEGMENTS ARE USED
      NSTEP=MIN(KSTEP,NSTEP)
      RETURN
C
  480 WRITE(6,490)
  490 FORMAT(/'  ***** ERROR IN SYMINV CALLED FROM RMPROP.',
     1        '  RUN HALTED.')
      STOP
      END
