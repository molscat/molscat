      SUBROUTINE WAVVEC(VL,P,IV,W,N,NHAM)
C  Copyright (C) 2020 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential, ONLY: NVLBLK
C
C  THIS SUBROUTINE CALCULATES THE W MATRIX FROM THE POTENTIAL ARRAY AND P
C  W(I,J)=SUM_{IHAM} POTENTIAL(IHAM,I,J)*P(IHAM)
C
C  IT LOOKS COMPLICATED BECAUSE IT ONLY GENERATES THE UPPER TRIANGLE OF
C  THE W ARRAY AND BECAUSE VL ONLY CONTAINS THE UPPER TRIANGLE OF THE
C  POTENTIAL MATRIX.
C
C  ALTERED BY CRLS 17-07-2018 TO DECOUPLE NHAM (NUMBER OF TERMS IN
C  POTENTIAL EXPANSION AND INTERNAL HAMILTONIAN EXPANSION) FROM NVLBLK
C  (NUMBER OF BLOCKS OF VL ARRAY USED FOR POTENTIAL EXPANSION AND INTERNAL
C  HAMILTONIAN EXPANSION PLUS NUMBER OF BLOCKS USED FOR EXTRA OPERATORS)
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      DIMENSION VL(1),P(1),IV(1),W(N,N)
C
C     DYNAMIC STORAGE COMMON BLOCK ...
      COMMON /MEMORY/ MX,IXNEXT,NIPR,IDUMMY,X(1)
      COMMON /VLFLAG/ IVLFL
      COMMON /VLSAVE/ IVLU
C
      IF (IVLFL.GT.0) GOTO 10
C
C     REACH HERE ONLY FOR IVLFL=0: NO IV ARRAY FOR INDEXING
C
      IF (IVLU.EQ.0) THEN
        I=1
C  EACH CALL TO DGEMV GENERATES THE J-TH COLUMN OF THE W ARRAY.  THE
C  RELEVANT STARTING ELEMENT OF THE VL ARRAY IS AT NHAM*(J)*(J-1)/2+1
        DO 1 J=1,N
          CALL DGEMV('T',NHAM,J,1.D0,VL(I),NVLBLK,P,1,0.D0,W(1,J),1)
          I=I+J*NVLBLK
   1    CONTINUE
      ELSE
C  ALTERNATIVELY READ A BLOCK OF THE VL ARRAY FROM CHANNEL IVLU AND THEN
C  GENERATE A COLUMN OF THE W ARRAY USING THE FORMULA ABOVE
        REWIND IVLU
        ISV=IXNEXT
        IXNEXT=ISV+N*(N+1)/2
        NUSED=0
        CALL CHKSTR(NUSED)
        DO 2 J=1,N
        DO 2 K=1,J
   2      W(K,J)=0.D0
        DO 5 LL=1,NHAM
          READ(IVLU) (X(ISV+I),I=0,N*(N+1)/2-1)
          I=1
          DO 4 J=1,N
            CALL DAXPY(J,P(LL),X(ISV+I-1),1,W(1,J),1)
   4        I=I+J
   5    CONTINUE
        IXNEXT=ISV
      ENDIF
C
C     FILL IN LOWER TRIANGLE
C
      CALL DSYFIL('L',N,W,N)
      RETURN
C
C  ARRIVE HERE FOR NON-TRIVIAL USE OF THE IV ARRAY, I.E., WHEN THE
C  P ARRAY IS NOT STORED IN THE SAME ORDER AS THE VL ARRAY.  IV PROVIDES
C  THE MAPPING BETWEEN THE TWO.
C
   10 IF (IVLU.NE.0) THEN
        WRITE(6,601)
  601   FORMAT(' *** ERROR IN WAVVEC. IVLU =',I2,' AND IVLFL =',I2/
     1    '     USE OF THE IV ARRAY IS NOT SUPPORTED FOR IVLU > 0.')
        STOP
      ENDIF
C
      I2=0
      DO 12 J=1,N
      DO 12 K=1,J
        I1=I2+1
        I2=I2+NVLBLK !NHAM
        WW=0.D0
        DO 11 I=I1,I1+NHAM-1
          IF (VL(I).NE.0.D0) WW=WW+VL(I)*P(IV(I))
  11    CONTINUE
        W(J,K)=WW
        W(K,J)=WW
  12  CONTINUE
C
      RETURN
      END
