      SUBROUTINE SCCTRL(N,MXLAM,NHAM,
     1                  JSINDX,SR,SI,KMTRIX,VL,
     2                  IV,EINT,CENT,WVEC,
     3                  L,NB,ERED,EP2RU,CM2RU,
     4                  RSCALE,DEGTOL,DRMAX,NSTAB,NOPEN,IPRINT,
     5                  IBOUND,ICHAN,WAVE,ILDSVU)
C  Copyright (C) 2025 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  THIS SUBROUTINE SETS UP THE STORAGE REQUIREMENTS FOR ALL THE
C  DIFFERENT PROPAGATORS IMPLEMENTED
C
C  03-12-15 CR Le Sueur:
C  LONG RANGE AND SHORT RANGE PROPAGATORS HAVE BEEN DECOUPLED.
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      INTEGER IPRINT
      LOGICAL PREV
      DIMENSION JSINDX(*),SR(*),SI(*),VL(*),IV(*),EINT(*),
     1          CENT(*),WVEC(*),L(*),NB(*)
      DOUBLE PRECISION, ALLOCATABLE :: Y(:),EVEC(:)
      DOUBLE PRECISION KMTRIX(*)

      INTEGER, PARAMETER :: MAXSEG=2
C
C  COMMON BLOCK FOR CONTROL OF USE OF PROPAGATION SCRATCH FILE
      LOGICAL IREAD,IWRITE,IREADR,IWRITR
      COMMON /PRPSCR/ ESHIFT,ISCRU,ISCRUR,IREAD,IWRITE,IREADR,IWRITR
C
C  COMMON BLOCK FOR CONTROL OF PROPAGATION SEGMENTS
      COMMON /RADIAL/ RMNINT,RMXINT,RMID,RMATCH,DRS,DRL,STEPS,STEPL,
     1                POWRS,POWRL,TOLHIS,TOLHIL,CAYS,CAYL,unset,
     2                IPROPS,IPROPL,NSEG
C
      DIMENSION STPSEG(MAXSEG),CAYSEG(MAXSEG),TOLSEG(MAXSEG),
     1          DRSEG(MAXSEG),IPRSEG(MAXSEG),RBSEG(MAXSEG),
     2          RESEG(MAXSEG),POWSEG(MAXSEG),NSTEPS(MAXSEG)

      LOGICAL WAVE

C  COMMON BLOCK FOR DERIVATIVES
      LOGICAL NUMDER
      COMMON /DERIVS/ NUMDER

C  COMMON BLOCK FOR INPUT/OUTPUT CHANNEL NUMBERS
      LOGICAL IWAVEF
      INTEGER IPSISC,IWAVSC,IWAVE
      COMMON /IOCHAN/ IPSISC,IWAVSC,IWAVE,NWVCOL,IWVSTP,IWAVEF

C  TIMING VARIABLES
      DOUBLE PRECISION TIMEI,TIMEP(MAXSEG)
C
      IF (WAVE) CALL WVOPEN(N)

      CALL SCPSET(IPRSEG,RBSEG,RESEG,DRSEG,STPSEG,
     1            TOLSEG,CAYSEG,POWSEG)

      IREC=1
C
C  COUNT THE NUMBER OF OPEN CHANNELS AND SET UP WVEC ARRAY
      CALL GTWVEC(WVEC,WMAX,ERED,EINT,NOPEN,N)
      IF (NOPEN.EQ.0) RETURN

C  SET S MATRIX TO IDENTITY IN CASE NO PROPAGATION IS DONE
      IJ=0
      DO I=1,NOPEN
      DO J=1,NOPEN
        IJ=IJ+1
        SR(IJ)=0.D0
        SI(IJ)=0.D0
        IF (I.EQ.J) SR(IJ)=1.D0
      ENDDO
      ENDDO

C     ---------------------------------------------------------------

      ISTART=0

      ALLOCATE (Y(N*N))
!  Start of long DO loop #1
      DO ISEG=1,NSEG
        CALL GCLOCK(TIMEI)
        NUSED=0
        IF (ISEG.GT.1 .AND. PREV) RBSEG(ISEG)=RSTOP
        IPROP=IPRSEG(ISEG)
        RSTART=RBSEG(ISEG)
        RSTOP=RESEG(ISEG)
        CAY=CAYSEG(ISEG)
        DRT=DRSEG(ISEG)
        STEP=STPSEG(ISEG)
        TOLHIT=TOLSEG(ISEG)
        POW=POWSEG(ISEG)

        IF (RSTART.GE.RSTOP) THEN
          PREV=.FALSE.
          DR=DRT
          CYCLE
        ELSE
          PREV=.TRUE.
        ENDIF
        IF (IPROP.GT.-1) THEN
          IF (IREAD) THEN
            READ(ISCRU) RSTART,RSTOP,DR,EFIRST,NSTEP
            ESHIFT=ERED-EFIRST

          ELSE
            CALL DRSET(RSTART,RSTOP,STEP,TOLHIT,CAY,DRT,NSTEP,DR,
     1                 unset,IPROP,POW)
            IF (IWRITE) WRITE(ISCRU) RSTART,RSTOP,DR,ERED,NSTEP
          ENDIF
        ENDIF
        DRSEG(ISEG)=DR
C
C  INITIALISE Y MATRIX
C
        IF (IPROP.NE.-1) THEN
          IF (ISTART.EQ.0)
     1      CALL YINIT(Y,VL,IV,CENT,EINT,
     2                 N,MXLAM,NHAM,
     3                 ERED,RSTART,EP2RU,CM2RU,RSCALE,
     4                 .TRUE.,IPRINT)
        ENDIF
C
!  Start of long IF block #1
        IF (IPROP.EQ.2) THEN
C  SOLVE COUPLED EQUATIONS BY PROPAGATOR OF DE VOGELAERE
C
          CALL DVPROP(N,MXLAM,NHAM,
     1                Y,VL,IV,EINT,CENT,L,NB,
     3                RSTART,RSTOP,NSTEP,DR,NSTAB,
     4                ERED,EP2RU,CM2RU,RSCALE,IPRINT)
          IF (IPRINT.GE.8) WRITE(6,1800) 'DVPROP',RSTART,RSTOP,NSTEP
 1800     FORMAT(/2X,A,'. PSI AND PSI'' PROPAGATED FROM ',
     &           F12.4,'  TO',1PG12.5,'  IN ',I6,'  STEPS.')
C
        ELSEIF (IPROP.EQ.3) THEN
C  SOLVE COUPLED EQUATIONS BY R-MATRIX PROPAGATOR OF WALKER & LIGHT
C
          CALL RMPROP(N,MXLAM,NHAM,
     1                Y,VL,IV,EINT,CENT,
     4                RSTART,RSTOP,NSTEP,DR,POW,
     5                ERED,EP2RU,CM2RU,RSCALE,IPRINT)
          IF (IPRINT.GE.8) WRITE(6,1900) 'RMPROP',RSTART,RSTOP,NSTEP
 1900     FORMAT(/2X,A,'. R MATRIX PROPAGATED FROM ',
     &           F12.4,'  TO',1PG12.5,'  IN ',I6,'  STEPS.')
C
        ELSEIF (IPROP.EQ.4) THEN
C  SOLVE COUPLED EQUATIONS BY VIVS PROPAGATOR OF PARKER
C
C  VVPROP PROPAGATES THE R MATRIX, WHICH IS THE INVERSE OF THE
C  LOG-DERIVATIVE MATRIX PRODUCED BY YINIT AND USED BY OTHER PROPAGATORS
C  INVERT Y TO GET R (UNCONDITIONALLY, THOUGH VIVS IS NOT CURRENTLY
C  ALLOWED AS A SHORT-RANGE PROPAGATOR)
C
          CALL SYMINV(Y,N,N,IFAIL)
          CALL DSYFIL('U',N,Y,N)
C
          TLDIAG=0.064D0*SQRT(TOLHIT/0.001D0)
          TOFF=TLDIAG
C
          CALL VVPROP(N,MXLAM,NHAM,
     1                Y,VL,IV,EINT,CENT,
     6                RSTART,RSTOP,NSTEP,DR,DRMAX,TLDIAG,TOFF,
     7                ERED,EP2RU,CM2RU,RSCALE,IPRINT)
C
C  INVERT R TO GET Y
C
          CALL SYMINV(Y,N,N,IFAIL)
          IF (IPRINT.GE.8) WRITE(6,1900) 'VVPROP',RSTART,RSTOP,NSTEP
C
        ELSEIF (IPROP.EQ.5) THEN
C  SOLVE COUPLED EQUATIONS BY LOG-DERIVATIVE PROPAGATOR OF JOHNSON
C
          CALL LDPROP(N,MXLAM,NHAM,
     1                Y,VL,IV,EINT,CENT,
     2                RSTART,RSTOP,NSTEP,DR,NODES,
     3                ERED,EP2RU,CM2RU,RSCALE,IPRINT)
          IF (IPRINT.GE.8) WRITE(6,2000) 'LDPROP',RSTART,RSTOP,NSTEP
C
        ELSEIF (IPROP.EQ.6) THEN
C  SOLVE COUPLED EQUATIONS BY DIABATIC LOG-DERIVATIVE PROPAGATOR
C  OF MANOLOPOULOS
C
          IF ((N.GT.1 .OR. WAVE) .AND. NSTEP.GT.0 .AND.
     1        RSTART.LT.RSTOP) THEN
            CALL MDPROP(N,MXLAM,NHAM,
     1                  Y,VL,IV,EINT,CENT,
     2                  RSTART,RSTOP,NSTEP,DR,NODES,IREC,WAVE,
     3                  ERED,EP2RU,CM2RU,RSCALE,IPRINT)
            IF (IPRINT.GE.8) WRITE(6,2000) 'MDPROP',RSTART,RSTOP,NSTEP
          ELSEIF (N.EQ.1) THEN
            CALL ODPROP(MXLAM,NHAM,
     1                  Y(1),VL,IV,EINT,CENT,
     4                  RSTART,RSTOP,NSTEP,DR,NODES,
     5                  ERED,EP2RU,CM2RU,RSCALE,IPRINT)
            IF (IPRINT.GE.8) WRITE(6,2000) 'ODPROP',RSTART,RSTOP,NSTEP
          ENDIF
C
        ELSEIF (IPROP.EQ.7) THEN
C  SOLVE COUPLED EQUATIONS BY ADIABATIC LOG-DERIVATIVE PROPAGATOR
C  OF MANOLOPOULOS
C
          CALL MAPROP(N,MXLAM,NHAM,
     1                Y,VL,IV,EINT,CENT,
     2                RSTART,RSTOP,NSTEP,DR,NODES,
     3                ERED,EP2RU,CM2RU,RSCALE,IPRINT)
          IF (IPRINT.GE.8) WRITE(6,2000) 'MAPROP',RSTART,RSTOP,NSTEP
 2000     FORMAT(/2X,A,'. LOG-DERIVATIVE MATRIX PROPAGATED FROM ',
     &           F12.4,'  TO',1PG12.5,'  IN ',I6,'  STEPS.')

C
        ELSEIF (IPROP.EQ.8) THEN
C  SOLVE COUPLED EQUATIONS BY SYMPLECTIC LOG-DERIVATIVE PROPAGATORS
C  OF MANOLOPOULOS & GREY
C
          CALL MGPROP(N,MXLAM,NHAM,
     1                Y,VL,IV,EINT,CENT,
     3                RSTART,RSTOP,NSTEP,DR,NODES,
     4                ERED,EP2RU,CM2RU,RSCALE,IPRINT)
          IF (IPRINT.GE.8) WRITE(6,2000) 'MGPROP',RSTART,RSTOP,NSTEP
C
        ELSEIF (IPROP.EQ.9) THEN
C  SOLVE COUPLED EQUATIONS BY AIRY PROPAGATOR OF ALEXANDER AND MANOLOPOULOS
C
          IF (RSTART.LT.RSTOP) THEN
            IF (ISCRU.EQ.0) ITWO=-1
            CALL AIPROP(N,MXLAM,NHAM,
     1                  Y,VL,IV,EINT,CENT,
     5                  RSTART,RSTOP,NSTEP,DR,POW,TOLHIT,NODES,
     6                  ERED,EP2RU,CM2RU,RSCALE,IPRINT,IREC,WAVE)
            IF (IPRINT.GE.8) WRITE(6,2000) 'AIPROP',RSTART,RSTOP,NSTEP
          ENDIF
C
        ELSEIF (IPROP.EQ.-1) THEN
C
C  SOLVE SINGLE-CHANNEL EQUATION BY WKB USING GAUSS-MEHLER INTEGRATION.
C
          IF (N.EQ.1) GOTO 810
          WRITE(6,601) N
  601     FORMAT(/' ***** ERROR.  WKB IMPLEMENTED ONLY FOR',
     1           ' ONE-CHANNEL CASE.  TERMINATED WITH N =',I4)
          STOP
  810     CONTINUE
C
          DWVEC=SQRT(ERED-EINT(1))
          CALL WKB(N,MXLAM,NHAM,
     1             SR,SI,VL,IV,EINT,CENT,
     2             DWVEC,L,
     3             RSTART,TOLHIT,ERED,EP2RU,CM2RU,RSCALE,IPRINT)
C
        ELSE
          WRITE(6,699) IPROP
  699     FORMAT(/' SCCTRL CALLED WITH AN ILLEGAL IPROP=',I4)
          STOP
        ENDIF
!  End of long IF block #1
        ISTART=1
        NSTEPS(ISEG)=NSTEP
        IF (IPRINT.GE.8) THEN
          CALL GCLOCK(TIMEP(ISEG))
          TIMEP(ISEG)=TIMEP(ISEG)-TIMEI
          WRITE(6,170) ' TIME TAKEN FOR CURRENT PROPAGATION: ',
     1                 TIMEP(ISEG),' CPU SECS.'
        ENDIF
  170   FORMAT(1X,A,F11.2,A)
      ENDDO
!  End of long DO loop #1
      IF (ISCRUR.NE.0) REWIND (ISCRUR)
C
C  -----------------------------------------------------------------------
C  END OF PROPAGATION
C
      ALLOCATE (EVEC(N*N))
      IF (IPRSEG(NSEG).GE.0) THEN
        CALL YTRANS(Y,EVEC,EINT,WVEC,
     1              JSINDX,L,N,VL,IV,
     2              MXLAM,NHAM,ERED,EP2RU,CM2RU,DEGTOL,
     3              NOPEN,IBOUND,CENT,IPRINT,.TRUE.)
C  IF OUTPUT OF LOG-DERIVATIVE MATRIX REQUESTED,
C  WRITE PROPAGATION VECTORS (LDRWPV) AND MATRIX (LDRWMD)
        IF (ILDSVU.GT.0) THEN
          IDUM=LDRWPV(ILDSVU,.TRUE.,N,JSINDX,L,EINT)
          IDUM=LDRWMD(ILDSVU,.TRUE.,N,RSTOP,Y)
        ENDIF
C
C  CONVERT LOG-DERIVATIVE MATRIX TO K MATRIX AND THEN TO S MATRIX
C
        CALL YTOK(NB,WVEC,L,N,NOPEN,Y,KMTRIX,RSTOP,CENT)
        CALL KTOS(KMTRIX,SR,SI,NOPEN)
      ENDIF

      IF (WAVE) THEN
C  CALCULATE SCATTERING WAVEFUNCTION
        CALL SCWAVE(RBSEG,RESEG,DRSEG,IPRSEG,NSTEPS,NSEG,
     1              WVEC,SR,SI,EVEC,KMTRIX,L,N,NOPEN,NB,
     2              ICHAN,IREC,IPRINT)
      ENDIF
      DEALLOCATE(Y,EVEC)
C
      IF (WAVE) THEN
        CLOSE(IPSISC)
        CLOSE(IWAVSC)
      ENDIF
      RETURN
      END
