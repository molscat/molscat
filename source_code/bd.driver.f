      SUBROUTINE DRIVER
C  Copyright (C) 2025 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE efvs, ONLY: EFVNAM, EFVUNT, IEFVST, ISVEFV, ITPSUB, MAPEFV,
     e                MXEFV, NEFV, NEFVP, SCALAM, SVNAME, SVUNIT
      USE potential, ONLY: CONLEN, CONFRQ, EP2RU, EPNAME, IREF, NCONST,
     p                     NDGVL, NEXTRA, NRSQ, NVLBLK, RMNAME
      USE basis_data, ONLY: NJLQN9
      USE physical_constants, ONLY: NIST_year, bfct
      USE sizes, ONLY: MXFLD => MXFLD_in_BOUND, mxlmda, MXNODE, MXOMEG
      USE pair_state, ONLY: ATAU, JSTATE
cINOLLS USE i_nolls, ONLY: idata, nidata, nrdata, rdata, inolls_init,
cINOLLS1                   inolls_data, inolls_check, save_res, write_res
C
C***********************************************************************
C
C  ---------------  BOUND - J.M. HUTSON & C.R. LE SUEUR  ---------------
C
C  QUANTUM MOLECULAR BOUND-STATE PROGRAM
C
C  BOUND VERSION 5 HAS COMPLETELY REWRITTEN EIGENVALUE SEARCHING
C  LOGIC BASED ON NODE COUNTS AND THE EIGENVALUES OF THE
C  LOG-DERIVATIVE MATCHING MATRIX
C
C  BOUND VERSION 6 WAS A PRIVATE JMH CODE
C  BOUND VERSIONS 7 TO 14 NEVER EXISTED: THE MARCH 2002 VERSION WAS
C  NAMED VERSION 15 TO BRING THE NUMBERING INTO LINE WITH MOLSCAT
C
C  CURRENT VERSION: 2025.0
C***********************************************************************
C
C  DEFAULT UNITS ARE
C   MASSES IN UNIFIED ATOMIC MASS UNITS (DALTONS, CARBON MASS/12)
C   ENERGIES AS WAVENUMBERS IN CM-1
C   LENGTHS IN ANGSTROMS
C  BUT THESE MAY BE CHANGED VIA THE VARIABLES EUNITS, EUNIT, MUNIT, RUNIT

C  ENERGIES ARE OFTEN WRITTEN OUT IN CM-1 AS WELL AS THE SPECIFIED UNITS
C
C  RMIN   IS THE RADIUS AT WHICH THE OUTWARDS PROPAGATION IS BEGUN
C  RMAX   IS THE RADIUS AT WHICH THE INWARDS  PROPAGATION IS BEGUN
C  RMATCH IS THE MATCHING POINT
C  RMID   IS THE POINT AT WHICH THE PROPAGATION METHOD CHANGES (IF IT DOES)
C
C  IPROPS AND IPROPL CONTROL METHODS OF PROPAGATING SOLUTIONS TO COUPLED
C  EQUATIONS
C  NHAM AND MXLAM CONTROL SUM OVER ANGULAR DEPENDENCE OF POTENTIAL
C  NQN IS NO. OF QUANTUM NUMBERS USED TO DESCRIBE INTERACTION PARTNERS
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      logical :: inolls=.false.
      PARAMETER (NEXP=10,MXMNQN=10)
      INTEGER EUNITS,PRNTLV,SHRINK,MONQN(MXMNQN)
      DOUBLE PRECISION MUNIT
C
C  ARRAY TO HOLD TIME AND DATE
      CHARACTER CTIME*9,CDATE*11

      CHARACTER METHOD*12
C
      LOGICAL LFSCAN,LFSRCH,NOBIS,REBIS,LESCAN,LIPEFV
      LOGICAL CONVGE,ENOISE,ZBRENT,LSKIP,EMAXBD,LTYP8
C
      CHARACTER(1)   PLUR(2)
      CHARACTER(80)  LABEL
      CHARACTER(122) LABL
      CHARACTER(3)   C1,C2
      CHARACTER(40)  FTITLE
      CHARACTER(8)   EUNAME,QNAME(10)
      CHARACTER(20)  PDATE
      CHARACTER(10)  RUNAME
C  ALLOCATABLE ARRAYS
      ALLOCATABLE ESCR(:)
      ALLOCATABLE JSINDX(:),EINT(:),CENT(:),WVEC(:),L(:),VL(:,:),
     1            IV(:,:),DGVL(:,:),VLTEMP(:,:),EVEMIN(:),EVEMAX(:),
     2            EVBDST(:),EVSMLL(:),EVELO(:,:),EVEHI(:,:)
      ALLOCATABLE LAM(:),LAMTMP(:)
C
C  FOLLOWING ARRAYS ALL HAVE DIMENSION MXNODE. MXNODE IS THE MAXIMUM
C  NUMBER OF NODES THAT CAN BE SOUGHT IN A SINGLE RUN.
C
      DIMENSION EHI(MXNODE),ELO(MXNODE),EIGHI(MXNODE),EIGLO(MXNODE),
     1          NLO(MXNODE),NHI(MXNODE),IHI(MXNODE),ILO(MXNODE)
C
C  VARIABLES DIMENSIONED FOR SAVING CONVERGED EIGENVALUES
      DIMENSION EREFP(0:NEXP),EVAL(MXNODE,NEXP),EXTRAP(MXNODE),
     1          EXTRAV(MXNODE,NEXP),NCHECK(MXNODE)
C
C  ARRAYS FOR EFVS
      DIMENSION FIELD(MXFLD)
      DIMENSION FIXFLD(MXEFV),IFVARY(MXEFV)
      LOGICAL ZCNTN
      DIMENSION OLDFAC(MXOMEG)
C
C  DUMMY ARRAYS
      DIMENSION DUMAR(1),IDUMAR(1),DUMAR2(1,1),IDUMA2(1,1)
C
C  VARIABLES FOR EXPECTATION VALUES
      DIMENSION IPPERT(NEXP),NPOW(NEXP),DELTA(NEXP),FACTOR(NEXP)
C
      COMMON /VLFLAG/ IVLFL
C
C  COMMON BLOCK FOR CONTROL OF USE OF PROPAGATION SCRATCH FILE
      LOGICAL IREAD,IWRITE,IREADR,IWRITR
      COMMON /PRPSCR/ ESHIFT,ISCRU,ISCRUR,IREAD,IWRITE,IREADR,IWRITR
      LOGICAL LSCRU,LSCRUR
C
C  COMMON BLOCK FOR CONTROL OF PROPAGATION SEGMENTS
      COMMON /RADIAL/ RMNINT,RMXINT,RMID,RMATCH,DRS,DRL,STEPS,STEPL,
     1                POWRS,POWRL,TOLHIS,TOLHIL,CAYS,CAYL,unset,
     2                IPROPS,IPROPL,NSEG
      PARAMETER (PUNSET=-237540475023D30)
C
      COMMON /VLSAVE/ IVLU
C
C  COMMON BLOCK FOR EXPECTATION VALUES, COMMUNICATING WITH PERTRB
      COMMON /EXPVAL/ IPERTN,NPOWN,DELTAN

C  COMMON BLOCK FOR NUMERICAL DERIVATIVES
      COMMON /DERIVS/ NUMDER
      LOGICAL NUMDER

C  COMMON BLOCK TO DESCRIBE WHICH DRIVER IS USED
      COMMON /CNTROL/ CDRIVE
      CHARACTER(1) CDRIVE

C  COMMON BLOCK FOR INPUT/OUTPUT CHANNEL NUMBERS
      LOGICAL IWAVEF
      COMMON /IOCHAN/ IPSISC,IWAVSC,IWAVE,NWVCOL,IWVSTP,IWAVEF
      DATA IPSISC,IWAVSC/972,973/
      LOGICAL WAVE

C  COMMON BLOCK FOR CONTROL OF PROPAGATION BOUNDARY CONDITIONS
      COMMON /BCCTRL/ BCYCMN,BCYCMX,BCYOMN,BCYOMX,ADIAMN,ADIAMX,
     1                WKBMN,WKBMX
      LOGICAL ADIAMN,ADIAMX,WKBMN,WKBMX
C
      NAMELIST /INPUT/ ADIAMN, ADIAMX, BCYCMN, BCYCMX, BCYOMN,
     1                 BCYOMX, CONFRQ, CONLEN, DEGTOL, DELTA,
     2                 DFIELD, DNRG,   DR,     DRAIRY, DRCON,
     3                 DRL,    DRS,    DTOL,   ECTRCT, EMAX,
     4                 EMAXBD, EMIN,   EPL,    EPS,    EREF,
     5                 EUNITS, EUNAME, EUNIT,  FACTOR, FIELD,
     6                 FIXFLD, FLDMAX, FLDMIN, IBDSUM, IBFIX,
     7                 IBHI,   ICON,   IFIELD, IFVARY, IMGSEL,
     8                 INTFLG, IPPERT, IPRINT, IPROPL, IPROPS,
     9                 IREF,   IRMSET, ISCRU,  IWAVE,  IWAVEF, 
     A                 IWVSTP, JSTEP,  JTOTL,  JTOTU,  KSAVE,  
     B                 LABEL,  LASTIN, MAGEL,  MHI,    MONQN,  
     C                 MSET,   MUNIT,  MXCALC, NCONV,  NFIELD, 
     D                 NFVARY, NOBIS,  NODMAX, NODMIN, NPERT,  
     E                 NPOW,   NUMDER, NWVCOL, PHILW,  PHIST,  
     F                 POWRL,  POWRS,  POWRX,  PRNTLV, RCTRCT, 
     G                 REBIS,  RMATCH, RMAX,   RMID,   RMIN,   
     H                 RUNIT,  RUNAME, SCALAM, STEPL,  STEPS,  
     I                 THETLW, THETST, TOLHI,  TOLHIL, TOLHIS, 
     J                 URED,   WKBMN,  WKBMX,
C
C  UNUSED NAMELIST VARIABLES RETAINED FOR DATA-FILE COMPATIBILITY
C
     1                 IRXSET,ISAVEU,ISIGPR,MXSIG,OTOL
      EQUIVALENCE (IPRINT,PRNTLV),(IBDSUM,KSAVE),(IBFIX,MSET),
     1            (IBHI,MHI)
C
      DIMENSION NLABV(9),NJLQN(9)
C  NLABV ARRAY CONTAINS NUMBER OF LABELS PER SYMMETRY TERM FOR EACH
C  VALUE OF ITYPE.
      DATA NLABV /1,3,3,4,2,2,5,2,1/
C  NJLQN ARRAY CONTAINS NUMBER OF QUANTUM NUMBER NEEDED TO IDENTIFY A
C  LEVEL CONTAINED IN JLEVEL. (VALUE FOR ITYP=9 IS A DUMMY VALUE)
      DATA NJLQN/1,2,2,3,3,2,2,2,1/
C
      DATA LABEL /'        '/
      DATA IPROGM /19/, PDATE /'2025.0'/
      DATA PLUR /' ','S'/
      DATA CDRIVE /'B'/
C
C  THE PHYSICAL CONSTANTS USED ARE COMBINED IN THE SINGLE NUMBER BFCT.
C  BFCT IS HBAR/(4PI*C) IN UNITS OF (ATOMIC MASS UNITS)*(WAVENUMBERS)
C                                      *(ANGSTROMS**2).
C  BFCT IS NOW IN MODULE physical_constants
C
C  * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *
C
   10 unset=PUNSET
C  RESET ITEMS IN COMMON BLOCKS IN CASE OF REPEAT CALCULATIONS
      IVLU=0
      IVLFL=0
      NCONST=0
      NRSQ=0
      RMATCH=0.D0
      ERED=0.D0
      IPSISC=972
      IWAVSC=973
      CALL GCLOCK(TFIRST)
      CALL GDATE(CDATE)
      CALL GTIME(CTIME)
C
C  SET DEFAULT NAMES FOR UNITS USED BY POTENTIAL ROUTINES
      EPNAME='EPSIL'
      RMNAME='RM'
C
C  SET INITIAL VALUES BEFORE READ(5,INPUT) . . .
C
      ADIAMN=.TRUE.
      ADIAMX=.TRUE.
      BCYCMN=-1.D0
      BCYCMX=-1.D0
      BCYOMN=unset
      BCYOMX=0.D0
      CONFRQ=0.D0
      CONLEN=0.D0
      DEGTOL=1.D-10
      DO I=1,NEXP
        DELTA(I)=1.D-3
        FACTOR(I)=1.D0
        IPPERT(I)=I
      ENDDO
      DFIELD=1.D0
      DNRG=1.D30
      DR=0.02D0
      DRAIRY=unset
      DRCON=0.1D0
      DRL=unset
      DRS=unset
      DTOL=1.D-7
      ECTRCT=-1.D30
      EMAX=1.D30
      EMAXBD=.FALSE.
      EMIN=-EMAX
      EPL=0.D0
      EPS=0.D0
      EREF=0.D0
      EUNITS=1
      EUNAME='E UNITS'
      EUNIT=1.D0
      DO I=1,MXFLD
        FIELD(I)=0.D0
      ENDDO
      DO I=1,MXEFV
        EFVNAM(I)=' '
        EFVUNT(I)=' '
        FIXFLD(I)=0.D0
        IFVARY(I)=MXEFV+1
      ENDDO
      FLDMAX=0.D0
      FLDMIN=0.D0
      IBDSUM=0
      IBFIX=0
      IBHI=0
      ICON=1
      IFIELD=-1 ! MLEO'S, REPLACED IFIELD=4 ON 19/09/07
      IMGSEL=4
      INTFLG=0
      IPRINT=2
      IPROPL=0
      IPROPS=0
      IREF=0
      IRMSET=0
      ISCRU=0
      IWAVE=0
      IWAVEF=.TRUE.
      IWVSTP=1
      JSTEP=1
      JTOTL=0
      JTOTU=0
      LABEL=' '
      LASTIN=1
      MAGEL=1
      MONQN(1)=-99999
      MUNIT=1.D0
      MXCALC=1000
      NCONV=0
      NFIELD=1
      NFVARY=-1
      NITER=20
      NOBIS=.FALSE.
      NODMAX=99999
      NODMIN=0
      NPERT=0
      NPOW=0
      NUMDER=.FALSE.
      NWVCOL=8
      PHILW=0.D0
      PHIST=0.D0
      POWRX=3.D0
      POWRL=unset
      POWRS=unset
      RCTRCT=0.D0
      REBIS=.TRUE.
      RMATCH=unset
      RMAX=10.D0
      RMID=unset
      RMIN=0.8D0
      RUNAME='RUNIT'
      RUNIT=unset
      SCALAM=1.D0
      STEPL=unset
      STEPS=-10.D0
      THETLW=0.D0
      THETST=0.D0
      TOLHI=1.D-4
      TOLHIL=unset
      TOLHIS=unset
      URED=0.D0
      WKBMN=.TRUE.
      WKBMX=.TRUE.
C  OTHER VARIABLES
      DRMAX=5.D0
      ILDSVU=0  ! FOR BOUND, TURN OFF ILDSVU
      ISCRUR=0
      ITPSUB=0
      MAPEFV=0
      NCALC=0
      NDGVL=0
      NEFV=-1
      NHAM=0
      NJLQN9=99999
      PI=ACOS(-1.D0)
C
      if (.not.inolls) READ(5,INPUT,END=999)
C
cINOLLS call inolls_init
cINOLLS include 'bound-input-namelist.h'
C
      LABEL=ADJUSTL(LABEL)
      LABLEN=MAX(LEN(TRIM(LABEL)),1)
      L1=(116-LABLEN)/2
      L2=116-L1-LABLEN
      WRITE(C1,'(I3)') L1
      WRITE(C2,'(I3)') L2
      FTITLE="(1X,"//ADJUSTR(C1)//"('='),' ',A,' ',"//ADJUSTR(C2)//
     1       "('='))"
      WRITE(LABL,FMT=FTITLE) TRIM(LABEL)

      IF (URED.EQ.0.D0) THEN
        WRITE(6,*) ' YOU HAVE FORGOTTEN THE REDUCED MASS'
        STOP
      ENDIF
C
      IF (IPRINT.GE.1) THEN
        WRITE(6,1001)
 1001   FORMAT(2X,'-- BOUND ---',4('--- BOUND ---'),'--- BOUND --')
        WRITE(6,1002)
 1002   FORMAT(' |',76X,'|'/' |',10X,
     2         'Bound states of interacting pairs of atoms and ',
     3         'molecules',10X,'|')
        CALL PROGVS(PDATE)
        CALL TIMEST(CDATE,CTIME)
        WRITE(6,1001)
        CALL CPRMS(PDATE)
      ENDIF
C
      IF (IPRINT.GE.1) WRITE(6,1003) NIST_year
 1003 FORMAT(/'  USING CODATA ',I4,' RECOMMENDED VALUES OF',
     1   ' FUNDAMENTAL PHYSICAL CONSTANTS')
C
      IF (IPRINT.GE.1) WRITE(6,1007) IPRINT
 1007 FORMAT(/'  PRINT LEVEL (IPRINT) =',I3)

      IF (IPRINT.GE.1) THEN
        IF (MUNIT.EQ.1.D0) THEN
          WRITE(6,1009) URED
 1009     FORMAT(/'  REDUCED MASS FOR INTERACTION =',F14.9,
     1            ' ATOMIC MASS UNITS (DALTONS)')
        ELSE
          WRITE(6,1010) URED,MUNIT
 1010     FORMAT(/'  REDUCED MASS FOR INTERACTION =',F14.9,
     1            ' (UNITS OF',1PG16.9,' DALTONS)')
        ENDIF
      ENDIF
C
      IF (ISCRU.LT.0) THEN
        IF (IPRINT.GE.1) WRITE(6,1020)
 1020   FORMAT('  *** WARNING. THE OPTION TO REUSE A SCRATCH FILE ',
     1         'FROM A PREVIOUS RUN IS NOT SUPPORTED IN THIS VERSION ',
     2         'OF BOUND')
        ISCRU=ABS(ISCRU)
      ENDIF
C
      IF (ISCRU.EQ.0) THEN
        IF (IPRINT.GE.1) WRITE(6,1030)
 1030   FORMAT(/'  NO SCRATCH FILE SPECIFIED BY ISCRU PARAMETER: ',
     1         'FULL CALCULATION WILL BE DONE AT EACH ENERGY')
      ELSE
        IF (IPRINT.GE.1) WRITE(6,1031) ISCRU
 1031   FORMAT(/'  ENERGY-INDEPENDENT MATRICES WILL BE SAVED ',
     1         'TEMPORARILY ON UNIT',I3)
      ENDIF
C
C  PROCESS TOTAL ENERGIES
C
      IF (EUNITS.NE.0) CALL ECNV(EUNITS,EUNIT,EUNAME,IPRINT)
C
      IF (EMAX.LE.EMIN) THEN
        WRITE(6,1050) EMIN,EMAX
 1050   FORMAT(/'  *** ERROR. EMAX MUST BE GREATER THAN EMIN.'/
     1         6X,'EMIN =',F12.5,' AND EMAX =',F12.5)
        STOP
      ENDIF
C
      NNRG=MAX(1+NINT((EMAX-EMIN)/DNRG),2)
      IF (DNRG.LT.EMAX-EMIN) EMAX=EMIN+NNRG*DNRG
      IF (NNRG.EQ.1) NNRG=2
      DTOL=DTOL*EUNIT
      EMIN=EMIN*EUNIT
      EMAX=EMAX*EUNIT
      DNRG=DNRG*EUNIT
      EPS=EPS*EUNIT
      EPL=EPL*EUNIT
      ECTRCT=ECTRCT*EUNIT
C
      IF (IPRINT.GE.1) WRITE(6,1060)
 1060 FORMAT(/2X,59('=='))
C
C--------------------------------------------------------------------
C  INITIALIZE BASIS ROUTINE  -----------------
C  COMBINED MOLSCAT (BASIN) AND IOS (IOSBIN) -- APR 86
C  IOSBIN IS NOT PART OF THE BOUND PROGRAM
C
      IF (ALLOCATED(JSTATE)) DEALLOCATE(JSTATE)
      IF (ALLOCATED(ATAU)) DEALLOCATE(ATAU)
      CALL BASIN(NSTATE,URED,NQN,NLABV(9),MXPAR,ITYPE,IPRINT,
     1           IOSFLG,IBOUND,NLEVEL,QNAME)
      LTYP8=ITYPE.EQ.8
      IVLSAV=IVLU
      IF (IPRINT.GE.1) WRITE(6,1060)
C
C  IF NEXTRA HAS BEEN SET, ASSUME THAT DEGTOL IS NEEDED
      IF (NEXTRA.NE.0) THEN
        DEGTOL=DEGTOL*EUNIT
        IF (IPRINT.GE.1) THEN
          IF (EUNIT.NE.1.D0) THEN
            WRITE(6,1101) DEGTOL,DEGTOL/EUNIT,EUNAME
          ELSE
            WRITE(6,1101) DEGTOL
          ENDIF
          WRITE(6,1102) NEXTRA
        ENDIF
 1101   FORMAT(/2X,'EXTRA OPERATORS WILL BE USED TO RESOLVE ',
     1         'ASYMPTOTIC DEGENERACIES CLOSER THAN'/2X,'DEGTOL =',
     2         G16.8,' CM-1 =',G16.8,1X,A)
 1102   FORMAT(2X,'THERE ARE ',I3,' EXTRA OPERATORS')
      ENDIF

      LFSRCH=.FALSE.
      LFSCAN=(FLDMIN.NE.0.D0 .OR. FLDMAX.NE.0.D0)
      LESCAN=(ABS(DNRG).LT.ABS(EMIN-EMAX) .OR. EMIN.EQ.EMAX)
C  INITIALISE ALL EXTERNAL VARIABLE QUANTITIES
      CALL INIEFV(FIXFLD,IPRINT,FLDMIN,FLDMAX,DFIELD,
     1            NFIELD,FIELD,MAGEL,NFVARY,IFVARY,LFSRCH,LFSCAN)
      SVNAME='ENERGY'
      SVUNIT=ADJUSTL(EUNAME)

      LENEFV=MAX(NEFVP-IEFVST+1,0)
C
C--------------------------------------------------------------------
C  INITIALIZE POTENTIAL.
C   EP2CM  (POTENTIAL ENERGY UNIT) MUST BE SET IN POTENL
C   RPUNIT (POTENTIAL LENGTH UNIT) MAY OPTIONALLY BE SET IN POTENTIAL
C     IF RPUNIT IS SET AND RUNIT IS UNSET, RUNIT IS TAKEN FROM RPUNIT,
C     AND VICE VERSA. IF BOTH ARE SET, POTENL USES SEPARATE LENGTH UNITS.
C     IF NEITHER IS SET, THEY DEFAULT TO 1 (ANGSTROMS).
C     THE SCALING FACTOR RUNIT/RPUNIT IS PLACED IN RSCALE.
C
      MXLAM=MXLMDA
C
      RPUNIT=unset
      IF (ALLOCATED(LAM)) DEALLOCATE (LAM)
      ALLOCATE (LAMTMP(MXLMDA))
      CALL POTENL(-1,MXLAM,LAMTMP,RPUNIT,EP2CM,ITYPE,IPRINT)
      ITYP=MOD(ITYPE,10)
      NLAM=MXLAM*NLABV(ITYP)
      IF (NLAM.GT.MXLMDA) THEN
        WRITE(6,*)'***** ERROR: NOT ENOUGH SPACE FOR LAMBDA ARRAY'
        WRITE(6,*)'INCREASE MXLMDA IN SIZES MODULE AND RECOMPILE'
        WRITE(6,*)'MXLMDA NEEDS TO BE AT LEAST',NLAM
        STOP
      ENDIF
      ALLOCATE (LAM(NLAM))
      LAM=LAMTMP(1:NLAM)
      DEALLOCATE (LAMTMP)
C
      CALL CHCKRU(RUNIT,RUNAME,RPUNIT,RSCALE,unset,IPRINT)
C
C  THE PROGRAM WORKS WITH REDUCED ENERGIES IN UNITS OF 1/RUNIT**2
C  ALL DISTANCES ARE IN THESE R UNITS.
C  MULT BY CM2RU CONVERTS ENERGIES IN CM-1 INTO UNITS OF 1/RUNIT**2
C  MULT BY EP2RU CONVERTS INTERACTION POTENTIAL INTO UNITS OF 1/RUNIT**2
C
      CM2RU=URED*MUNIT*RUNIT*RUNIT/BFCT
      EP2RU=EP2CM*CM2RU
C
C  IF HARMONIC CONFINEMENT SPECIFIED, CALCULATE CONFINEMENT LENGTH
C  FROM FREQUENCY, OR VICE VERSA. FREQUENCY TAKES PRIORITY.
C
      IF (CONFRQ.GT.0.D0) THEN
        CONLEN=SQRT(2.D0/(EUNIT*CM2RU*CONFRQ))
        WRITE(6,1105) 'FREQUENCY',CONFRQ,EUNAME,'LENGTH',CONLEN,RUNAME
      ELSEIF (CONLEN.GT.0.D0) THEN
        CONFRQ=2.D0/(EUNIT*CM2RU*CONLEN**2)
        WRITE(6,1105) 'LENGTH',CONLEN,RUNAME,'FREQUENCY',CONFRQ,EUNAME
 1105 FORMAT('  BOUND STATES SUBJECT TO SPHERICAL CONFINEMENT IN',
     1       ' RELATIVE COORDINATE, INPUT AS'/
     2       '  HARMONIC ',A9,' =',F14.6,1X,A,', CORRESPONDING TO'/
     3       '  HARMONIC ',A9,' =',F14.6,1X,A/)
      ENDIF
C
      CALL GNHAM(IPRINT,ITYPE,NLABV(ITYP),MXLAM,NHAM,LAM)

      IF (IPRINT.GE.1) WRITE(6,1060)
C
C  PROCESS REQUESTED PROPAGATOR -- AND ITS INPUT DATA.
C
      CALL PROPST(RMIN,RMAX,DR,IRMSET,0,0.D0,
     1            INTFLG,IMGSEL,IPRINT,
     2            EPS,EPL,POWRX,TOLHI,DRMAX,NSTAB)
C  SET UP LOGICAL CONTROLLING USE OF SCRATCH FILES
      LSCRU=ISCRU.NE.0
      LSCRUR=.FALSE.
C  ONLY NEED FILE FOR R VALUES IF MORE THAN 1 FIELD AND USING VARIABLE STEPS
      IF (NFIELD.GT.1 .AND. LSCRU) THEN
        IF ((IPROPS.EQ.9 .AND. TOLHIS.GT.0.D0) .OR.
     1      (IPROPL.EQ.9 .AND. TOLHIL.GT.0.D0)) THEN
          LSCRUR=.TRUE.
          ISCRUR=ISCRU+1
        ENDIF
      ENDIF
C  ONLY NEED FILE FOR MATRICES IF MORE THAN 1 ENERGY
      IF (NNRG.EQ.1 .AND. LSCRU) THEN
        LSCRUR=.TRUE.
        ISCRUR=ISCRU
        ISCRU=0
        LSCRU=.FALSE.
      ENDIF
C
C  IF WAVEFUNCTIONS ARE REQUESTED, 3 EXTRA FILES ARE USED:
C  CHANNEL IWAVSC   SCRATCH  SAVING BITS REQUIRED FOR WAVEFN PROPAGATION
C  CHANNEL IPSISC   SCRATCH  SAVING WAVEFN AS CREATED BY MDPROP
C  CHANNEL IWAVE    FINAL FILE WITH WAVEFN SEQUENTIALLY FROM RMIN TO RMAX
C  OPEN TEMPORARY FILES LATER AS N IS REQUIRED FOR RECORD LENGTH
C
      IF (IWAVE.NE.0 .AND. IRMSET.NE.0) THEN
        WRITE(6,1109)
 1109   FORMAT(/' RMIN MUST BE FIXED IF WAVEFUNCTIONS ARE BEING ',
     1          'CALCULATED')
        IRMSET=0
      ENDIF
      WAVE=(IWAVE.NE.0 .AND. (IPROPS.EQ.6 .OR. IPROPS.EQ.9) .AND.
     1                       (IPROPL.EQ.6 .OR. IPROPL.EQ.9))
      IF (IWAVE.NE.0 .AND. .NOT.WAVE) THEN
        WRITE(6,1110) IWAVE
 1110   FORMAT(/'  CALCULATION OF WAVEFUNCTIONS IS IMPLEMENTED ONLY',
     1          ' FOR LDMD AND AIRY PROPAGATORS (IPROP = 6 AND 9):'/
     2          '  REQUEST FOR WAVEFUNCTIONS ON CHANNEL',I3,
     3          ' CANCELLED')
      ENDIF
C
C  WRITE HEADER ON WAVEFUNCTION FILE
C
      IF (WAVE) CALL WVHEAD(LABEL,ITYPE,IBOUND,NQN,URED,MUNIT,RUNIT,
     1                      EUNIT,CONLEN,RUNAME,EUNAME,EREF,IPRINT,IREF,
     2                      MONQN(1))
C
      IF (RCTRCT.GT.0.D0 .AND. (IPROPS.EQ.6 .OR. IPROPL.EQ.6)) THEN
        WRITE(6,*) ' CURRENT IMPLEMENTATION OF LDMD PROPAGATOR'//
     1             ' DOES NOT ALLOW CONTRACTED BASIS SETS: TRY'//
     2             ' LDJ PROPAGATOR (CODE 5) INSTEAD'
        STOP
      ENDIF
      IF (IPRINT.GE.1) WRITE(6,1060)
C
      EFIRST=EMIN*CM2RU
      EBAS=EFIRST
C
      IF (ITYPE.EQ.8) EBAS=100.D0

      EREF=EREF*EUNIT
      NJLQN(9)=NQN-1
      IF (ITYP.EQ.9 .AND. NJLQN9.NE.99999) NJLQN(9)=MIN(NJLQN9,NQN-1)
C
C  EINT AND JSINDX ARE NOT DEFINED YET BUT ARE NOT NEEDED IN THIS CALL
C
      IF (NCONST.EQ.0 .AND. NDGVL.EQ.0)
     1  CALL THRESH(DUMAR,N,CM2RU,MONQN,NQN,
     2              NJLQN(ITYP),EREF,IDUMAR,0)

!  Start of long IF block #1
      IF (IPRINT.GE.1) THEN
        IF (.NOT.LESCAN) THEN
          WRITE(6,'(/2X,A)') 'PROGRAM WILL ATTEMPT TO CONVERGE ON '//
     1                       'BOUND STATES AS A FUNCTION OF ENERGY'
          WRITE(6,1330)
 1330     FORMAT(/'  FOR EACH LEVEL, THE BISECTION METHOD WILL BE USED',
     1           ' UNTIL THE VWDB METHOD OFFERS RELIABLE CONVERGENCE.')
          WRITE(6,1340) DTOL/EUNIT,EUNAME
 1340     FORMAT(/'  VWDB CONVERGENCE WILL TERMINATE WHEN THE STEP ',
     1            'SIZE IS LESS THAN',1PG12.5,1X,A)
        ENDIF
C  IF EXTERNAL FIELDS OR POTENTIAL SCALING INCLUDED, WRITE SUMMARY
        IF (NEFV.GT.0 .OR. ISVEFV.EQ.0) THEN

C  WRITE HEADER
          CALL MSGEFV(0,NFIELD)

          DO IFLD=1,NFIELD
C  WRITE MESSAGE FOR THIS CYCLE
            CALL SETEFV(FIELD,SV_VAL)
            CALL MSGEFV(-IFLD,NFIELD)
          ENDDO

C  RESET VALUES READY FOR LOOP LATER ON
          CALL RSTEFV(FIXFLD)
        ENDIF

        IF (EUNITS.EQ.1) THEN
          IF (IPRINT.GE.1) WRITE(6,1200)
        ELSEIF (EUNITS.LE.9) THEN
          WRITE(6,1210) TRIM(EUNAME),EUNITS
        ELSE
          WRITE(6,1220) TRIM(EUNAME),EUNITS
        ENDIF
 1200   FORMAT(/'  INPUT ENERGIES IN UNITS OF CM-1 BY DEFAULT.')
 1210   FORMAT(/'  INPUT ENERGIES CONVERTED FROM ',A/
     1         '  TO INTERNAL WORKING UNITS OF CM-1 DUE TO INTEGER ',
     2         'INPUT EUNITS =',I4)
 1220   FORMAT(/'  INPUT ENERGIES CONVERTED FROM ',A/
     1          '  TO INTERNAL WORKING UNITS OF CM-1 DUE TO ',
     2          'ALPHANUMERIC INPUT EUNITS =',A4)

        IF (LESCAN) THEN
          WRITE(6,1300) EMIN/EUNIT,EUNAME,EMAX/EUNIT,EUNAME,
     1                  DNRG/EUNIT,EUNAME
 1300     FORMAT(/'  PROGRAM WILL SCAN ENERGY FROM ',1PG12.5,' ',A,
     1            ' TO',G12.5,' ',A,' IN STEPS OF ',G12.5,' ',A/
     2            '  CONVERGENCE WILL NOT BE ATTEMPTED')
        ELSE
          WRITE(6,1350) NODMIN,NODMAX,EMIN/EUNIT,EMAX/EUNIT,EUNAME
 1350     FORMAT(/'  PROGRAM WILL SEEK STATES WITH NODE COUNTS FROM',
     1            I5,' TO',I7/'  LYING IN ENERGY RANGE ',1PG12.5,
     2            ' TO ',G12.5,1X,A)
        ENDIF

        CALL EREFIN(MONQN,NQN,NJLQN(ITYP),EUNAME,EREF,EUNIT)
C
      ENDIF
!  End of long IF block #1
C
      CALL GCLOCK(TITIME)
      TTIME=TITIME-TFIRST
      TIMLST=TITIME

      IF (IPRINT.GE.1) THEN
        WRITE(6,1060)
        IF (JTOTL.LT.JTOTU) THEN
          WRITE(6,1500) JTOTL,JTOTU,JSTEP
        ELSE
          WRITE(6,1510) JTOTL
        ENDIF
 1500   FORMAT(/'  TOTAL ANGULAR MOMENTUM JTOT RUNS FROM',I4,'  TO',
     1          I6,'  IN STEPS OF',I4)
 1510   FORMAT(/'  TOTAL ANGULAR MOMENTUM JTOT =',I4)
      ENDIF
C
C  INCORPORATING POSSIBILITY OF LOOPING FROM IBFIX TO IBHI CRLS 25-07-18
      IF (IPRINT.GE.1 .AND. MXPAR.GT.1)
     1  WRITE(6,'(/2X,A,I3,A)') 'EACH JTOT IS SPLIT INTO A MAXIMUM OF ',
     1                          MXPAR,' SYMMETRY BLOCKS'
      IF (IBFIX.GT.0) IBHI=MAX(IBHI,IBFIX)
      IBMIN=MAX(1,IBFIX)
      IF (IBHI.EQ.0) IBHI=MXPAR
      IBMAX=MIN(IBHI,MXPAR)
      IF (IBFIX.GT.0 .AND. IPRINT.GE.1) THEN
        IF (IBMIN.LT.IBMAX) THEN
          WRITE(6,1600) IBMIN,IBMAX
        ELSE
          WRITE(6,1610) IBMIN
        ENDIF
      ENDIF
 1600 FORMAT(/'  CALCULATIONS WILL BE FOR SYMMETRY BLOCKS',
     1       I4,'  TO',I4)
 1610 FORMAT(/'  CALCULATIONS WILL BE FOR SYMMETRY BLOCK',
     1       I4,'  ONLY')

      IF (IPRINT.GE.1) WRITE(6,1900) TTIME
 1900 FORMAT(/'  INITIALIZATION DONE.  TIME WAS',F7.2,' CPU SECS.')
C
C
C  **************  LOOP OVER JTOT BEGINS HERE.  ******************
C
      IF (IPRINT.GE.1) WRITE(6,'(/1X,A)') TRIM(LABL)
!  Start of long DO loop #1
      LOOP_JTOT: DO JTOT=JTOTL,JTOTU,JSTEP
        THETA=THETLW+THETST*DBLE(JTOT)
C
C  ***************  LOOP OVER SYMMETRY BLOCKS BEGINS HERE  **************
C
!  Start of long DO loop #2
        DO IB=IBMIN,IBMAX

          IWRITR=LSCRUR
          IREADR=.FALSE.
          IF (LSCRU) THEN
            OPEN(ISCRU,STATUS='SCRATCH',FORM='UNFORMATTED')
            IF (IPRINT.GE.1)
     1        WRITE(6,*) ' SCRATCH FILE OPENED ON UNIT ',ISCRU
          ENDIF
          IF (LSCRUR) THEN
            OPEN(ISCRUR,STATUS='SCRATCH',FORM='UNFORMATTED')
            IF (IPRINT.GE.1)
     1        WRITE(6,*) ' SCRATCH FILE OPENED ON UNIT ',ISCRUR
          ENDIF
          IF (IPRINT.GE.1) WRITE(6,2100) JTOT,IB
 2100     FORMAT(/2X,29('*'),'  ANGULAR MOMENTUM JTOT  =',I4,'  ',
     1           ' AND SYMMETRY BLOCK  = ',I4,2X,28('*'))

          PHI=PHILW+PHIST*DBLE(IB-1)
          IVLU=IVLSAV
C
C  CHOOSE BASIS FUNCTIONS
C
          CALL BASE(JTOT,N,IDUMAR,IDUMAR,CM2RU,
     1              DUMAR,DUMAR,DUMAR2,IDUMA2,MXLAM,NHAM,
     2              LAM,DUMAR,WGHT,IEXCH,THETA,PHI,IB,
     3              .TRUE.,EBAS,NSTATE,IPRINT,IBOUND,
     4              DUMAR,QNAME)
C
C  N IS THE NUMBER OF BASIS FUNCTIONS
C  SKIP THIS JTOT,IB IF NO CHANNELS
C
          IF (N.LE.0) CYCLE
          NSQ = N*N
C
C  ALLOCATE STORAGE FOR COUPLED EQUATION SOLVER.
C
          ALLOCATE (JSINDX(N),EINT(N),CENT(N),WVEC(N),L(N))
          NV=N*(N+1)/2
          IF (IVLU.EQ.0) THEN
            NVL=NVLBLK
          ELSE
            NVL=1
          ENDIF
          ALLOCATE (VL(NVL,NV))
          IF (IVLFL.GT.0) THEN
            ALLOCATE (IV(NVL,NV))
          ELSE
            ALLOCATE (IV(1,1))
          ENDIF
          IF (NCONST.GT.0) THEN
            ALLOCATE (DGVL(N,NDGVL))
          ELSE
            ALLOCATE (DGVL(1,1))
          ENDIF
C
C  SET UP BASIS FUNCTIONS IN ALLOCATED STORAGE
C
          CALL BASE(JTOT,N,JSINDX,L,CM2RU,
     1              EINT,CENT,VL,IV,MXLAM,NHAM,
     2              LAM,WVEC,WGHT,IEXCH,THETA,PHI,IB,
     3              .FALSE.,EBAS,NSTATE,IPRINT,IBOUND,
     4              DGVL,QNAME)
          IF (WAVE) CALL WVSBLK(JTOT,IB,N,NQN,NSTATE,JSTATE,
     1                          JSINDX,L,CENT,EREF,
     2                          EUNIT,EUNAME,QNAME,IBOUND,IPRINT,
     3                          NFIELD,'FIELD'//PLUR(MIN(NFIELD,2)))
C
C  LOOP OVER EXTERNAL FIELDS (NOT USED BY DEFAULT)
C
          OLDFAC=0.D0
C
C  ******************  LOOP OVER FIELDS BEGINS HERE  ******************
C
!  Start of long DO loop #3
          LOOP_IFLD: DO IFLD=1,NFIELD
            IWRITE=LSCRU
            IREAD=.FALSE.
            CALL SETEFV(FIELD,FLDNOW)
            IF (IPRINT.GE.1) THEN
              IF (IPRINT.GE.2 .AND. IFLD.GT.1) WRITE(6,2300)
 2300         FORMAT(2X,59('= '))
              IF (LENEFV.GT.0) CALL MSGEFV(IFLD,NFIELD)
            ENDIF
C
C  CHECK THAT RMAX IS BEYOND CENTRIFUGAL BARRIER
            IF (RCTRCT.GT.0.D0) THEN
              ALLOCATE (VLTEMP(NHAM,N*(N+1)/2))
              ECTRED=ECTRCT*CM2RU
C  NOTE THAT CNTRCT INCREASES NHAM BY 2 TO FIT 2 EXTRA BLOCKS INTO THE
C  VL ARRAY FOR THE NOW NON-DIAGONAL INTERNAL HAMILTONIAN AND THE
C  CENTRIFUGAL TERM.  NCONST=1 AND NRSQ=1 AFTER THIS CALL
              CALL CNTRCT(N,MCTRCT,RCTRCT,ECTRED,VLTEMP,
     1                    VL,IV,EINT,CENT,
     2                    EP2RU,CM2RU,RSCALE,MXLAM,NHAM,IPRINT)
              N=MCTRCT
C
C  WILL NEED THIS EXTRA CODE ONCE VL IS CONVERTED TO ALLOCATABLE ARRAY
              DEALLOCATE (VL)
              ALLOCATE (VL(NHAM,N*(N+1)/2))
              NVLC=N*(N+1)/2
              DO IHAM=1,NHAM
                DO IJ=1,NVLC
                  VL(IHAM,IJ)=VLTEMP(IHAM,IJ)
                ENDDO
              ENDDO
              DEALLOCATE (VLTEMP)
            ENDIF
C
C  FOR CASES WHERE THE HAMILTONIAN IS DIAGONAL AT INFINITY, BUT THE INTERACTION
C  ENERGY IS DEPENDENT ON EFVS, CHEINT WILL SHIFT THE VALUES IN EINT
C
            IPERTN=0
            IF (NCONST.EQ.0 .AND. NDGVL.GT.0) THEN
              CALL CHEINT(EINT,DGVL,N,OLDFAC,CM2RU)
              IF (IPRINT.GE.6) CALL THRLST(N,EINT,CENT,CM2RU,
     1                                     L,IBOUND,EUNIT,EUNAME)
            ENDIF
C
C  FOR CASES WHERE THE HAMILTONIAN CONTAINS OFF-DIAGONAL TERMS
C  AT INFINITY, YTRANS PROVIDES A CONVENIENT WAY TO GET EINT
C
            EUBD=EMAX
            IF (NCONST.GT.0 .OR. NRSQ.GT.0) THEN
              ALLOCATE (ESCR(N*N))
              CALL YTRANS(DUM,ESCR,EINT,WVEC,
     1                    JSINDX,L,N,VL,IV,
     2                    MXLAM,NHAM,ERED,EP2RU,CM2RU,DEGTOL,
     3                    NOPEN,IBOUND,CENT,IPRINT,.FALSE.)
              DEALLOCATE (ESCR)
              EUBD=EINT(1)
              DO I=2,N
                EUBD=MIN(EINT(I),EUBD)
              ENDDO
C
              IF (IPRINT.GE.6) CALL THRLST(N,EINT,CENT,CM2RU,
     1                                     L,IBOUND,EUNIT,EUNAME)
            ENDIF
C
C
C  CALCULATE EREF
C
            IF (IREF.NE.0 .OR. MONQN(1).NE.-99999)
     1        CALL THRESH(EINT,N,CM2RU,MONQN,NQN,
     2                    NJLQN(ITYP),EREF,JSINDX,IPRINT)
            EUBD=EUBD/CM2RU-EREF
            EMXBD=MIN(EMAX,EUBD)
            IF (.NOT.EMAXBD) EMXBD=EMAX
            EMINB=EMIN+EREF
            EMAXB=EMXBD+EREF
            IF (EMAXB.LT.EMINB) CYCLE
            ERED=EMAXB*CM2RU
            IF (IPRINT.GE.3) CALL PREREF(EREF,EUNIT,EUNAME)
            IF (IRMSET.GT.0 .AND. IFLD.EQ.1) THEN
              RMNINT=RMIN
              CALL FINDRM(N,RMNINT,RTURN,VL,IV,ERED,EINT,CENT,
     1                    MXLAM,NHAM,EP2RU,CM2RU,RSCALE,IRMSET,
     2                    LTYP8,IPRINT)
            ENDIF

            CAYS=CALCK(EPS*CM2RU,ERED,EINT,N)
            IF (STEPS.GT.0.D0 .AND. CAYS.EQ.0.D0) THEN
              WRITE(6,*) ' *** ERROR: EKIN+EPS IS NOT +VE'
              STOP
            ENDIF
            CAYL=CALCK(EPL*CM2RU,ERED,EINT,N)
            IF (STEPL.GT.0.D0 .AND. CAYL.EQ.0.D0) THEN
              WRITE(6,*) ' *** ERROR: EKIN+EPL IS NOT +VE'
              STOP
            ENDIF
C
C  ******************  LOOP OVER ENERGIES BEGINS HERE  ******************
C
C
C  ALLOCATE STORAGE USED FOR EIGENVECTORS OF SMALLEST EIGENVALUE AT
C  EMIN AND EMAX
            ALLOCATE (EVEMIN(N),EVEMAX(N))

            ALLOCATE (EVBDST(N))
!  Start of long DO loop #4
            DO INRG=1,NNRG
              IF (.NOT.LESCAN) THEN
                IF (INRG.EQ.1) ENERGY=EMXBD
                IF (INRG.EQ.2) ENERGY=EMIN
                IF (INRG.GT.2) EXIT
              ELSE
                ENERGY=EMIN+DNRG*DBLE(INRG-1)
                IF (ENERGY.GT.EMXBD) THEN
                  DEALLOCATE (EVEMIN,EVEMAX,EVBDST)
                  CYCLE LOOP_IFLD
                ENDIF
              ENDIF
              ENERB=ENERGY+EREF
              ERED=ENERB*CM2RU
              SVVAL=ENERGY/EUNIT
              IF (.NOT.LESCAN) THEN
                IF (IPRINT.GE.7) CALL PRPROP(SVNAME,SVVAL,SVUNIT)
              ELSE
                IF (IPRINT.GE.1) CALL PRPRCT(INRG,SVNAME,SVVAL,SVUNIT)
              ENDIF
              IF (IPRINT.GE.8) CALL PREABS(ENERB,EREF,EUNIT,SVUNIT)

              CALL BDCTRL(N, MXLAM, NHAM, EVBDST, VL, IV,
     2                    EINT, CENT, NODE, ERED,
     3                    EP2RU, CM2RU, RSCALE, EIGMIN, .FALSE.,
     4                    IMIN, IPRINT)
              IREAD=LSCRU
              IREADR=LSCRUR
              IWRITE=.FALSE.
              IWRITR=.FALSE.
              IF (LESCAN .AND. IPRINT.GE.1) CALL PREVAL(NODE,EIGMIN)
C
              CALL GCLOCK(TITIME)
              TTIME=TITIME-TIMLST
              TIMLST=TITIME
C
              CALL FLUSH(6)

              IF (INRG.EQ.1) THEN
                IF (NODE.EQ.0 .AND. .NOT.LESCAN) THEN
                  IF (IPRINT.GE.2) WRITE(6,2400) EMXBD/EUNIT,
     1                                           TRIM(EUNAME)
 2400             FORMAT(/'  NO NODES FOUND AT EMAX = ',1PG17.10,1X,A,
     1                   '. END SEARCH.')
                  DEALLOCATE (EVEMIN,EVEMAX,EVBDST)
                  CYCLE LOOP_IFLD
                ENDIF
                NODHI=NODE
                EIGHIG=EIGMIN
                IHIGH=IMIN
                CALL DCOPY(N,EVBDST,1,EVEMAX,1)
              ELSE
                NODLO=NODE
                EIGLOW=EIGMIN
                ILOW=IMIN
                IF (INRG.EQ.2 .AND. .NOT.LESCAN)
     1            CALL DCOPY(N,EVBDST,1,EVEMIN,1)
              ENDIF
C
              IF ((INRG.EQ.2 .AND. .NOT.LESCAN) .AND. IPRINT.GE.2)
     1          CALL PRMNMX(NODLO,NODHI,EMIN/EUNIT,EMXBD/EUNIT,SVNAME,
     2                      SVUNIT)

              NCALC=NCALC+1
              NFOUND=0
              IF (NCALC.GE.MXCALC) GOTO 390
            ENDDO
!  End of long DO loop #4

            IF (LESCAN) THEN
              DEALLOCATE (EVEMIN,EVEMAX,EVBDST)
              CYCLE LOOP_IFLD
            ENDIF
C

            NBDST=MIN(NODHI-NODLO,MXNODE)
            IF (WAVE) CALL WVSETS(DUMMY,EUNIT,EUNAME,EREF,IFLD,
     1                            NODLO+1,NODHI)
            IF (NODHI.EQ.NODLO) THEN
              IF (IPRINT.GE.3)
     1          WRITE(6,2410)
 2410         FORMAT(/'  NO NODES BETWEEN EMIN AND EMAX.'/
     1               '  CONVERGENCE WILL BE SKIPPED FOR THIS JTOT AND ',
     2               'SYMMETRY BLOCK.')
            ENDIF
C
C  ALLOCATE STORAGE FOR EIGENVECTORS OF SMALLEST EIGENVALUE AT RANGE
C  ENDPOINTS FOR EACH NODE
C
            ALLOCATE (EVELO(N,NBDST),EVEHI(N,NBDST))
C
C  LOOP OVER ALL NODES, SETTING UP INITIAL VALUES FOR RANGE ENDPOINTS
C
            DO I=1,NBDST

              ELO(I)=EMINB
              NLO(I)=NODLO
              EIGLO(I)=EIGLOW
              ILO(I)=ILOW
              CALL DCOPY(N,EVEMIN,1,EVELO(1,I),1)
              EHI(I)=EMAXB
              NHI(I)=NODHI
              EIGHI(I)=EIGHIG
              IHI(I)=IHIGH
              CALL DCOPY(N,EVEMAX,1,EVEHI(1,I),1)
            ENDDO
            DEALLOCATE (EVEMIN,EVEMAX)
C
C  ***************  LOOP OVER NODES BEGINS HERE  ************************
C
C  FROM NOW ON, THE CLOSEST AVAILABLE BOUNDS ON THE ENERGY OF NODE
C  NUMBER NODE WILL BE STORED IN EHI(NODE-NODLO) AND ELO(NODE-NODLO).
C
!  Start of long DO loop #5
            DO IBDST=1,NBDST
              NPROP=1
              TNTIME=TITIME
              NSEEK=IBDST+NODLO
              IF (NSEEK.LT.NODMIN .OR. NSEEK.GT.NODMAX) CYCLE
              IF (IPRINT.GE.2) CALL PRNDCT(NSEEK)
C
C  START BISECTION TO FIND THIS NODE
C
  510         EHALF=0.5D0*(ELO(IBDST)+EHI(IBDST))
              NCALC=NCALC+1
              IF (NCALC.GT.MXCALC) THEN
                NFOUND=IBDST-1
                GOTO 390
              ENDIF
              SVMIN=(ELO(IBDST)-EREF)/EUNIT
              SVMAX=(EHI(IBDST)-EREF)/EUNIT
              SVVAL=(EHALF-EREF)/EUNIT

              IF (IPRINT.GE.7) CALL PRBIS(SVMIN,SVMAX,SVVAL,SVUNIT,
     1                                    'BISECTION')
              IF (IPRINT.GE.8) CALL PREABS(EHALF,EREF,EUNIT,SVUNIT)
              ERED=EHALF*CM2RU
              IF (IPRINT.GE.6) CALL PRPRCT(NPROP,SVNAME,SVVAL,SVUNIT)

              CALL BDCTRL(N, MXLAM, NHAM, EVBDST, VL, IV,
     2                    EINT, CENT, NODE, ERED,
     3                    EP2RU, CM2RU, RSCALE, EIGMIN, .FALSE.,
     4                    IMIN, IPRINT)

              IF (IPRINT.GE.7) CALL PREVAL(NODE,EIGMIN)
              NPROP=NPROP+1
C
              CALL GCLOCK(TITIME)
              TTIME=TITIME-TIMLST
              TIMLST=TITIME
C
              CALL FLUSH(6)
C
C  LOOP OVER THIS NODE AND THE REST, UPDATING INITIAL VALUES FOR RANGE
C  ENDPOINTS
              DO I=IBDST,NBDST
                IF (NODE.GE.NODLO+I) THEN
                  IF (EHALF.LT.EHI(I)) THEN
                    EHI(I)=EHALF
                    NHI(I)=NODE
                    EIGHI(I)=EIGMIN
                    IHI(I)=IMIN
                    CALL DCOPY(N,EVBDST,1,EVEHI(1,I),1)
                  ENDIF
                ELSE
                  IF (EHALF.GT.ELO(I)) THEN
                    ELO(I)=EHALF
                    NLO(I)=NODE
                    EIGLO(I)=EIGMIN
                    ILO(I)=IMIN
                    CALL DCOPY(N,EVBDST,1,EVELO(1,I),1)
                  ENDIF
                ENDIF
              ENDDO
C
              IF (NHI(IBDST)-NLO(IBDST).GT.1) GOTO 510
C
              IF (NHI(IBDST).LE.NLO(IBDST) .OR.
     1            EHI(IBDST).LE.ELO(IBDST)) THEN
                WRITE(6,2500) NSEEK,IBDST,
     1                       (NLO(I),ELO(I),NHI(I),EHI(I),I=1,NBDST)
 2500           FORMAT(/'  ERROR IN NODE COUNT LOGIC.'/
     1                 '  CURRENTLY SEARCHING FOR BOUND STATE',I4,
     2                 ' (INDEX NUMBER',I3,')'/
     3                 '  NLO       ELO         NHI      EHI'/
     4                 (2(I5,F16.5)))
                STOP
              ENDIF
C
C  ARRIVE HERE IF ONLY THE REQUIRED NODE LIES IN THIS INTERVAL.
C  DECIDE WHETHER TO USE VWDB METHOD OR CONTINUE BISECTION.
C
C  ONLY SAFE TO USE VWDB METHOD IF WE ARE ON THE CORRECT BRANCH
C  OF THE MATCHING EIGENVALUE. IT SHOULD BE SAFE TO WAIT UNTIL
C  THE EIGENVALUES WITH THE SMALLEST ABSOLUTE VALUES ARE BOTH
C  OF THE CORRECT SIGN.
C
C  PROBLEMS SOMETIMES ARISE BECAUSE THE NODE COUNT CHANGES AT
C  AN ENERGY SLIGHTLY AWAY FROM THE ZERO IN THE EIGENVALUE.
C  AVOID FURTHER BISECTION IF EHI AND ELO ARE TOO CLOSE
C
              IF ((EIGHI(IBDST).GT.0.D0 .OR. EIGLO(IBDST).LT.0.D0)
     1            .AND.
     2            EHI(IBDST)-ELO(IBDST).GT.100.D0*DTOL) GOTO 510
C
C  START THE VWDB METHOD WITH A POINT EITHER SIDE
C
              IF (EIGHI(IBDST)*EIGLO(IBDST).GE.0.D0) THEN
                IF (EHI(IBDST)-ELO(IBDST).GT.100.D0*DTOL) GOTO 510
                WRITE(6,*) ' *** BISECTION TO FIND EIGENVALUE '//
     1                     'CROSSING ZERO IN MATCHING MATRIX FOR '//
     2                     'BOUND STATE',IBDST,'HAS FAILED'
                IF (IPRINT.GE.8) THEN
                  WRITE(6,*) ' LATEST VALUES FOR ENERGIES AND ',
     1                       'EIGENVALUES ARE',ELO(IBDST),EIGLO(IBDST),
     2                                         EHI(IBDST),EIGHI(IBDST)
                ENDIF
                CYCLE
              ENDIF
C
C  EIGENVECTOR FOR SMALLEST EIGENVALUE IN CURRENT SEARCH IS STORED AT
C  POSITION IVSMLL (WHICH IS THE SAME AS IVLO - EVECS AT RANGE ENDPOINTS
C  NO LONGER NEEDED FOR THE CURRENT NODE)
C
              ALLOCATE (EVSMLL(N))
              IF (ABS(EIGHI(IBDST)).LT.ABS(EIGLO(IBDST))) THEN
                ENOW=EHI(IBDST)
                EIGNOW=EIGHI(IBDST)
                ESMALL=EIGNOW
                ISMALL=IHI(IBDST)
                CALL DCOPY(N,EVEHI(1,IBDST),1,EVSMLL,1)
                ELST=ELO(IBDST)
                EIGLST=EIGLO(IBDST)
              ELSE
                ENOW=ELO(IBDST)
                EIGNOW=EIGLO(IBDST)
                ESMALL=EIGNOW
                ISMALL=ILO(IBDST)
                CALL DCOPY(N,EVELO(1,IBDST),1,EVSMLL,1)
                ELST=EHI(IBDST)
                EIGLST=EIGHI(IBDST)
              ENDIF

              IF (IPRINT.GE.7) CALL PRBEND('VWDB METHOD')

              DLAST=1.D30
C
              XA=ELST
              XC=XA
              XB=ENOW
              FA=EIGLST
              FB=EIGNOW
              FC=FA
              SV1=(XA-EREF)/EUNIT
              SV2=(XB-EREF)/EUNIT
              IF (IPRINT.GE.7) CALL PRVWST(SV1,SV2,FA,FB,SVUNIT)

              CONVGE=.FALSE.
              ENOISE=.FALSE.
!  Start of long DO loop #6
              DO ITER=1,NITER
C
C  CALCULATE THE NEXT ENERGY USING THE VWDB ALGORITHM
C
                SV1=(XA-EREF)/EUNIT
                SV2=(XB-EREF)/EUNIT
                SV3=(XC-EREF)/EUNIT
                ENEW=BRENT(XA,XB,XC,FA,FB,FC,ITER.EQ.1,DTOL,
     1                     CONVGE,METHOD)
                SVVAL=(ENEW-EREF)/EUNIT
                IF (IPRINT.GE.7) CALL PRVWDB(SV1,SV2,SV3,SVNAME,SVUNIT,
     1                                       METHOD,SVVAL)
                DE=ENEW-XB
C
                NCALC=NCALC+1
                IF (NCALC.GT.MXCALC) THEN
                  NFOUND=IBDST-1
                  GOTO 390
                ENDIF

                ERED=ENEW*CM2RU
                IF (IPRINT.GE.8) CALL PREABS(ENEW,EREF,EUNIT,SVUNIT)
                IF (CONVGE) EXIT

                IF (IPRINT.GE.6) CALL PRPRCT(NPROP,SVNAME,SVVAL,SVUNIT)

                CALL BDCTRL(N, MXLAM, NHAM, EVBDST, VL, IV,
     2                      EINT, CENT, NODE, ERED,
     3                      EP2RU, CM2RU, RSCALE, EIGMIN, .FALSE.,
     4                      IMIN, IPRINT)

                IF (ABS(EIGMIN).LT.ABS(ESMALL)) THEN
C  UPDATE SMALLEST EIGENVALUE, EIGENVECTOR OF SMALLEST EIGENVALUE AND
C  INDEX
                  ESMALL=EIGMIN
                  ISMALL=IMIN
                  CALL DCOPY(N,EVBDST,1,EVSMLL,1)
                ENDIF

                IF (IPRINT.GE.7) CALL PREVAL(NODE,EIGMIN)
                NPROP=NPROP+1
C
                CALL GCLOCK(TITIME)
                TTIME=TITIME-TIMLST
                TIMLST=TITIME
C
                CALL FLUSH(6)

                IF (EIGMIN*FA.GT.0.D0) THEN
                  FCOMP=FA
                ELSE
                  FCOMP=FB
                ENDIF
                IF (ABS(EIGMIN).GT.ABS(FCOMP)) THEN
                  ENOISE=.TRUE.
                  IF (ABS(FA).LT.ABS(FB)) THEN
                    FSMALL=FA
                    XSMALL=(XA-EREF)/EUNIT
                  ELSE
                    FSMALL=FB
                    XSMALL=(XB-EREF)/EUNIT
                  ENDIF
                  EXIT
                ENDIF
C
C  SET UP FOR NEXT CYCLE OF BRENT ALGORITHM
C  (CONTRAPOINT (XA,FA) AND CURRENT ITERATE (XB,FB) MUST BRACKET THE ROOT)
                IF (FA*EIGMIN.LT.0.D0) THEN
                  XB=ENEW
                  FB=EIGMIN
                ELSE
                  XA=ENEW
                  FA=EIGMIN
                ENDIF
C
              ENDDO
!  End of long DO loop #6

              IF (.NOT.CONVGE) THEN
                WRITE(6,*) ' BOUND STATE',IBDST,'NOT CONVERGED IN',
     1                     NITER,'ITERATIONS'
              ENDIF
C
              ZCNTN=(ENEW.LE.EHI(IBDST) .AND. ENEW.GE.ELO(IBDST))
              ECM=ENEW-EREF

              IF (IBDSUM.GT.0)
     1          CALL OUTEFV(NSEEK,ECM,EUNIT,EUNAME,NODE,ZCNTN,CONVGE,
     2                      ENOISE,IBDSUM)
              IF (IPRINT.GE.1) THEN
                IF (ENOISE) THEN
                  CALL PRINCR(SVVAL,SVUNIT,METHOD,EIGMIN,NSEEK,
     1                        FSMALL,XSMALL,CONVGE)
                ELSE
                  CALL PRCONV(NSEEK,SVNAME,SVVAL,SVUNIT,NODE,ZCNTN,
     1                        CONVGE,METHOD)
                ENDIF
              ENDIF
              IF (IPRINT.GE.6) CALL PRLAST(DE/EUNIT,SVUNIT)
              IF (IPRINT.GE.8) CALL PRLOC(TITIME-TNTIME)
C
C  SPECIAL CODE FOR DERIVATIVE WITH RESPECT TO EFV
C
              IF (DFIELD.LT.FLDMAX-FLDMIN .AND. IFLD.GT.1 .AND.
     1            NPERT.EQ.0 .AND. IPRINT.GE.6) THEN
                XJAC=(ENEW-EVAL(IBDST,1))/(EUNIT*DFIELD)
                WRITE(6,*) ' 1-POINT DERIVATIVE OF ABS ENERGY WRT EFV',
     1                     ' AT IFLD = ',IFLD,' IS ',XJAC
              ENDIF
C
              EVAL(IBDST,1)=ENEW
C
cINOLLS call save_res((enew-eref)/eunit)
C
              NCHECK(IBDST)=NSEEK
C
              IF (IPRINT.GE.5 .OR. WAVE) THEN
                CALL EVMTCH(EVSMLL,ESMALL,ISMALL,JSTATE,
     2                      L,JSINDX,N,NSTATE,NQN,IPRINT)
              ENDIF
              DEALLOCATE (EVSMLL)
C  END OF BOUND-STATE LOCATION SECTION
C ===========================================================================
C
C  IF WAVEFUNCTIONS REQUIRED DO A FINAL PROPAGATION TO SAVE
C  NECESSARY INFORMATION FOR BACK-SUBSTITUTION
C
              IF (WAVE) THEN
C
C  WRITE OUT HEADER FOR THIS WAVEFN
C
                IF (IPRINT.GE.11) WRITE(6,2580) IPSISC
 2580           FORMAT(/'  WAVEFUNCTIONS REQUESTED. FINAL PROPAGATION ',
     1                 'WILL WRITE OUT INFORMATION ON CHANNEL ',I3)
                ISTATE=NSEEK
                IF (.NOT.CONVGE) ISTATE=-NSEEK
                CALL WVCURR(ISTATE,ENEW,EREF,EUNIT,EUNAME)

                CALL BDCTRL(N, MXLAM, NHAM, EVBDST, VL, IV,
     2                      EINT, CENT, NODE, ERED,
     3                      EP2RU, CM2RU, RSCALE, EIGMIN, .TRUE.,
     4                      IMIN, IPRINT)
C
                CALL GCLOCK(TITIME)
                TTIME=TITIME-TIMLST
                TIMLST=TITIME
              ENDIF
C
            ENDDO
!  End of long DO loop #5
  390       IF (NCALC.GE.MXCALC) THEN
              NBDST=NFOUND
              IF (IPRINT.GE.1) WRITE(6,2390) MXCALC,NFOUND
 2390         FORMAT(/'  *** WARNING. MXCALC =',I5,' REACHED AFTER ONLY'
     1               ,I5,' NODES FOUND.')
              EXIT LOOP_JTOT
            ENDIF
C  RELEASE STORAGE USED FOR STORING EIGENVECTORS OF SMALLEST EIGENVALUES
            IF (ALLOCATED(EVELO)) DEALLOCATE (EVELO,EVEHI)
C
C  END OF WAVEFUNCTION CALCULATION
C ============================================================================
C
C  NOW CALCULATE EXPECTATION VALUES OR DO CONVERGENCE TESTING.
C  USE STORED EIGENVALUES AS STARTING POINT FOR VWDB METHOD.
C
            ISTOR=1
C
C  CYCLE THROUGH PERTURBATIONS (IPERT=0 CORRESPONDS TO ENERGIES THEMSELVES)
!  Start of long DO loop #7
            DO IPERT=0,NPERT
C
!  Start of long IF block #2
            IF (IPERT.EQ.0) THEN
              IPERTN=0
              EREFP(IPERT)=EREF
            ELSE
              IPERTN=IPPERT(IPERT)
              NPOWN=NPOW(IPERT)
              DELTAN=DELTA(IPERT)
              IF (IPERTN.GT.0 .AND. IPERTN.LE.MXLAM)
     1          DELTAN=DELTAN*EUNIT/EP2CM
              IF (IPRINT.GE.1) WRITE(6,6100) IPERTN,NPOWN,DELTA(IPERT)
 6100         FORMAT(/2X,59('==')/'  CALCULATE EXPECTATION VALUES ',
     1               'OF POTENTIAL(',I3,')/R**',I2,' WITH DELTA = ',
     2               1PE10.3)
C
C  FOR CASES WHERE THE HAMILTONIAN IS DIAGONAL AT INFINITY, BUT THE INTERACTION
C  ENERGY IS DEPENDENT ON EFVS, CHEINT WILL SHIFT THE VALUES IN EINT
C
            IF (NCONST.EQ.0 .AND. NDGVL.GT.0) THEN
              CALL CHEINT(EINT,DGVL,N,OLDFAC,CM2RU)
              IF (IPRINT.GE.6) CALL THRLST(N,EINT,CENT,CM2RU,
     1                                     L,IBOUND,EUNIT,EUNAME)
            ENDIF
              IF (NCONST.GT.0) THEN
                ALLOCATE (ESCR(N*N))
                CALL YTRANS(DUM,ESCR,EINT,WVEC,
     1                      JSINDX,L,N,VL,IV,
     2                      MXLAM,NHAM,ERED,EP2RU,CM2RU,DEGTOL,
     3                      NOPEN,IBOUND,CENT,IPRINT,.FALSE.)
                DEALLOCATE (ESCR)
C
                IF (IPRINT.GE.6) CALL THRLST(N,EINT,CENT,CM2RU,
     1                                       L,IBOUND,EUNIT,EUNAME)
              ENDIF
C
C
C  CALCULATE EREF, SHIFTED BY THE PERTURBATION IF APPROPRIATE
C
              IF (IREF.NE.0 .OR. MONQN(1).NE.-99999) THEN
                CALL THRESH(EINT,N,CM2RU,MONQN,NQN,
     1                    NJLQN(ITYP),EREFIP,JSINDX,IPRINT)
              ENDIF
              EREFP(IPERT)=EREFIP
              IF (IPRINT.GE.3. AND. EREFIP.NE.EREF)
     1            CALL PREREF(EREFIP,EUNIT,EUNAME)
            ENDIF
!  End of long IF block #2
C
            DRSSTR=DRS
            DRLSTR=DRL
            RMNSTR=RMNINT
            RMXSTR=RMXINT
C
C  CYCLE THROUGH CONVERGENCE TESTS (ICONV=0 CORRESPONDS TO ORIGINAL
C  CALCULATIONS)
!  Start of long DO loop #8
              DO ICONV=0,NCONV
                IF (IPERT.EQ.0 .AND. ICONV.EQ.0) CYCLE
                ISTOR=ISTOR+1
C
                IF (ICONV.GT.0 .AND. NBDST.GT.0) THEN
                  IF (ICON.EQ.1) THEN
                    FAC=2.D0
                    DRS=DRS*FAC
                    IF (DRL.NE.unset) THEN
                      DRL=DRL*FAC
                      IF (IPRINT.GE.1) WRITE(6,6200) 'DR',DRS,DRL
                    ELSE
                      IF (IPRINT.GE.1) WRITE(6,6200) 'DRS',DRS
                    ENDIF
                  ELSEIF (ICON.EQ.2) THEN
                    RMXINT=RMXINT-DRCON
                    IF (IPRINT.GE.1) WRITE(6,6200) 'RMAX',RMXINT
                  ELSEIF (ICON.EQ.3) THEN
                    RMNINT=RMNINT+DRCON
                    IF (IPRINT.GE.1) WRITE(6,6200) 'RMIN',RMNINT
                  ELSE
                    WRITE(6,*) ' *** ERROR. ICON =',ICON,
     1                         ' NOT IMPLEMENTED.'
                    STOP
                  ENDIF
                ENDIF
C
 6200           FORMAT(/2X,59('--')/'  TESTING CONVERGENCE OF ',
     1                 'PROPAGATION PARAMETERS: ',A,' CHANGED TO ',
     2                 1PG13.5,:' & ',G13.5)
C
C  BEGINNING OF LOOP FOR PERTURBATION CALCULATIONS / CONVERGENCE TESTING
                IWRITR=LSCRUR
                IREADR=.FALSE.
!  Start of long DO loop #9
                DO IBDST=1,NBDST
                  NPROP=1
                  NSEEK=IBDST+NODLO
                  IF (NSEEK.LT.NODMIN .OR. NSEEK.GT.NODMAX) CYCLE
C
C  GET EIGMIN FOR THE NEW PROBLEM NEAR THE PREVIOUS EIGENVALUE
C
                  IWRITE=LSCRU
                  IREAD=.FALSE.
                  DE=100.D0*DTOL
                  ENOW=EVAL(IBDST,1)-DE
                  SVVAL=(ENOW-EREFP(IPERT))/EUNIT
                  IF (IPRINT.GE.7) CALL PRPROP(SVNAME,SVVAL,SVUNIT)
                  IF (IPRINT.GE.8) CALL PREABS(ENOW,EREFP(IPERT),
     1                                         EUNIT,SVUNIT)
                  ERED=ENOW*CM2RU
                  IF (IPRINT.GE.6) CALL PRPRCT(NPROP,
     1                            'STARTING VALUE OF E',SVVAL,SVUNIT)

                  CALL BDCTRL(N, MXLAM, NHAM,EVBDST,VL, IV,
     2                        EINT, CENT, NODE, ERED,
     3                        EP2RU, CM2RU, RSCALE, EIGMIN, .FALSE.,
     4                        IMIN, IPRINT)
                  IREAD=LSCRU
                  IREADR=LSCRUR
                  IWRITE=.FALSE.
                  IWRITR=.FALSE.

                  IF (IPRINT.GE.7) CALL PREVAL(NODE,EIGMIN)
                  NPROP=NPROP+1
C
                  CALL GCLOCK(TITIME)
                  TTIME=TITIME-TIMLST
                  TIMLST=TITIME
C
C  SET UP A FAKE PREVIOUS POINT TO GET THE VWDB METHOD STARTED
C  WITH THE NEXT POINT AT EXACTLY THE PREVIOUS EIGENVALUE
C
                  EIGNOW=EIGMIN
                  ELST=ENOW+DE
                  EIGLST=0.D0
C
                  EIGNOW=EIGMIN
                  CONVGE=.FALSE.
                  METHOD='SECANT      '
                  ZBRENT=.FALSE.
!  Start of long DO loop #10
                  DO ITER=1,NITER

!  Start of long IF block #3
                    IF (EIGLST*EIGMIN.LT.0.D0 .OR. ZBRENT) THEN
                      IF (.NOT.ZBRENT) THEN
                        XA=ELST
                        XB=ENOW
                        FA=EIGLST
                        FB=EIGNOW
                        FC=FA
                      ENDIF
C
C  EITHER CALCULATE THE NEXT ENERGY USING THE VWDB ALGORITHM
C
                      SV1=(XA-EREFP(IPERT))/EUNIT
                      SV2=(XB-EREFP(IPERT))/EUNIT
                      SV3=(XC-EREFP(IPERT))/EUNIT
                      ENEW=BRENT(XA,XB,XC,FA,FB,FC,.NOT.ZBRENT,DTOL,
     1                           CONVGE,METHOD)
                      SVVAL=(ENEW-EREFP(IPERT))/EUNIT
                      IF (IPRINT.GE.7) CALL PRVWDB(SV1,SV2,SV3,SVNAME,
     1                                             SVUNIT,METHOD,SVVAL)
                      ZBRENT=.TRUE.
                    ELSE
C
C  OR CALCULATE THE NEXT ENERGY USING SECANT
C
                      ENEW=ENOW-EIGNOW*(ENOW-ELST)/(EIGNOW-EIGLST)
                      IF (IPRINT.GE.7) THEN
                        SVMIN=(ELST-EREFP(IPERT))/EUNIT
                        SVMAX=(ENOW-EREFP(IPERT))/EUNIT
                        SVVAL=(ENEW-EREFP(IPERT))/EUNIT
                        CALL PRBIS(SVMIN,SVMAX,SVVAL,SVUNIT,'SECANT')
                      ENDIF
                      IF (ABS(ENEW-ENOW).LE.DTOL) CONVGE=.TRUE.
                    ENDIF
!  End of long IF block #3
C
                    ERED=ENEW*CM2RU
                    SVVAL=(ENEW-EREFP(IPERT))/EUNIT
                    IF (IPRINT.GE.8)
     1                  CALL PREABS(ENEW,EREFP(IPERT),EUNIT,SVUNIT)
                    IF (IPRINT.GE.6) CALL PRPRCT(NPROP,SVNAME,SVVAL,
     1                                           SVUNIT)

                    CALL BDCTRL(N,MXLAM,NHAM,EVBDST,VL, IV,
     2                          EINT,CENT,NODE,ERED,
     3                          EP2RU, CM2RU, RSCALE, EIGMIN,.FALSE.,
     4                          IMIN, IPRINT)

                    IF (IPRINT.GE.7) CALL PREVAL(NODE,EIGMIN)
                    NPROP=NPROP+1
C
                    CALL GCLOCK(TITIME)
                    TTIME=TITIME-TIMLST
                    TIMLST=TITIME
C
                    CALL FLUSH(6)
                    EIGLST=EIGNOW
                    IF (CONVGE) EXIT
C
C  SET UP FOR NEXT CYCLE OF BRENT ALGORITHM
C  (CONTRAPOINT (XA,FA) AND CURRENT ITERATE (XB,FB) MUST BRACKET THE ROOT)
                    IF (.NOT.ZBRENT) THEN
                      ELST=ENOW
                      ENOW=ENEW
                      EIGNOW=EIGMIN
                    ELSE
                      IF (FA*EIGMIN.LT.0.D0) THEN
                        XB=ENEW
                        FB=EIGMIN
                      ELSE
                        XA=ENEW
                        FA=EIGMIN
                      ENDIF
                    ENDIF
                  ENDDO
!  End of long DO loop #10

                  IF (.NOT.CONVGE) THEN
                    IF (IPRINT.GE.1) WRITE(6,6500) ITER
 6500               FORMAT('  VWDB FAILS TO CONVERGE AFTER',I3,
     1                     ' ITERATIONS')
                    CYCLE
                  ENDIF
C
C  CHECK NODE COUNT
C  NOW BASED ON CURRENT NODE COUNT, NOT LAST
C
                  IF (EIGMIN.LE.0.D0) THEN
                    NSEEK=NODE
                  ELSE
                    NSEEK=NODE+1
                  ENDIF
C
                  IF (NSEEK.NE.NCHECK(IBDST))
     1              WRITE(6,6510) NSEEK,NCHECK(IBDST)
 6510             FORMAT(/'  *** WARNING. THE FOLLOWING EIGENVALUE ',
     1                   'DOES NOT HAVE THE SAME NODE COUNT (',I5,')'/
     2                   6X,'AS THE REFERENCE CALCULATION',' (',I5,')'/
     3                   6X,'EXPECTATION VALUES AND CONVERGENCE ',
     4                   'TESTS MAY BE IN ERROR.'/6X,'A DIFFERENT ',
     5                   'VALUE OF RMATCH OR A SMALLER DR MAY AVOID ',
     6                   'THIS ISSUE.')
C
                  EVAL(IBDST,ISTOR)=ENEW
                  ERELP=ENEW-EREFP(IPERT)
!  Start of long IF block #4
                  IF (ICONV.EQ.0) THEN
C
C  CALCULATE EXPECTATION VALUE AND STORE IN EXTRAV FOR WRITING TO IBDSUM
C  IF NOT REPLACED BY IMPROVED VALUE FROM RICHARDSON EXTRAPOLATION
C
                    EXPV=(ENEW-EVAL(IBDST,1))/(DELTAN*EP2CM)
                    EXTRAV(IBDST,IPERT)=EXPV
cINOLLS call save_res(expv)
C
C  LOGICAL TO INDICATE THAT THIS IPERT IS FOR AN EFV
                    LIPEFV=((NDGVL.GT.0 .AND. IPERTN.LT.0)
     1               .OR. (MAPEFV.GT.0 .AND. IPERTN.GE.MXLAM+MAPEFV
     1               .AND. IPERTN.LT.MXLAM+MAPEFV+NEFV))
     2               .AND. NPOWN.EQ.0
C
C  CALCULATE DERIVATIVES WITH RESPECT TO EFV
C  (ABSOLUTE AND RELATIVE TO THRESHOLD)
C
                    IF (LIPEFV) THEN
                      DEDBAB=(ENEW-EVAL(IBDST,1))/(DELTAN*EUNIT)
                      DEDBAT=(EREFP(IPERT)-EREF)/(DELTAN*EUNIT)
                      DEDBRL=DEDBAB-DEDBAT
                      EXTRAV(IBDST,IPERT)=DEDBRL
                    ENDIF
C
!  Start of long IF block #5
                    IF (IPRINT.GE.1) THEN
!  Start of long IF block #6
                      IF (LIPEFV) THEN
C  IPERTN INDICATES AN EXTERNAL FIELD
                        IF (NDGVL.GT.0) THEN
                          I=-IPERTN
                        ELSE
                          I=IPERTN-MXLAM-MAPEFV+1
                        ENDIF
                        WRITE(6,7005) ERELP/EUNIT,TRIM(SVUNIT),NSEEK,
     1                                -DEDBAB,
     2                                TRIM(SVUNIT),TRIM(EFVUNT(I)),
     3                                -DEDBRL,
     4                                TRIM(SVUNIT),TRIM(EFVUNT(I))
 7005                   FORMAT(/1P,'  FINITE DIFFERENCE CALCULATION ',
     1                         'CONVERGED AT ',21X,'ENERGY = ',G17.10,
     2                         1X,A//'  FOR NODE',I5,',',7X,
     3                         'MU = -DE/DFIELD = ',
     4                         G13.6,1X,A,'/',A,' (ABSOLUTE)'/
     5                         '  DELTA MU = MU(THRESHOLD) - MU(BOUND)',
     6                         ' = ',G13.6,1X,A,'/',A,
     7                         ' (RELATIVE TO THRESHOLD)')
                        IF (FACTOR(IPERT).NE.1.D0) THEN
                          WRITE(6,7006) FACTOR(IPERT),
     1                                TRIM(SVUNIT),TRIM(EFVUNT(I)),
     2                                -DEDBRL/FACTOR(IPERT)
 7006                     FORMAT(/1P,'  IF MU =',G13.6,1X,A,'/',A,
     1                         ' (RELATIVE TO THRESHOLD) FOR THE BARE',
     2                         ' BOUND STATE,'/'  BARE BOUND-STATE',
     3                         ' FRACTION',11X,' = ',G13.6)
                        ENDIF
                      ELSE
C  IPERTN DOES NOT INDICATE AN EXTERNAL FIELD
                        WRITE(6,7010) ERELP/EUNIT,TRIM(SVUNIT),IPERTN,
     1                                NPOWN,NSEEK,EXPV
 7010                   FORMAT(/1P,'  FINITE DIFFERENCE CALCULATION ',
     1                         'CONVERGED AT ',21X,'ENERGY = ',G17.10,
     2                         1X,A/'  CALCULATED EXPECTATION VALUE ',
     3                         '<POT(',I3,')/R**',I2,'> FOR NODE',I5,
     4                         ' IS ',G13.6)
                        IF (FACTOR(IPERT).NE.1.D0) THEN
                          FEXPV=FACTOR(IPERT)*EXPV
                          WRITE(6,7020) FACTOR(IPERT),FEXPV
 7020                     FORMAT('  MULTIPLIED BY FACTOR ',1PE12.5,22X,
     1                           ' GIVES ',G13.6)
                        ENDIF
                      ENDIF
!  End of long IF block #6
                    ENDIF
!  End of long IF block #5
                  ELSEIF (ICON.EQ.1) THEN
C
C  ARRIVE HERE FOR DR CONVERGENCE TESTING
C  USE RICHARDSON EXTRAPOLATION.
C  FIRST SECTION BELOW DEALS WITH ENERGIES
C
!  Start of long IF block #7
                    IF (IPERT.EQ.0) THEN
                      IF (IPROPS.EQ.8) THEN
                        IRICH=IMGSEL
                      ELSE
                        IRICH=4
                      ENDIF
                      RDELTA=ENEW-EVAL(IBDST,1)
                      RFAC=FAC**IRICH-1.D0
                      EXTRA=EVAL(IBDST,1)-RDELTA/RFAC
                      SVEXT=(EXTRA-EREFP(IPERT))/EUNIT
                      IF (IPRINT.GE.1) THEN
                        IF (DRL.NE.unset) THEN
                          WRITE(6,8010) ERELP/EUNIT,TRIM(SVUNIT),DRS,DRL
                        ELSE
                          WRITE(6,8010) ERELP/EUNIT,TRIM(SVUNIT),DRS
                        ENDIF
                        WRITE(6,8020) IBDST,IRICH,IPROPS,SVEXT,
     1                                TRIM(SVUNIT)
 8010                   FORMAT(/'  ENERGY CONVERGED: E = ',1PG17.10,1X,
     1                         A,' FOR DR = ',G10.3:,' AND ',G10.3)
 8020                   FORMAT(/'  FOR BOUND STATE',I4,', RICHARDSON',
     1                         ' EXTRAPOLATION BASED ON ERROR',
     2                         ' PROPORTIONAL TO DR**',I1,
     3                         ' FOR IPROPS = ',I2/
     4                         '  GIVES EXTRAPOLATED ENERGY =',46X,
     5                         G17.10,1X,A)
                        CALL PREABS(EXTRA,EREFP(IPERT),EUNIT,SVUNIT)

                      ENDIF
                      IF (ICONV.EQ.1) THEN
                        EXTRAP(IBDST)=EXTRA
                        IF (IPRINT.GE.1) WRITE(6,8030)
 8030                   FORMAT('  THIS IS THE BEST AVAILABLE',
     1                         ' EXTRAPOLATED VALUE FROM THIS SET OF',
     2                         ' CALCULATIONS.')
                      ELSE
                        DIF=EXTRA-EXTRAP(IBDST)
                        IF (IPRINT.GE.1) WRITE(6,8040) DIF
 8040                   FORMAT('  THIS DIFFERS FROM FIRST EXTRAPOLATED',
     1                         ' VALUE BY ',1PG12.4/'  SUGGESTING',
     2                         ' THAT THE FIRST VALUE COULD BE',
     3                         ' UNCONVERGED BY UP TO THIS AMOUNT.')
                      ENDIF
                    ELSE
C
C  ARRIVE HERE IF ICON=1 AND IPERT>0:
C  RICHARDSON EXTRAPOLATION FOR EXPECTATION VALUES
C
                      EXPV=(ENEW-EVAL(IBDST,ICONV+1))/
     1                     (DELTAN*EP2CM)
                      JREF=1+IPERT*(NCONV+1)
                      EXP0=(EVAL(IBDST,JREF)-EVAL(IBDST,1))/
     1                     (DELTAN*EP2CM)
                      IRICH=IMGSEL !4
                      RDELTA=EXPV-EXP0
                      RFAC=FAC**IRICH-1.D0
                      EXTRA=EXP0-RDELTA/RFAC
C  SCALING FOR ABSOLUTE ENERGY DERIVATIVE WITH RESPECT TO EFV
                      IF (LIPEFV) THEN
                        EXPV =EXPV /EP2CM/EUNIT
                        EXTRA=EXTRA/EP2CM/EUNIT
                      ENDIF
                      IF (IPRINT.GE.1) THEN
                        WRITE(6,8110) IBDST,IRICH,IPROPS
                        IF (DRL.NE.unset) THEN
                          WRITE(6,8120) IPERTN,EXPV,DRS,DRL,EXTRA
                        ELSE
                          WRITE(6,8130) IPERTN,EXPV,DRS,EXTRA
                        ENDIF
                        IF (LIPEFV) WRITE(6,8135) EXTRA-DEDBAT
 8110                   FORMAT(/'  FOR BOUND STATE ',I4,
     1                         ', RICHARDSON EXTRAPOLATION BASED ON',
     2                         ' ERROR PROPORTIONAL TO DR**',I1,
     3                         ' FOR IPROPS = ',I2)
 8120                   FORMAT('  AND <POT(',I2,')> = ',1PG13.6,
     1                         ' FOR DR = ',G10.3,' AND ',G10.3,
     2                         ' GIVES EXTRAPOLATED VALUE = ',G13.6)
 8130                   FORMAT('  AND <POT(',I2,')> = ',1PG13.6,
     1                         ' FOR DR = ',G10.3,
     2                         ' GIVES EXTRAPOLATED VALUE = ',G13.6)
 8135                   FORMAT(50X,'RELATIVE TO THRESHOLD, ',
     1                         'EXTRAPOLATED VALUE = ',G13.6)
                        IF (FACTOR(IPERT).NE.1.D0) THEN
                          FEXPV=FACTOR(IPERT)*EXTRA
                          WRITE(6,7020) FACTOR(IPERT),FEXPV
                        ENDIF
                      ENDIF
                      IF (ICONV.EQ.1) THEN
                        EXTRAV(IBDST,IPERT)=EXTRA
                        IF (LIPEFV) EXTRAV(IBDST,IPERT)=EXTRA-DEDBAT
                        IF (IPRINT.GE.1) WRITE(6,8030)
                      ELSE
                        DIF=EXTRA-EXTRAV(IBDST,IPERT)
                        IF (IPRINT.GE.1) WRITE(6,8040) DIF
                      ENDIF
                    ENDIF
!  End of long IF block #7
                  ELSE
C
C  ARRIVE HERE IF ICONV.NE.0 AND ICON.NE.1
C  NO SENSIBLE EXTRAPOLATION IS POSSIBLE:
C  JUST CALCULATE DIFFERENCES
C
!  Start of long IF block #8
                    IF (IPERT.EQ.0) THEN
                      RDELTA=ENEW-EVAL(IBDST,1)
                      IF (IPRINT.GE.1)
     1                  WRITE(6,9010) IBDST,RMNINT,RMXINT,
     2                                (ENEW-EREFP(IPERT))/EUNIT,
     3                                EUNAME,RDELTA/EUNIT,EUNAME
 9010                 FORMAT('  FOR BOUND STATE',I4,', CALCULATION ',
     1                       'WITH RMIN = ',F6.3,' AND RMAX = ',1PG10.3,
     2                       ' GIVES E = ',G17.10,1X,A/'  THIS ',
     3                       'DIFFERS FROM REFERENCE VALUE BY ',
     4                       47X,G17.10,1X,A)
                    ELSE
                      EXPV=(ENEW-EVAL(IBDST,ICONV+1))/
     1                     (DELTAN*EP2CM)
                      JREF=1+IPERT*(NCONV+1)
                      EXP0=(EVAL(IBDST,JREF)-EVAL(IBDST,1))/
     1                     (DELTAN*EP2CM)
C  SCALING FOR ABSOLUTE ENERGY DERIVATIVE WITH RESPECT TO EFV
                      IF (LIPEFV) THEN
                        EXPV=EXPV/EP2CM/EUNIT
                        EXP0=EXP0/EP2CM/EUNIT
                      ENDIF
                      IF (IPRINT.GE.1) THEN
                        WRITE(6,9020) IBDST,RMNINT,RMXINT,IPERTN,
     1                                EXPV
 9020                   FORMAT('  FOR BOUND STATE',I4,', FINITE ',
     1                         'DIFFERENCE CALCULATION WITH RMIN = ',
     2                         F6.3,' AND RMAX = ',F10.3/
     3                         '  GIVES <POT(',I2,')> = ',43X,1PG13.6)
                        IF (LIPEFV) WRITE(6,9025) EXPV-DEDBAT
 9025                   FORMAT('  RELATIVE TO THRESHOLD =',38X,G13.6)
                        IF (FACTOR(IPERT).NE.1.D0) THEN
                          FEXPV=FACTOR(IPERT)*EXPV
                          WRITE(6,7020) FACTOR(IPERT),FEXPV
                        ENDIF
                        RDELTA=EXPV-EXP0
                        WRITE(6,9030) RDELTA
 9030                   FORMAT('  THIS DIFFERS FROM REFERENCE VALUE ',
     1                         'BY ',25X,1PG13.6)
                        IF (FACTOR(IPERT).NE.1.D0) THEN
                          FEXPV=FACTOR(IPERT)*RDELTA
                          WRITE(6,7020) FACTOR(IPERT),FEXPV
                        ENDIF
                      ENDIF
                    ENDIF
!  End of long IF block #8
                  ENDIF
!  End of long IF block #4
                  IF (IPRINT.GE.1) WRITE(6,9040)
 9040             FORMAT(/2X,59('- '))
C
                ENDDO
!  End of long DO loop #9
                ENDDO
!  End of long DO loop #8
C
C  END OF LOOP OVER CONVERGENCE TESTS AND EXTRAPOLATION
C  RESET ALL THE PROPAGATION PARAMETERS BACK TO THEIR STARTING VALUES
C
                DRL=DRLSTR
                DRS=DRSSTR
                RMNINT=RMNSTR
                RMXINT=RMXSTR
            ENDDO
!  End of long DO loop #7
            DEALLOCATE(EVBDST)
C
C  END OF PERTURBATION CALCULATIONS. WRITE SUMMARY OF BEST VALUES
C
            IF (NPERT.GT.0 .AND. IBDSUM.GT.0) THEN
              DO IBDST=1,NBDST
                WRITE(IBDSUM,9050) NCHECK(IBDST),
     1                            (EXTRAV(IBDST,IPERT),IPERT=1,NPERT)
 9050           FORMAT(I13,' EXP VALS:',(6G17.6))
              ENDDO
            ENDIF
C
C ============================================================================
C
C
          ENDDO LOOP_IFLD
!  End of long DO loop #3
          CALL RSTEFV(FIXFLD)
C
C  **********************  END OF LOOP OVER FIELDS  *********************
C
          DEALLOCATE (JSINDX,EINT,CENT,WVEC,L,VL,IV,DGVL)
          IF (LSCRU) CLOSE(ISCRU)
          IF (LSCRUR) CLOSE(ISCRUR)
        ENDDO
!  End of long DO loop #2
C
C  ******************  END OF LOOP OVER SYMMETRY BLOCKS  ****************
C
      ENDDO LOOP_JTOT
!  End of long DO loop #1
C
C  ********************  END OF LOOP OVER JTOT  ******************
C
C  END OF RUN BOOKKEEPING
C
      IF (WAVE) CLOSE(IWAVE)
C
      CALL GCLOCK(TLAST)
      TOTIME=TLAST-TFIRST
C
      IF (IPRINT.GE.1) THEN
        WRITE(6,'(/)')
        WRITE(6,1001)
        WRITE(6,1002)
        CALL PROGVS(PDATE)
        CALL TIMEMS(TOTIME)
        WRITE(6,1001)
      ENDIF

      IF (LASTIN.EQ.0) GOTO 10

  999 CONTINUE
C
cINOLLS call write_res
C
      RETURN
      END
