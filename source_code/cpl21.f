      SUBROUTINE CPL21(N,MXLAM,LAM,NSTATE,JSTATE,JSINDX,MVALUE,
     1                 VL,IPRINT,LFIRST)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
C
C  THIS SUBROUTINE CALCULATES THE COUPLING MATRIX FOR COUPLED STATES
C  LINEAR ROTOR-ATOM (ITYPE=21).
C  SEE (FOR EXAMPLE) EQN 30 OF MCGUIRE AND KOURI JCP (1974) 60 2488
C
C  30 DEC 93. INCORPORATES JM Hutson (V12) USE OF J3J000
C  ALL MV = 0,MAX(MVALUE) ARE CALCULATED EVEN IF LOWER ONES ARE
C  NOT NEEDED, E.G., BECAUSE OF MSET,MHI.
C
C  CRLS 10-05-22: SAVES COUPLING COEFFICIENTS USING ALLOCATABLE ARRAYS
C
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      SAVE NL12,IXMX,ISTART,IFIRST,CPL
C  SPECIFICATIONS FOR ARGUMENTS
      DIMENSION LAM(MXLAM),JSTATE(NSTATE),JSINDX(N),VL(*)
      INTEGER IPRINT
      LOGICAL LFIRST
      ALLOCATABLE CPLTMP(:),CPL(:)
C
      LOGICAL LODD
      PARAMETER (Z0=0.D0)
C
C  STATEMENT FUNCTION DEFINITIONS
      Z(I)=DBLE(I+I+1)
      LODD(I)=I-2*(I/2).NE.0
C
C  IF LFIRST IS TRUE (FIRST CALL), DO SOME INITIALIZATION
      IF (LFIRST) THEN
        IFIRST=-1
        LFIRST=.FALSE.
        IF (ALLOCATED(CPL)) DEALLOCATE (CPL)
      ENDIF
C
      XM=MVALUE
      PM=1.D0
      IF (LODD(MVALUE)) PM=-1.D0
C
      IF (IFIRST.GT.-1) GOTO 1000
C  FIRST TIME THROUGH SET UP SOME STORAGE POINTERS
      NL12=NSTATE*(NSTATE+1)/2
      IXMX=NL12*MXLAM
      ALLOCATE (CPL(IXMX))
C
 1000 MVABS=ABS(MVALUE)
C  SEE IF VALUES ARE STORED FOR THIS HIGH AN MVALUE
C  IF NOT, TRY TO STORE THEM IN CPL().
      IF (MVABS.LE.IFIRST) GOTO 2000

C  TEST FOR AVAILABLE STORAGE; NEED IXMX FOR THIS MVAL
      NCPL=SIZE(CPL)
      IF (NCPL/IXMX.LE.MVABS) THEN
        ALLOCATE (CPLTMP(NCPL))
        CPLTMP=CPL
        DEALLOCATE (CPL)
        ALLOCATE (CPL(IXMX*(MVABS+1)))
        CPL(1:NCPL)=CPLTMP
        DEALLOCATE (CPLTMP)
      ENDIF

!  Start of long DO loop #1
      DO MV=IFIRST+1,MVABS

        IX=MV*IXMX
C
C  THIS SECTION OF CODE CALCULATES THE COUPLING MATRIX ELEMENTS USING
C  THE SUBROUTINE J3J000 WHICH CALCULATES A WHOLE SET OF 3J
C  COEFFICIENTS (J1 J2 J3).  THE MATRIX ELEMENTS ARE STORED IN CPLTMP
C               ( 0  0  0)
C
        PMV=1.D0
        ALLOCATE (CPLTMP(2*MXLAM+1))
        IF (LODD(MV)) PMV=-1.D0
        DO IL=1,MXLAM
          LM=LAM(IL)
          JSAV=-1
          DO I1=1,NSTATE
            J1=JSTATE(I1)
            IF (J1.NE.JSAV) THEN
              CALL J3J000(DBLE(J1),DBLE(LM),IVALJ,CPLTMP,XJMIN)
              JMIN=ABS(J1-LM)
              JMAX=J1+LM
              JSAV=J1
            ENDIF
          DO I2=1,I1
            J2=JSTATE(I2)
            IX=IX+1
            IF (J2.LT.JMIN .OR. J2.GT.JMAX .OR. J1.LT.MV .OR.
     1          J2.LT.MV   .OR. LODD(J2+JMAX)) THEN
              CPL(IX)=0.D0
            ELSE
              INDJ=(J2-JMIN)/2+1
              IF (MV.EQ.0) THEN
                CPL(IX)=PMV*SQRT(Z(J1)*Z(J2))*CPLTMP(INDJ)**2
              ELSE
                CPL(IX)=PMV*SQRT(Z(J1)*Z(J2))*CPLTMP(INDJ)*
     1                  THRJ(DBLE(J1),DBLE(LM),DBLE(J2),
     2                       -DBLE(MV),0.D0,DBLE(MV))
              ENDIF
            ENDIF
          ENDDO
          ENDDO
        ENDDO
        DEALLOCATE (CPLTMP)
C
C  RESET IFIRST TO REFLECT HIGHEST M-VALUE STORED.
      ENDDO
!  End of long DO loop #1
      IFIRST=MVABS
C
C  THIS SECTION OF CODE TRANSFERS THE COUPLING MATRIX ELEMENTS FROM THE
C  ARRAY CPL WHERE THEY WERE PREVIOUSLY STORED INTO THE VL ARRAY, CHANGING
C  SIGNS AS NECESSARY.
C
 2000 IXM=MVABS*IXMX

      NZERO=0
!  Start of long DO loop #2
      DO LL=1,MXLAM
        NNZ=0
        I=LL
        LM=LAM(LL)
        XLM=LM
        DO ICOL=1,N
          I1=JSINDX(ICOL)
          JCOL=JSTATE(I1)
          XJCOL=JCOL
        DO IROW=1,ICOL
          I2=JSINDX(IROW)
          JROW=JSTATE(I2)
          XJROW=JROW
C         IF (NOMEM) THEN
C
C  THIS SECTION OF CODE CALCULATES COUPLING MATRIX ELEMENTS USING
C  FUNCTION THREEJ WHICH PRODUCES A SINGLE 3J COEFFICIENT (J1 J2 J3)
C                                                         ( 0  0  0).
C           VL(I)=PM*SQRT(Z(JROW)*Z(JCOL))*THREEJ(JROW,LM,JCOL)*
C    &            THRJ(XJROW,XLM,XJCOL,-XM,Z0,XM)
C         ELSE
            IF (I1.GT.I2) THEN
              IX12=I1*(I1-1)/2+I2
            ELSE
              IX12=I2*(I2-1)/2+I1
            ENDIF
            IX=IXM+(LL-1)*NL12+IX12
            VL(I)=CPL(IX)
C  WE HAVE STORED COUPLING FOR POSITIVE MVALUES; CORRECT IF NECESSARY
            IF (MVALUE.LT.0 .AND. LODD(JCOL+JROW+LM)) VL(I)=-VL(I)
C         ENDIF
          IF (VL(I).NE.0.D0) NNZ=NNZ+1
          I=I+MXLAM
        ENDDO
        ENDDO
        IF (NNZ.LE.0) THEN
          NZERO=NZERO+1
          IF (IPRINT.GE.14) WRITE(6,612) MVALUE,LL
        ENDIF
  612   FORMAT('  * * * NOTE.  FOR MVALUE =',I4,',  ALL COUPLING '
     1         'COEFFICIENTS ARE 0.0 FOR EXPANSION TERM',I4)
      ENDDO
!  End of long DO loop #2


      IF (NZERO.GT.0 .AND. IPRINT.GE.10 .AND. IPRINT.LT.14)
     1  WRITE(6,620) MVALUE,NZERO
  620 FORMAT('  * * * NOTE.  FOR MVALUE =',I4,',  ALL COUPLING ',
     1       'COEFFICIENTS ARE 0.0 FOR',I5,' POTENTIAL EXPANSION TERMS')

      RETURN
      END
