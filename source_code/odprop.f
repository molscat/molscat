      SUBROUTINE ODPROP(MXLAM,NHAM,
     1                  Y,VL,IV,EINT,CENT,
     3                  RSTART,RSTOP,NSTEP,DR,NODES,
     4                  ERED,EP2RU,CM2RU,RSCALE,IPRINT)
C  Copyright (C) 2022 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential, ONLY: CONLEN, NCONST, NRSQ, VCONST
C  VERSION (1/27/93) USES /MEMORY/ ..,IVLFL, ONLY TO CHECK USE OF
C  IV ARRAY.  BETTER CODE IN LOOPS 130, 230 POSSIBLE
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C
C  ROUTINE TO SOLVE THE SINGLE CHANNEL PROBLEM USING A
C  MODIFIED LOG-DERIVATIVE ALGORITHM. THE POTENTIAL
C  EVALUATED AT THE MIDPOINT OF EACH SECTOR IS USED AS A
C  REFERENCE POTENTIAL FOR THE SECTOR.
C
C  THIS VERSION IS WRITTEN TO VECTORISE AS MUCH AS POSSIBLE
C
C  COMMON BLOCK FOR CONTROL OF USE OF PROPAGATION SCRATCH FILE
      LOGICAL IREAD,IWRITE,IREADR,IWRITR
      COMMON /PRPSCR/ ESHIFT,ISCRU,ISCRUR,IREAD,IWRITE,IREADR,IWRITR

      ALLOCATABLE :: U(:),W(:),Q(:),Y1(:),Y2(:),P(:,:)
      DIMENSION IDUM1(1),VL(NHAM),IV(NHAM),EINT(*),CENT(*)
C
      COMMON /VLFLAG/ IVLFL
C
      IF (IVLFL.LT.0) THEN
        WRITE(6,*) ' ERROR. IVLFL.LT.0 NOT IMPLEMENTED IN ODPROP'
        STOP
      ENDIF
C
      NODES=0
C
C  THIS VERSION USES A CONSTANT STEP SIZE, DR, THROUGHOUT THE
C  PROPAGATION RANGE, BUT IS WRITTEN SO THAT THIS MAY BE EASILY
C  CHANGED (THOUGH VECTORISATION WOULD REQUIRE EXPLICIT R ARRAYS).
      NPT=NSTEP+1
      DR6=DR/6.D0
      H=DR/2.D0
      EP2CM=EP2RU/CM2RU
C
      ALLOCATE (Q(NPT),U(NPT),W(NPT))
      IF (IREAD) GOTO 400
      NPELS=MAX(NHAM,MXLAM+NCONST+NRSQ)
      ALLOCATE (P(NPELS,NPT))
C
C  FIRST GET POTENTIAL U AT EVEN-NUMBERED POINTS
C
      R=RSTART
      DO I=1,NPT
        CALL POTENL(0,MXLAM,IDUM1,R*RSCALE,P(1,I),IDUM2,IPRINT)
        DO J=1,NCONST
          P(MXLAM+J,I)=VCONST(J)/EP2CM
        ENDDO
        CALL SCAPOT(P(1,I),MXLAM)
        CALL PERTRB(R*RSCALE,P(1,I),NPELS,0)
        DO J=1,MXLAM+NCONST
          P(J,I)=EP2RU*P(J,I)
        ENDDO
        R=R+DR
      ENDDO

C  COMPUTE THE RADIAL CONTRIBUTION TO W
C
      EINTEP=0.D0
      IF (NCONST.EQ.0) EINTEP=EINT(1)

      DO I=1,NPT
        U(I)=EINTEP
      ENDDO

      DO J=1,NHAM
        V=VL(J)
        IF (V.EQ.0.D0) CYCLE
        IF (IVLFL.NE.0) THEN
          IVJ=IV(J)
        ELSE
          IVJ=J
        ENDIF
        DO I=1,NPT
          U(I)=U(I)+V*P(IVJ,I)
        ENDDO
      ENDDO
C
C  THE NEXT 2 LOOPS DO NOT VECTORISE BECAUSE OF THE FACTORS OF R.
C  THEY COULD BE MADE TO BY EXTENDING THE P ARRAY
C  BUT NRSQ>0 AND CONLEN>0.D0 ARE UNCOMMON CASES SO NOT DONE.
C
      IF (NRSQ.EQ.0) THEN
        R=RSTART
        DO I=1,NPT
          RSQ=MIN(1.D0/(R*R),1.D16)
          U(I)=U(I)+CENT(1)*RSQ
          R=R+DR
        ENDDO
      ENDIF

      IF (CONLEN.GT.0.D0) THEN
        R=RSTART
        DO I=1,NPT
          RSQ=R*R/CONLEN**4
          U(I)=U(I)+RSQ
          R=R+DR
        ENDDO
      ENDIF

      UBEG=U(1)
C
C  NOW GET POTENTIAL W AT ODD-NUMBERED POINTS
C
      R=RSTART+H
      DO I=1,NSTEP
        CALL POTENL(0,MXLAM,IDUM1,R*RSCALE,P(1,I),IDUM2,IPRINT)
        DO J=1,NCONST
          P(MXLAM+J,I)=VCONST(J)/EP2CM
        ENDDO
        CALL SCAPOT(P(1,I),MXLAM)
        CALL PERTRB(R*RSCALE,P(1,I),NPELS,0)
        DO J=1,MXLAM+NCONST
          P(J,I)=EP2RU*P(J,I)
        ENDDO
        R=R+DR
      ENDDO

      DO I=1,NSTEP
        W(I)=EINTEP
      ENDDO

      DO J=1,NHAM
        V=VL(J)
        IF (V.EQ.0.D0) CYCLE
        IF (IVLFL.NE.0) THEN
          IVJ=IV(J)
        ELSE
          IVJ=J
        ENDIF
        DO I=1,NPT
          W(I)=W(I)+V*P(IVJ,I)
        ENDDO
      ENDDO

      IF (NRSQ.EQ.0) THEN
        R=RSTART+H
        DO I=1,NSTEP
          RSQ=MIN(1.D0/(R*R),1.D16)
          W(I)=W(I)+CENT(1)*RSQ
          R=R+DR
        ENDDO
      ENDIF

      IF (CONLEN.GT.0.D0) THEN
        R=RSTART+H
        DO I=1,NSTEP
          RSQ=R*R/CONLEN**4
          U(I)=U(I)+RSQ
          R=R+DR
        ENDDO
      ENDIF
C
C  FORM VECTOR OF CORRECTIONS U
C
      Q(1)=0.D0
      DO I=2,NPT
        Q(I)=U(I)-W(I-1)
      ENDDO
      QLAST=Q(NPT)*DR6
      DO I=1,NSTEP
        U(I)=(U(I)-W(I)+Q(I))*DR6
      ENDDO
      DEALLOCATE(P)
      IF (IWRITE) WRITE(ISCRU) CENT(1),QLAST,W,U
      GOTO 500
C
  400 READ(ISCRU) CSAV,QLAST,W,U
C
C  CORRECT THE CENTRIFUGAL TERM IF DIFFERENT FROM THAT SAVED
C
C  02-08-18 CRLS: NOT SURE WHAT TO DO WITH CORRECTIONS TO
C                 CENTRIFUGAL TERM IF NRSQ>0, SO...
      IF (NRSQ.GT.0) THEN
        WRITE(6,*) ' *** CODING WORK NEEDED IN ODPROP'
        STOP
      ENDIF
C
      DC=CENT(1)-CSAV
      IF (ABS(DC).LT.1.D-8) GOTO 500
      R=RSTART+H
      DO I=1,NSTEP
        Q(I)=DC/(R*R)
        W(I)=W(I)+Q(I)
        R=R+DR
      ENDDO
      R=RSTART
      U(1)=U(1)+DC/(R*R)-Q(1)
      R=R+DR
      DO I=2,NSTEP
        U(I)=U(I)+((DC+DC)/(R*R)-Q(I)-Q(I-1))*DR6
        R=R+DR
      ENDDO
      QLAST=QLAST+(DC/(R*R)-Q(NSTEP))*DR6
C
C  NOW GET PROPAGATORS.
C  THIS LOOP REQUIRES SPECIAL TREATMENT TO VECTORISE ON CRAY
C
  500 ALLOCATE (Y1(NSTEP),Y2(NSTEP))
      DO I=1,NSTEP
        WREF=W(I)-ERED
        FLAM=0.5D0*SQRT(ABS(WREF))
        IF (WREF.LT.0.D0) THEN
          TN=TAN(FLAM*DR)
          Y1(I)=FLAM/TN-FLAM*TN
          Y2(I)=FLAM/TN+FLAM*TN
        ELSE
          TN=TANH(FLAM*DR)
          Y1(I)=FLAM/TN+FLAM*TN
          Y2(I)=FLAM/TN-FLAM*TN
        ENDIF
        Y2(I)=Y2(I)*Y2(I)
        Q(I)=Y1(I)+U(I)
      ENDDO
C
C  FINALLY DO THE PROPAGATION. THIS LOOP IS NOT VECTORISABLE,
C  SO THE WORK IN IT IS KEPT TO AN ABSOLUTE MINIMUM.
C
      DO I=1,NSTEP
        Y=Y1(I)-Y2(I)/(Y+Q(I))
      ENDDO
C
      DEALLOCATE(Y1,Y2,Q,U,W)
      Y=Y+QLAST
      RETURN
      END
