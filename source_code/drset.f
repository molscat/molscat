      SUBROUTINE DRSET(RSTART,RSTOP,STEP,TOLHI,CAY,DRSEG,NSTEP,DR,
     1                 unset,IPROP,POW)
C  Copyright (C) 2019 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3

C  CR Le Sueur Dec 2018
C  THIS ROUTINE SETS AN INTERIM VALUE FOR DR (DRFIT) FROM EITHER
C  1) A CALCULATION INVOLVING THE WAVELENGTH (K)
C  2) THE VALUE SET IN THE NAMELIST AS EITHER DRS OR DRL (AS APPROPRIATE)
C  3) THE VALUE USED IN THE PREVIOUS SEGMENT
C
C  IT THEN CALCULATES A NUMBER OF STEPS THAT DIVIDES THE INTERVAL INTO
C  STEPS OF THIS SIZE (OR BEGINNING WITH A STEP OF THIS SIZE IF POWR/=-1)
C
C  IT THEN ADJUSTS THE VALUE OF DR SO THAT IT EXACTLY DIVIDES UP THE
C  INTERVAL (EXCEPT FOR VIVS PROPAGATION AND AIRY PROPAGATION WITH 0<TOLHI<1,
C  IN WHICH CASES THE INTERVAL SIZE IS ADJUSTED ACCORDING TO A PERTURBATION
C  THEORY CALCULATION) AND RETURNS THIS VALUE IN DR
      IMPLICIT NONE

      DOUBLE PRECISION, INTENT(IN)    :: RSTART,RSTOP,STEP,TOLHI,CAY,
     1                                   DRSEG,unset,POW
      INTEGER,          INTENT(IN)    :: IPROP
      INTEGER,          INTENT(INOUT) :: NSTEP
      DOUBLE PRECISION, INTENT(INOUT) :: DR

      DOUBLE PRECISION DRFIT,PRANGE,ZSTART,ZSTOP,ZRATIO,ZRM1,
     1                 RRATIO,DRCALC,RSMALL,RLARGE,POWINT

      RSMALL=MIN(RSTART,RSTOP)
      RLARGE=MAX(RSTART,RSTOP)
      POWINT=1.D0-POW
      PRANGE=RLARGE-RSMALL
C
C  OBTAIN STARTING VALUE FOR STEP SIZE
C
      IF (IPROP.EQ.4) THEN
        IF (DRSEG.NE.unset) DR=DRSEG
        RETURN

      ELSEIF (STEP.GT.0.D0) THEN
C  STEP SIZE IS CALCULATED FROM STEP AND K
        DRFIT=ACOS(-1.D0)/(CAY*STEP)

      ELSEIF (DRSEG.NE.unset) THEN
C  STEP SIZE HAS BEEN SET IN DRS/DRL
        DRFIT=ABS(DRSEG)
C  NB THIS ASSUMES THAT DRSEG=unset MEANS OBTAIN FROM PREVIOUS SEGMENT

      ELSE
C  STEP SIZE IS INHERITED FROM PREVIOUS SEGMENT
        DRFIT=ABS(DR)
      ENDIF

C
C  ADJUST STEP SIZE SO THAT RANGE IS EQUAL TO A WHOLE NUMBER OF STEPS
C  NB THIS ASSUMES THAT TOLHI AND POW ARE SET TO 0 AND 0 IN INITIALISATION
C  IF PROPAGATOR ONLY USES A FIXED STEP SIZE.
C
      IF (TOLHI.EQ.0.D0) THEN
        RRATIO=DRFIT/RSMALL
        IF (POW.EQ.0.D0) THEN
          NSTEP=MAX(NINT(PRANGE/DRFIT),1)
          DR=PRANGE/DBLE(NSTEP)
C
C  NSTEP IS CALCULATED ASSUMING THAT DRFIT SPECIFIES THE SIZE OF THE
C  INNERMOST STEP BUT DR IS CALCULATED AS THE SIZE OF THE FIRST STEP
C
        ELSEIF (POW.EQ.1.D0) THEN
          NSTEP=NINT(LOG(RLARGE/RSMALL)/LOG(RRATIO+1.D0))
          DR=DRCALC(RSTART,RSTOP,0,NSTEP,POW)
        ELSE
          ZRATIO=(RLARGE/RSMALL)**POWINT
          ZRM1=ZRATIO-1.D0
          NSTEP=NINT(ZRM1/((RRATIO+1.D0)**POWINT-1.D0))
          DR=DRCALC(RSTART,RSTOP,0,NSTEP,POW)
        ENDIF
      ELSEIF (TOLHI.LT.1.D0) THEN
        DR=DRFIT
      ENDIF

C
C  CHANGE SIGN OF DR IF PROPAGATING INWARDS
      IF (RSTOP.LT.RSTART) DR=-ABS(DR)

      RETURN
      END
