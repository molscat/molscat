      SUBROUTINE SET4(NSTATE,EFACT,EUNAME,IASYMU,IPRINT)
C  Copyright (C) 2025 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE basis_data, ONLY: ELEVEL, EMAX, JLEVEL, JMAX, JMIN, JSTEP,
     b                      NLEVEL, MXELVL, MXJLVL, ROTI
      USE pair_state, ONLY: ATAU, JSTATE
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
C  CRLS 06-2022: CONVERTED TO USE ALLOCATABLE ARRAYS, INCLUDING THE
C                JSTATE AND ATAU ARRAYS WHICH ARE NOW IN pair_state MODULE
C  CRLS 08-2022: REARRANGED SO THAT CODE COMMON TO SET4 AND SET6 IS IN
C                SUBROUTINES
C
C  THIS ROUTINE SETS UP BASIS FOR ITYPE=4.
C  LINEAR RIGID ROTOR + ASYMMETRIC RIGID ROTOR SCATTERING.
C  INITIAL ROUTINE WRITTEN BY T.R. PHILLIPS, GISS, AUGUST 1990
C    DERIVED FROM ROUTINES SET6 AND SET3.
C  EXTENSIVELY REVISED FOR MOLSCAT VERSION 12 BY TRP (JUL 93)
C  CURRENT CODE ENTIRELY REWRITTEN (VERSION 14) BY S GREEN (5 AUG 94)
C
C  IMPLEMENTS SEVERAL METHODS TO INPUT BASIS:
C  1) A,B,C .GT. 0 SPECIFIED; GENERATE ASYM TOP FNS VIA SET6C ROUTINE
C  2) A=B=C=0;                READ ASYM TOP FNS FROM IASYMU
C    a) NLEVEL>=0; READ NLEVEL LEVELS (ALL IF NLEVEL=0) FROM IASYMU
C    b) NLEVEL<0;  READ ALL LEVELS FROM IASYMU BUT FILTER ON
C                  JLEVEL(3*I-2)=J1,JLEVEL(3*I-1)=ITAU
C  EXPAND ASYMMETRIC ROTOR BASIS WITH LINEAR ROTOR BASIS:
C  -- FOR BOTH 1 & 2a, EXPAND WITH J2=J2MIN,J2MAX,J2STEP; LINEAR
C     ROTOR ENERGIES MUST BE CALCULABLE FROM BE; SCREEN WITH EMAX
C  2b) NLEVEL<0 SPECIFIED; EXPAND WITH J2 VALUES SPECIFIED IN JLEVEL(3*I)
C                          USE ENERGIES IN ELEVEL(I) IF NON-ZERO, OTHERWISE
C                          CALCULATE FROM ASYMMETRIC TOP LEVELS IN EASYMU
C                          AND ROTOR ENERGIES FROM BE; SCREEN WITH EMAX
C
      ALLOCATABLE JTEMP(:),ATEMP(:),EASYM(:)
C
      LOGICAL EIN,LJ2IN
      CHARACTER(8) EUNAME
C
C  DEFAULT UNIT IS STANDARD INPUT
      DATA IDU /5/
C
      A=ROTI(1)
      BE=ROTI(2)
      B=ROTI(3)
      ALPHAE=ROTI(4)
      C=ROTI(5)
      DE=ROTI(6)

      IF (A.GT.0.D0 .AND. B.GT.0.D0 .AND. C.GT.0.D0) THEN
C  GENERATE ASYMMETRIC TOP BASIS VIA SET6C
        CALL SET6C(NSTATE,.FALSE.,EFACT,EUNAME,IPRINT,IASYMU)
        ALLOCATE (EASYM(NSTATE))
        EASYM=ELEVEL(1:NSTATE)
        LJ2IN=.FALSE.
      ELSE
C
C  ASYMMETRIC TOP FUNCTIONS WILL BE INPUT FROM IASYMU
        LJ2IN=(NLEVEL.LT.0)
        ALLOCATE (EASYM(MXELVL))
        CALL RASYMU(NSTATE,IASYMU,EASYM,EFACT,EUNAME,IPRINT,.FALSE.)
      ENDIF
C
C  J6TO4 EXPANDS ITYPE=6 DATA FORMAT TO PRODUCE ITYPE=4 DATA FORMAT
C        EITHER WITH POSSIBLE J2 VALUES         (IF LJ2IN=.FALSE.)
C        OR     WITH J2 VALUES LISTED IN JLEVEL (IF LJ2IN=.TRUE.)
      CALL J6TO4(NSTATE,EASYM,LJ2IN,IPRINT)
      DEALLOCATE (EASYM)

      RETURN
C
      END
