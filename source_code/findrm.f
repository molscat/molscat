      SUBROUTINE FINDRM(N,RSTART,RTURN,VL,IV,ERED,EINT,CENT,
     1                  MXLAM,NHAM,EP2RU,CM2RU,RSCALE,IRMSET,
     2                  LTYP8,IPRINT)
C  Copyright (C) 2025 J. M. Hutson & C. R. Le Sueur
C  Distributed under the GNU General Public License, version 3
      USE potential, ONLY: NCONST, NRSQ
      IMPLICIT DOUBLE PRECISION (A-H,O-Z)
      COMMON /CNTROL/ CDRIVE
      CHARACTER(1) CDRIVE
C
C  SUBROUTINE TO FIND A SUITABLE STARTING POINT FOR PROPAGATION
C
C  FIND CLASSICAL TURNING POINT OF DIAGONAL POTENTIAL
C  IN LOWEST-LYING CHANNEL.
C  START FROM A GUESS BASED ON THE CENTRIFUGAL POTENTIAL
C
C  JMH APR 2023: SIMPLIFIED AND LARELY REWRITTEN
C  TO REMOVE SOME OF THE MORE OBSCURE BRANCHES.
C  THIS VERSION TRIES TO CONVERGE ON A TURNING POINT UP TO TWICE,
C  FIRST FROM A CENTRIFUGAL TURNING POINT AND THEN FROM THE INPUT RSTART
C  IF BOTH FAIL, IT RETURNS RSTART AS THE INPUT VALUE
C
      DIMENSION VL(*),IV(*),EINT(N),CENT(N)
      ALLOCATABLE :: DIAG(:),DIAG2(:),P(:),W(:,:)
      LOGICAL LTYP8
C
      ALLOCATE (DIAG(N),DIAG2(N),W(N,N),P(MXLAM+NCONST+NRSQ))
      IF (LTYP8 .OR. CDRIVE.NE.'M') THEN
C
C  FOR BOUND AND FIELD, OR SURFACE SCATTERING (WITH NO CENTRIFUGAL
C  TERMS) START CONVERGENCE DIRECTLY AT INPUT RSTART AND SET ITRY=1
C  TO PREVENT A SECOND ATTEMPT IDENTICAL TO THE FIRST
C
        RTURN=RSTART
        ITRY=1
        GOTO 10
      ELSE
C
C  FOR MOLSCAT, UNLESS SURFACE SCATTERING, FIND THE TURNING POINTS OF 
C  PURE CENTRIFUGAL POTLS IN OPEN CHANNELS AND PICK THE SMALLEST
C  THIS PART OPERATES IN THE ASYMPTOTIC BASIS WITHOUT DIAGONALISATION
C
        RMIN=RSTART
        RTURN=1.D30
        NOPEN=0
        DO I=1,N
          DIF=ERED-EINT(I)
          IF (DIF.LT.0.D0) CYCLE
          NOPEN=NOPEN+1
          RCENT=SQRT(CENT(I)/DIF)
          RCENT=MAX(RCENT,RMIN)
          RTURN=MIN(RTURN,RCENT)
        ENDDO
        ITRY=0
        IF(RTURN.EQ.RMIN) ITRY=1
      ENDIF
C
C
C  SECANT CONVERGENCE ON TURNING POINT
C  FIRST ATTEMPT IS INTENDED BUT NOT GUARANTEED TO FIND
C  OUTER TURNING POINT OF CENTRIFUGAL BARRIER IF THERE IS ONE
C  IF THAT FAILS, SECOND TRY USUALLY STARTS AT RMIN
C  ALTHOUGH THERE ARE OTHER MORE COMPLICATED ROUTES
C
   10 ITRY=ITRY+1
      RSTART=RTURN
      IF (ITRY.GT.25) GOTO 140

      CALL HAMMAT(W,N,RSTART,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1            RSCALE,DIAG,MXLAM,NHAM,IPRINT)
      CALL DIAGVL(W,N,N,DIAG)
C
      RTURN=0.999D0*RSTART

C  SECANT LOOP
!  Start of long DO loop #1
      DO II=1,100
        CALL HAMMAT(W,N,RTURN,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1              RSCALE,DIAG2,MXLAM,NHAM,IPRINT)
        CALL DIAGVL(W,N,N,DIAG2)
C
C  CALCULATE POTENTIAL DERIVATIVE FOR USE LATER
C
        V2=DIAG2(1)
        DV1=(DIAG2(1)-DIAG(1))/(RTURN-RSTART)
        DIAG(1)=DIAG2(1)
C
        IF (IPRINT.GE.13) WRITE(6,601) RTURN,V2,DV1
  601   FORMAT('  FINDRM: AT R =',F10.3,' REDUCED V-E IN LOWEST',
     1         ' ADIABATIC CHANNEL IS',1PG11.3,' WITH DV/DR =',1PG11.3)
C
C  IF GRADIENT IS POSITIVE, PROBABLY CONVERGING ON INNER EDGE OF
C  CENTRIFUGAL BARRIER. IF THIS TRY BASED ON CENTRIFUGAL POTENTIAL,
C  TRY AGAIN FROM START. OTHERWISE, CONVERGENCE FAILED. 
C
        IF (DV1.GE.0.D0) THEN
          IF (IPRINT.GE.3) WRITE(6,602) DV1,RTURN
  602     FORMAT('  FINDRM. DV/DR IS POSITIVE:',G12.5,' AT R =',F10.3)
          IF (ITRY.EQ.1) THEN
            IF (IPRINT.GE.3) WRITE(6,603)
  603       FORMAT(10X,'TRY CONVERGENCE AGAIN FROM INPUT RSTART')
            RTURN=RMIN
            GOTO 10
          ELSE
            GOTO 999
          ENDIF
        ENDIF

        RSTART=RTURN
        V1=V2
        DR=-V1/DV1
C  MODERATE NEGATIVE STEP IF VERY BIG
        IF (DR.LT.-0.3D0*RTURN .AND. .NOT.LTYP8) DR=-0.3D0*RTURN
        RTURN=RTURN+DR
        IF (DR.GT.1.D3) EXIT
        IF (RTURN.LE.0.D0 .AND. .NOT.LTYP8) EXIT

        IF (ABS(DR/RTURN).LE.1.D-3) GOTO 160

      ENDDO
!  End of long DO loop #1
C
C  ARRIVE HERE IF DR BECOMES HUGE, RTURN BECOMES NEGATIVE,
C  OR THERE IS NO CONVERGENCE IN 100 NEWTON-RAPHSON ITERATIONS.
C  IF THIS HAPPENS, JUST USE THE INPUT VALUE OF RMIN
C
  140 IF (IPRINT.GE.3) WRITE(6,*) ' *** FINDRM. UNABLE TO FIND ',
     1                            'CLASSICAL TURNING POINT BY SECANT'
      IF (IPRINT.GE.3) WRITE(6,*) '     START INTEGRATION AT INPUT RMIN'
      RSTART=RMIN
      DEALLOCATE (DIAG,DIAG2,W,P)
      RETURN
C
C  ARRIVE HERE IF WE HAVE CONVERGED ON A CLASSICAL TURNING POINT
C  DIAG ARRAY CONTAINS DIAGONAL ELEMENTS
C
  160 IF (IPRINT.GE.9) WRITE(6,604) RTURN
  604 FORMAT('  INNER CLASSICAL TURNING POINT AT R =',F10.3)
C
C  SPECIAL CASE: CALLED TO FIND RTURN ONLY.
C  RETURN IT BUT SET STARTING POINT TO RMIN
C
      IF (IRMSET.LE.0) THEN
        RSTART=RMIN
        DEALLOCATE (DIAG,DIAG2,W,P)
        RETURN
      ENDIF
C
      IF (DV1.GE.0.D0) THEN
        IF (IPRINT.GE.3) WRITE(6,*) ' *** FINDRM. DV/DR SHOULD NOT',
     1     ' BE POSITIVE HERE. STOPPING. R =',RTURN,', DV/DR =',DV1
        STOP
      ENDIF

C  FIND NEW RSTART BY INTEGRATING PHASE INTEGRAL INWARDS.
C  WE WANT RSTART SUCH THAT
C  INT(RSTART,RTURN) SQRT(E-V) DR = 2.303 * IRMSET
C  TRY TO DO IT IN NSTEP ROUGHLY EQUAL STEPS
C
      NSTEP=3+IRMSET/3
      TARGET=2.303D0*DBLE(IRMSET)
C
C  FOR LINEAR POTENTIAL, V = DV (R-RTURN) AND K = SQRT(V)
C  SO INTEGRAL OVER STEP OF LENGTH DR IS (2/3) DV DR**(3/2)
C  AIM FOR FIRST STEP DR TO ACCUMULATE PHASE INTEGRAL TARGET/NSTEP
C  JMH 31/03/23 ADDED **(2.D0/3.D0) AND CHANGED XK TO DV1 HERE
C
      DR=(1.5D0*TARGET/SQRT(ABS(DV1))/DBLE(NSTEP))**(2.D0/3.D0)
C
      IF (IPRINT.GE.13) WRITE(6,606) NSTEP,TARGET
  606 FORMAT('  USE',I3,' STEPS TO SEEK DISTANCE SUCH THAT ESTIMATED',
     1       ' PHASE INTEGRAL IS AT LEAST',F6.2,' IN EVERY CHANNEL')
      PHASE=0.D0
C
  220 CONTINUE
C
!  Start of long DO loop #2
      DO ISTEP=1,NSTEP
        RNEXT=RSTART-DR
        IF (RNEXT.LT.0.D0 .AND. .NOT.LTYP8) THEN
          RSTART=0.D0
          IF (IPRINT.GE.1) THEN
            WRITE(6,*) ' *** FINDRM. REACHED ORIGIN WHILE'//
     2                 ' ACCUMULATING PHASE INTEGRAL.'
            WRITE(6,*) '     PROPAGATION WILL START AT ORIGIN.'
          ENDIF
          DEALLOCATE (DIAG,DIAG2,W,P)
          RETURN
        ENDIF
C
        CALL HAMMAT(W,N,RNEXT,P,VL,IV,ERED,EINT,CENT,EP2RU,CM2RU,
     1              RSCALE,DIAG,MXLAM,NHAM,IPRINT)
        CALL DIAGVL(W,N,N,DIAG)
C
        DRNEXT=0.D0
        IF (DIAG(1).LE.0.D0) THEN
          IF (IPRINT.GE.3) THEN
            WRITE(6,*) ' FINDRM. INNER CLASSICALLY ALLOWED '//
     2                 'REGION ENCOUNTERED AT R =',RNEXT
            WRITE(6,*) '         WHILE INTEGRATING INWARDS FROM '//
     1                 'TURNING POINT, BEFORE PHASE INTEGRAL '//
     2                 'REACHES TARGET.'
          ENDIF
          IF (ITRY.EQ.1) THEN
            IF (IPRINT.GE.3) WRITE(6,603)
            RTURN=RMIN
            GOTO 10
          ELSE
            GOTO 999
          ENDIF
        ENDIF

        XK1=SQRT(DIAG(1))
        IF (ISTEP.EQ.1) THEN
C  USE TERM INCLUDING ZERO AT EDGE FOR FIRST STEP
          PHASE=(2.D0/3.D0)*XK1*DR
        ELSE
C  USE TRAPEZIUM RULE FOR SUBSEQUENT STEPS
          XKAVG=0.5D0*(XK1+XK2)
          PHASE=PHASE+DR*XKAVG
        ENDIF
        DRNEXT=MAX(DRNEXT,(TARGET-PHASE)/XK1)
        XK2=XK1
C
        RSTART=RNEXT
        IF (ISTEP.LT.NSTEP) DR=DRNEXT/DBLE(NSTEP-ISTEP)
C
        IF (IPRINT.GE.13) WRITE(6,608) ISTEP,RNEXT,DIAG(1),PHASE
  608   FORMAT('  FINDRM: STEP',I3,' TO R =',F10.3,', REDUCED ',
     1         ' V-E =',1PG11.2,', PHASE INTEGRAL =',0PF7.3)
C
        IF (DRNEXT.LE.0.D0) EXIT
C
C  IF THE STEP SIZE SEEMS EXCESSIVE, TRY ACCUMULATING THE
C  PHASE INTEGRAL MORE CAUTIOUSLY
C
        IF (ISTEP.LT.NSTEP .AND. .NOT.LTYP8 .AND.
     1      DR.GT.0.5D0*RSTART .AND. DR.GT.0.5D0*RMIN) THEN
          DR=0.02D0*RSTART
          GOTO 220

        ENDIF
C
      ENDDO
!  End of long DO loop #2
C
      IF (IPRINT.GE.9) WRITE(6,609) RSTART
  609 FORMAT('  RADIAL PROPAGATION WILL START AT R =',F10.3)
      DEALLOCATE (DIAG,DIAG2,W,P)
      RETURN
C
  999 RSTART=RMIN
      RTURN=2.D0*RMIN
      IF (IPRINT.GE.3) WRITE(6,610)
  610 FORMAT(2X,'*** FINDRM. CONVERGENCE ON TURNING POINT FAILED'/
     1       14X,'RSTART SET TO RMIN AND RTURN SET TO 2*RMIN')
      DEALLOCATE (DIAG,DIAG2,W,P)
      RETURN
      END
